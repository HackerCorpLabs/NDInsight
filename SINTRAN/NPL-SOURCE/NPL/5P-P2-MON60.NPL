026000   @DEV (S-S-L)5P-P2-MON60:NPL
026000

026000   %==========================================================================
026000   %
026000   %       ( 5 )    N 5 0 0 M   ( MON 60)
026000   %
026000   %==========================================================================
026000
026000   *"8N500
"026000   %==========================================================================
026000   %       ( 5 )    P R E D E C L A R A T I O N S
026000   %
026000   INTEGER 5SPASSTYPE=?
026000   INTEGER 5CSTCK=?
026000   INTEGER 5BCHFLAG=?
026000   INTEGER 5PASSTYPE=?
026000   INTEGER 5TTIFIELD=?
026000
026000   INTEGER ENDBUFFER=?
026000   INTEGER 5LREG=?
026000   INTEGER 5RPRONO=?
026000   INTEGER 55DBFLAG=?
026000   INTEGER SGBRKADR=?
026000
026000   SUBR FROMESC,5OKRET,EEILPAR,INZERET
026000   RBUS
026000
026000
026000   %========================================================
026000   % PREDECLARATIONS:
026000   %
026000   INTEGER ARRAY FUNCS=?
026000   INTEGER ARRAY 5IFUNCS=?
026000
026000
026000
026000          SYMBOL ENAUTHORISED=  25        % YOU ARE NOT AUTHORIZED TO DO THIS
026000   %%     SYMBOL EC174=        174        % ILLEGAL PARAMETER
026000   %%     SYMBOL N5TIMOUT=      2000      % N500 TIME-OUT
026000   %%     SYMBOL ILMICFUNC=     2001      % ILLEGAL MICRO FUNCTION
026000   %%     SYMBOL ILN5STATUS=    2002      % ILLEGAL STATUS IN MESSAGE TO N-500
026000   %%     SYMBOL N5DMAERROR=    2003      % NORD-500 DMA ERROR
026000          SYMBOL ILSTOP=        2004      % ILLEGAL STOP REASON
026000   %%     SYMBOL ILTRAP=        2005      % UNKNOWN TRAP
026000          SYMBOL SPCTRAP=       2006      % MOR/PF/PN/HE/ME/CP/MSR TRAP
026000          SYMBOL EILREG=        2007      % ILLEGAL REGISTER NUMBER
026000
026000          SYMBOL EILFUNC=       2011      % ILLEGAL FUNCTION CODE IN MON 60
026000          SYMBOL EILSEG=        2012      % ILLEGAL SEGMENT NUMBER IN LOAD
026000          SYMBOL EILFILN=       2013      % ILLEGAL FILE NUMBER IN LOAD
026000          SYMBOL EFATAL=        2014      % FATAL ERROR FROM PIT-0
026000          SYMBOL ESPRES=        2015      % N500 RESERVED FOR SPECIAL USE
026000          SYMBOL ENOPROC=       2016      % NO N500 PROCESS AVAILABLE
026000          SYMBOL ENODBUF=       2017      % NO BUFFER AVAILABLE FOR DATA TRANSFER
026000          SYMBOL EBIGBUF=       2020      % TOO BIG BYTE COUNT IN DATA TRANSFER
026000          SYMBOL ETMSHARED=     2021      % TOO MANY SHARED AREA
026000          SYMBOL ENORTC=        2022      % NO RT-COMMON DEFINED
026000          SYMBOL ESGFNCONT=     2023      % SHARED SEGMENT FIXED, BUT NOT CONTIGUOUSLY
026000          SYMBOL ESGWRADD=      2024      % SHARED SEGMENT FIXED IN WRONG ADDRESS
026000          SYMBOL ESHOUTSIDE=    2025      % SHARED AREA OUTSIDE N500 MEMORY
026000          SYMBOL EPSGBIG=       2026      % TOO BIG PROGRAM SEGMENT
026000          SYMBOL EDSGBIG=       2027      % TOO BIG DATA SEGMENT
026000          SYMBOL ENOPCOMMU=     2030      % NO PROCESS TO COMMUNICATE WITH
026000          SYMBOL ENOMEMORY=     2031      % NO MEMORY AVAILABLE FOR NORD 500
026000          SYMBOL ECSLOAD=       2032      % CONTROL STORE MUST BE LOADED
026000          SYMBOL EMDFCOM=       2033      % DEFINE-MEMORY-CONFIG. COMMAND IS REQUIRED
026000          SYMBOL EUSRON=        2034      % OTHER USER(S) ALREADY LOGGED ON N500
026000          SYMBOL ENSPRES=       2035      % N500 NOT RESERVED FOR SPECIAL USE
026000
026000
026000          SYMBOL SWADEF=        2040      % SWAP FILE ALREADY DEFINED
026000          SYMBOL NOCMASFILE=    2041      % SWAP FILE IS NOT CONTIGUOUS MASS STORAGE FILE
026000          SYMBOL SWFINUSE=      2042      % SWAP FILE IS IN USE
026000          SYMBOL SWFNFOUND=     2043      % SWAP FILE NOT FOUND
026000
026000          SYMBOL NSWFENTRY=     2045      % NO FREE SWAP FILE ENTRY
026000          SYMBOL NOMASSFILE=    2046      % NOT MASS STORAGE FILE
026000   %%     SYMBOL SWPFATAL=      2047      % FATAL ERROR FROM SWAPPER
026000          SYMBOL MEMNAVAILABLE= 2050      % MEMORY NOT AVAILABLE
026000          SYMBOL MICFATAL=      2051      % FATAL ERROR FROM MICRO-PROGRAM
026000          SYMBOL ENOCPU=        2052      % NO ND-500(0) CPU FOUND
026000          SYMBOL EZADNAVA=      2053      % N500 PAGE ZERO NOT AVAILABLE
026000          SYMBOL EIMDCONF=      2054      % ERROR IN MEMORY CONFIGURATION
026000          SYMBOL EHIUSED=       2055      % HISTOGRAM ALREADY IN USE
026000          SYMBOL EHNRESERVED=   2056      % HISTOGRAM NOT RESERVED BY YOU
026000          SYMBOL POWER=         2057      % POWER FAIL DETECTED IN NORD-500
026000   %%     SYMBOL N5IERROR=      2060      % NORD-500 INTERFACE ERROR
026000   %%     SYMBOL N5STOPPED=     2061      % NORD-500 STOPPED
026000          SYMBOL EPFDETECT=     2062      % POWER FAIL DETECTED IN LOADING CS.
026000          SYMBOL PFECSLOAD=     2063      % LOAD CS. AFTER POWER FAIL
026000          SYMBOL EPOWUP=        2064      % POWER UP
026000          SYMBOL EILSTYPE=      2065      % ILLEGAL LOGICAL SEGMENT TYPE IN PLACE
026000          SYMBOL ESWLOAD=       2066      % SWAPPER MUST BE PLACED
026000          SYMBOL EILPHSNO=      2067      % ILLEGAL PHYSICAL SEGMENT NUMBER
026000   %%     SYMBOL EPFINSWP=      2070      % PAGE FAULT IN SWAPPER
026000   %%     SYMBOL ESPTIMOUT=     2071      % TIMOUT, INPOSSIBLE TO TERMINATE N500
026000          SYMBOL EMPBREAK=      2072      % MICRO-PROGRAM BREAK REACHED
026000          SYMBOL ELOGNRESERVED= 2073      % LOGGING FACILITY NOT RESERVED BY YOU
026000          SYMBOL ELOGINUSE=     2074      % LOGGING FACILITY ALREADY RESERVED
026000          SYMBOL EN5BUFFR=      2075      % NO MEMORY AVAILABLE FOR N500 BUFFERS
026000
026000
026000          SYMBOL ENRACCESS=     2100      % NOT REQUIRED ACCESS TO THE SEGMENT
026000          SYMBOL ENIMPLEMENT=   2101      % FUNCTION NOT IMPLEMENTED
026000          SYMBOL ENAUSED=       2102      % NAME ALREADY USED
026000          SYMBOL EILOCS=        2103      % ERROR IN LOADING CONTROL STORE
026000          SYMBOL ETMFIXED=      2104      % TOO MANY FIXED MEMORY PARTS
026000          SYMBOL EDESWAP=       2105      % MASS STORAGE TRANSFER ERROR IN SWAPPING
026000          SYMBOL ETMS3FIXED=    2106      % TOO MANY SHARED SEGMENTS TO FIX
026000          SYMBOL EFUNRTP=       2136      % FUNCTION NOT ALLOWED FROM RT-PROGR.
026000   %%     SYMBOL EIRESPL=       2140      % ILLEGAL IN RESIDENT PLACE
026000   %%     SYMBOL ESCSTOP=       2142      % ESCAPE/BREAK TYPED
026000   %      SYMBOL ERCFIL=        2143      % NOT ALLOWED ON REMOTE OPENED FILE
026000
026000
026000   %      ERROR NUMBERS FOR ERRORS THAT PROGRAMS MAY GET
026000          SYMBOL E5NOPART=       1050      % NO SWAP-FILE PART AVAILABLE
026000          SYMBOL ENOSWSPACE=    1051      % SWAPPING SPACE NOT AVAILABLE
026000          SYMBOL NOPHSEG=       1052      % NO FREE PHYSICAL SEGMENT
026000          SYMBOL EROSEGMENT=    1053      % SEGMENT NOT MODIFYABLE
026000   %%     SYMBOL EILPROC=       1054      % ILLEGAL PROCESS NUMBER
026000   %%     SYMBOL SWDERR=        1055      % SWAP DEVICE ERROR
026000   %%     SYMBOL EPRMC=         1056      % PRIVILGED MONITOR CALL
026000          SYMBOL EILLSNO=       1057      % ILLEGAL LOGICAL SEGMENT NUMBER
026000          SYMBOL EPRNFOUND=     1060      % NO SUCH PROCESS
026000          SYMBOL EILADDR=       1061      % ILLEGAL ADDRESS
026000
026000
026000
026000   DISP 25; INTEGER S25; PSID
026000
026000   SYMBOL 5DFSIZE=50            % SIZE OF N500 DATAFIELD TO WRITE ONTO DATA SEG.
026000   SYMBOL COMAUTO=17            % COMMAND IN AUTO-MODE
026000   % SYMBOL BNSYVARIABLES=NSYVARIABLES+NSYVARIABLES              % REMOVED 880405 BTT
026000   SYMBOL NREGS=65              % NUMBER OF N500 REGISTERS
026000   SYMBOL PRSELSIZE=142         % PROCESS TABLE ELEMENT SIZE
026000   SYMBOL PHSELSIZE=61          % PHYSICAL SEGMENT TABLE ELEMENT SIZE
026000   SYMBOL 5PRSELSIZE=227        % N500 PROCESS TABLE ELEMENT SIZE
026000   SYMBOL 55SEGSIZE=44          % N500 SEGMENT TABLE SIZE
026000   SYMBOL DASEGSTART=166000     % START OF ND-500 DATASEGMENT
026000   SYMBOL DSEGSIZE=12000        % SIZE OF DATA SEGMENT
026000   %SYMBOL DSSYVARIABLES=176000+400-NSYVARIABLES % ST ADDR. OF "SYS-DEP"
026000                                               % REMOVED 880405 BTT
026000   SYMBOL FT500=DASEGSTART+2    % FLAG-WORD TO N500
026000   SYMBOL FF500=FT500+2         % FLAG-WORD FROM N500
026000   SYMBOL S500DF=FF500+2        % ADDRESS OF DATAFIELD-COPY ON DATA SEGMENT
026000   SYMBOL SBUFFER=S500DF+5DFSIZE % ADDR OF GENERAL DATA BUFFER
026000   SYMBOL 5COMPRIOR=            71        % ND-500 COMMUNICATION PRIORITY
026000   SYMBOL DFIX5DSPAGE=          177776    % ADDR OF "IOF-EXIT" ROUTINE TO EXECUTE WHEN FIXING THE SYS.MON'S STACK-WINDOW
026000   SYMBOL MXMPARTS=             16        % MAXIMUM MEMORY PARTS
026000
026000
026000   * 5LREG=DASEG
026000   * 5AREG=5LREG
026000   * FPT2E=40000+3              % ENTRY POINT AFTER STANDARD SEGMENT HEADER
026000   * SGBRK=DASEG+1
026000
026000
026000   %*******************************************************************************
026000   %
026000   %       ( 5 )    F U N C T I O N S  I N  M O N I T O R  C A L L  6 0
026000   %
026000   %*******************************************************************************
026000   SYMBOL RREG=                 0         % READ REGISTER
026000   SYMBOL WREG=                 1         % WRITE REGISTER
026000   SYMBOL PMREAD=               2         % READ PROGRAM MEMORY
026000   SYMBOL D5MREAD=              3         % READ DATA MEMORY
026000   SYMBOL PMWRITE=              4         % WRITE PROGRAM MEMORY
026000   SYMBOL DMWRITE=              5         % WRITE DATA MEMORY
026000   SYMBOL SEGLOAD=              6         % LOAD SEGMENT
026000   SYMBOL PLSWAPPER=            7         % PLACE SWAPPER
026000   SYMBOL RREGS=                10        % READ REGISTERS
026000   SYMBOL WREGS=                11        % WRITE REGISTERS
026000   SYMBOL PRSTART=              12        % START PROGRAM
026000   SYMBOL FILCON=               13        % CONNECT FILE
026000   SYMBOL FILCLO=               14        % CLOSE FILE
026000   SYMBOL N5RES=                15        % ALLOCATE NORD 500 PROCESS
026000   SYMBOL N5REL=                16        % RELEASE NORD 500 PROCESS
026000   SYMBOL FLIOP=                17        % LIST OPEN FILES
026000   SYMBOL TIMEUS=               20        % TIME USED
026000   SYMBOL WISON=                21        % WHO IS ON
026000   SYMBOL ERRFSET=              22        % SET ERRFLAG
026000   SYMBOL RCNTS=                23        % READ CONTROL STORE
026000   SYMBOL WCNTS=                24        % WRITE CONTROL STORE
026000   SYMBOL MICPSTART=            25        % MICRO PROGRAM START
026000   SYMBOL DMEXA=                26        % DATA MEMORY EXAMINE
026000   SYMBOL DMDEP=                27        % DATA MEMORY DEPOSIT
026000   SYMBOL PMEXA=                30        % PROGRAM MEMORY EXAMINE
026000   SYMBOL PMDEP=                31        % PROGRAM MEMORY DEPOSIT
026000   SYMBOL DAMR=                 32        % ABSOLUTE DATA MEMORY READ
026000   SYMBOL DAMW=                 33        % ABSOLUTE DATA MEMORY WRITE
026000   SYMBOL STOPMIC=              34        % STOP MICRO PROGRAM
026000   SYMBOL FMCLEAR=              35        % MASTER CLEAR
026000   SYMBOL ALLPSEG=              36        % ALLOCATE PROGRAM SEGMENT
026000   SYMBOL CSLOAD=               37        % LOAD CONTROL STORE
026000   SYMBOL MEMDEF=               40        % DEFINE MEMORY CONFIGURATION
026000   SYMBOL RSTATU=               41        % READ N500 AND N100 COMMUNICATION STATUS
026000   SYMBOL ABREL=                42        % ABORT CURRENT PROCESS AND ND-100 PROGR.
026000   SYMBOL N5SRS=                43        % RESERVE N500 FOR SPECIAL USE
026000   SYMBOL N5SRL=                44        % RELEASE N500 FROM SPECIAL USE
026000   SYMBOL SCPLOOP=              45        % SCOPE-LOOP
026000   SYMBOL DEFSWAP=              46        % DEFINE-SWAP-FILE
026000   SYMBOL DELSWAP=              47        % DELETE-SWAP-FILE
026000   SYMBOL TSTFUNC=              50        % TEST-FUNCTION
026000   SYMBOL RIREG=                51        % READ INTERFACE (COMMUNICATION) IODATUT REG.
026000   SYMBOL GIVPAGES=             52        % GIVE N500 SWAPPING PAGES
026000   SYMBOL TAKPAGES=             53        % TAKE N500 SWAPPING PAGES
026000   SYMBOL STSWAPPER=            54        % START SWAPPER PROCESS
026000   SYMBOL SPLACE=               55        % START PLACE
026000   SYMBOL EPLACE=               56        % END PLACE
026000   SYMBOL RMVERS=               57        % READ MICROPROGRAM VERSION
026000   SYMBOL LIMEM=                60        % LIST MEMORY CONFIGURATION
026000   SYMBOL MRESSPES=             61        % RESERVE N500 MEMORY FOR SPECIAL USE
026000   SYMBOL 5DEFHIST=             62        % DEFINE HISTOGRAM
026000   SYMBOL 5STAHIST=             63        % START HISTOGRAM
026000   SYMBOL 5STOHIST=             64        % STOP HISTOGRAM
026000   SYMBOL REAHIST=              65        % READ HISTOGRAM
026000   SYMBOL RELHIST=              66        % RELEASE HISTOGRAM
026000   SYMBOL SPRTE=                67        % SEARCH FOR PROCESS ENTRY ON NAME SEG
026000   SYMBOL GPRTE=                70        % READ PROCESS ENTRY FROM NAME SEG
026000   SYMBOL SSGTE=                71        % SEARCH FOR PH. SEG. ENTRY ON NAME SEG
026000   SYMBOL GSGTE=                72        % READ PH. SEG. ENTRY FROM NAME SEG
026000   SYMBOL RPHSG=                73        % READ PHYSICAL SEGMENT
026000   SYMBOL SPRNM=                74        % SET PROCESS NAME OF CURRENT PROCESS
026000   SYMBOL TSTUSER=              75        % TEST FOR USER SYSTEM
026000   SYMBOL TOSWP=                76        % SEND MESSAGE TO SWAPPER
026000   SYMBOL RMESSAGE=             77        % READ LAST MESSAGE
026000   SYMBOL 5RFLAG=               100       % READ FLAG-INFO FROM N500
026000   SYMBOL 5WFLAG=               101       % WRITE FLAG-INFO TO N500
026000   SYMBOL FORGET=               102       % RELEASE N500 SYSTEM FROM SINTRAN III
026000   SYMBOL RSYSP=                103       % READ SYSTEM PARAMETERS
026000   SYMBOL WSYSP=                104       % WRITE SYSTEM PARAMETERS
026000   SYMBOL S1PRIOR=              105       % SET PRIORITY OF BACKGROUND PROGRAM
026000   SYMBOL LINKTO=               106       % LINK TO PROCESS
026000   SYMBOL MICBRK=               107       % MICRO PROGRAM BREAK
026000   SYMBOL WPHSG=                110       % WRITE INTO PHYSICAL SEGMENT
026000   SYMBOL 5STALOG=              111       % START PROCESS LOG
026000   SYMBOL 5STOLOG=              112       % STOP LOGGING
026000   SYMBOL PRILOG=               113       % PRINT LOGG-INFO
026000   SYMBOL RELLOG=               114       % RELEASE LOGGING
026000   SYMBOL STLAPROC=             115       % START LOGG ALL ACTIVE PROCESSES
026000   SYMBOL XN5REL=               116       % LOGOUT OWN PROCESS
026000   SYMBOL PRSTOP=               117       % ABORT PROCESS
026000   SYMBOL SOUTFIL=              120       % SET PRINT DEVICE
026000   SYMBOL RSWPDATA=             121       % READ FROM SWAPPER'S DATA MEMORY
026000   SYMBOL LOGOFF=               122       % LOGG OFF PROCESS
026000   SYMBOL MRELSPES=             123       % RELEASE ND-500 MEMORY
026000   SYMBOL STAMLOG=              124       % RESERVE AND START MONCALL LOGG
026000   SYMBOL PRIMLOG=              125       % PRINT MONCALL LOGG
026000   SYMBOL STOMLOG=              126       % STOP AND RELEASE MONCALL LOGG
026000   SYMBOL DFSYDOM=              127       % DEFINE SYSTEM-DOMAIN
026000   SYMBOL SFSYDOM=              130       % SEARCH FOR SYSTEM-DOMAIN
026000   SYMBOL DLSYDOM=              131       % DELETE SYSTEM-DOMAIN
026000   SYMBOL LISYDOM=              132       % LIST "SYSTEM-DOMAINS"
026000   SYMBOL LI5EXQU=              133       % LIST ND-500 EXECUTION QUEUE
026000   SYMBOL PLDEB=                134       % PLACE DEBUGGER
026000   SYMBOL ABLOG=                135       % LOGG OFF PROCESS AND ABORT PROGRAM
026000   SYMBOL PRACTIVATE=           136       % ACTIVATE STOPPED PROCESS
026000   SYMBOL STENPLACE=            140       % START RESIDENT-PLACE
026000   SYMBOL S5BLSIZE=             141       % SET BLOCK SIZE OF A FILE
026000   SYMBOL S3CPNT=               142       % VALUE OF BYTE POINTER IN COMMAND BUFFER
026000   SYMBOL MO5RT=                143       % ACTIVATE ND-500 PROC. OR ND-100 PROG.
026000   SYMBOL CHACPU=               144       % CHANGE CPU
026000   SYMBOL SSTDOM=               145       % START STANDARD DOMAIN FROM COMMAND SEG.
026000   % FUNCTION CODE 146 IS USED IN DRIVER/SYSTEM MONITOR
026000   SYMBOL N5S3REL=              147       % ESCAPE TYPED WHEN MONITOR STARTED FROM S3.OPCOM.
026000   SYMBOL LI5TMQ=               150       % LIST ND-500 TIME-QUEUE
026000   SYMBOL 5DBUGSW=              154       % DEBUG SWAPPER <ON/OFF>
026000   %SYMBOL STSELFTST=           155       % START SAMSON SELFTEST
026000   %SYMBOL WRSYSINFO=           156       % NEW 'VERSION'
026000   %SYMBOL MONACCP=             157       % ACCP-INTERFACE
026000   %SYMBOL NSGLOAD=             160       % PLACE SEGMENT WITH NEW DOMAIN FORMAT
026000   %SYMBOL 5NDFSYDOM=           161       % DEFINE STANDARD DOMSIN WITH NEW DOMAIN FORMAT
026000   %SYMBOL INITRACE=            162       % TRACE MICROPROGRAM FUNCTION: INITIALIZE TRACE BUFFER
026000   %SYMBOL CLRTRACE=            163       % TRACE MICROPROGRAM FUNCTION: CLEAR TRACE BUFFER
026000   %SYMBOL ARMTRACE=            164       % TRACE MICROPROGRAM FUNCTION: ARM TRACE
026000   %SYMBOL DISARMTRACE=         165       % TRACE MICROPROGRAM FUNCTION: DISARM TRACE
026000   %SYMBOL DUMPTRACE=           166       % TRACE MICROPROGRAM FUNCTION: DUMP TRACE BUFFER
026000   %SYMBOL CLRADR=              167       % TRACE MICROPROGRAM FUNCTION: CLEAR TRACE ADDRESS
026000   %SYMBOL RCPUTYPE=            170       % READ CPU TYPE AND VERSION (CONTROL STORE IS NOT LOADED)
026000   %SYMBOL SCACHEMODE=          171       % SET CACHE MODE (SAMSON ONLY)
026000   %SYMBOL RSRF=                172       % READ HW SCRATCH REGISTERS (SAMSON ONLY)
026000   %SYMBOL SCPUSTATUS=          173       % SET CPU STATUS (AVAILABLE/UNAVAILABLE/AUTOMATIC/NON AUTOMATIC ALLOCATION)
026000   %SYMBOL SETDIT=              177       % SET CURRENT DOMAIN NUMBER
026000
026000   SYMBOL FUNCMAX=177                     % FREE FOR PATCHING FUNC.CODE 155-167
026000
026000
026000   %======================================================================
026000   %       ( 5 )    P A R A N T
026000   %
026000   % MON60 FUNCTION DESCRIPTION.
026000   % (1 BYTE FOR EACH FUNCTION; FUNCTION CODE IS INDEX IN PARANT)
026000   %
026000   %----------------------------------------------------------------------
026000   %  CHARACTERISTICS OF MON-60 FUNCTIONS  (BITS IN PARANT)
026000   %      BIT 0-2 ARE NUMBER OF PARAMETERS OF EACH FUNCTION
026000   SYMBOL RTPLEGAL=4            % COMMAND ALLOWED FROM RT-PROGRAMS
026000   SYMBOL BSPRES=5              % SYSTEM MUST BE RESERVED FOR SPECIAL USE
026000   SYMBOL COMPROT=6             % COMMAND ONLY ALLOWED
026000                                % WHEN SYSTEM RESERVED FOR SPECIAL USE BY YOU
026000                                % WHEN USER SYSTEM CALLS
026000                                % WHEN AUTO-COMMAND MODE
026000   SYMBOL COMSPEC=7             % FUNCTION LEGAL WITHOUT ANY PROCESS DESCR. RESERVED
026000   %----------------------------------------------------------------------
026000
026000   @ICR
026000   INTEGER ARRAY PARANT:=(
026000   % 0   1    2   3   4   5   6   7   0   1   2   3   4   5   6   7
026000     22\22,   24\24,  23\23,  35\171, 21\21,  23\25,  21\32,  20\1,   % 000 - 017
026010     11\12,    2\173,173\171, 22\22,  22\22, 124\123,170\170,170\173, % 020 - 037
026020    173\33,    0\121,120\152,131\131, 14\131,131\131,170\30,  31\31,  % 040 - 057
026030     31\132,  23\20,  20\21,  20\132, 23\133, 33\135, 31\10,  31\22,  % 060 - 077
026040     22\22,  130\131,131\34, 111\112,134\101,100\102,100\101, 20\121, % 100 - 117
026050     11\124, 121\130, 21\21,  21\111, 32\111, 10\1,   32\331,131\0,   % 120 - 137
026060    171\22,   11\321, 21\31,   0\0,    1\0,    0\0,  101\160, 21\173, % 140 - 157
026070     34\111, 124\120,120\120,122\122, 22\122, 23\324,  0\0,    0\021);% 160 - 177
026100   @CR;
026100
026100   %========================================================================
026100   %       ( 5 )    5 T O E R S
026100   %
026100   % SUBROUTINE TO CALL ERROR LOGGER ROUTINE 9FLER
026100   %
026100   % ENTRY:     A=MESSAGE SIZE IN NUMBER OF WORDS
026100   %            X=ADDRESS OF PARAMTER LIST
026100   % EXIT:      OK
026100   %
026100   SUBR 5TOERS
026100   INTEGER POINTER LREG
026101   INTEGER 9FLRMSG=?,9FLRSIZE=?
026101
026101   5TOERS: *IOF
026102         A=:9FLRSIZE; X=:9FLRMSG
026104         A:=L=:"LREG"
026106         CALL 9FLER
026107   INTEGER 9FLRMSG
026110   INTEGER 9FLRSIZE
026111         *ION
026112         GO LREG
026113   RBUS
026114
026114
026114
026114   %========================================================================
026114   %       ( 5 )    D O H O L D
026114   %
026114   % SUBROUTINE TO EXECUTE HOLD
026114   %
026114   % ENTRY:     L.S0=NUMBER OF TIME UNITS
026114   %            L.S1=TIME UNIT
026114   % EXIT:      OK
026114   %
026114   SUBR DOHOLD
026114   INTEGER 5TM,5TMUNIT
026116   INTEGER ARRAY PM104:=(5TM,5TMUNIT)
026120   INTEGER LREG,XREG
026122
026122   DOHOLD:  A:=L=:LREG; X=:XREG
026125            "A5PIT"=:D; CALL DALTON
026130            0=:5TM=:5TMUNIT; "PM104"; *MON 2HOLD
026134            LREG.S0=:5TM; X.S1=:5TMUNIT; "PM104"; *MON 2HOLD
026143            CALL ALTOFF
026144            X:=XREG; LREG+2=:L; EXIT
026151   RBUS
026155
026155
026155   %========================================================================
026155   %       ( 5 )    5 R E S W O R K A      5 R E L W O R K A
026155   %
026155   % SUBROUTINE TO RESERVE/RELEASE WORKING AREA ON SYSTEM SEGMENT IF
026155   % FOREGROUND PROGRAM
026155   %
026155   % ENTRY:     NONE
026155   % EXIT:      ERROR RETURN
026155   % EXIT AD1:  OK RETURN
026155   %
026155   SUBR 5RESWORKA,5RELWORKA
026155
026155   INTEGER POINTER 5RLREG
026156   5RESWORKA:
026156          IF BACKGROUND=0 THEN
026160             A:=L=:"5RLREG"
026162             XSEMS(7)
026164             A=:B
026165   NYTRY:    MLEV; *MCL PIE
026167             X:=RTREF; CALL BRESERVE
026171             IF A<0 THEN
026172                CALL FREXQU; CALL TOWQU; CALL ANTIJAMMER
026175                "STUPR"; *IRW MLEVB DP
026177                MLEV; *MST PIE; MST PID
026202                GO NYTRY
026203             FI; MLEV; *MST PIE
026205             "N500DF"=:B; GO 5RLREG
026210          FI; EXIT
026211
026211   5RELWORKA:
026211          IF BACKGROUND=0 THEN
026213             A:=L=:"5RLREG"
026215             XSEMS(7)
026217             A=:B
026220             IF X:=RTREF=RTRES THEN
026224                *IOF
026225                CALL BRELEASE
026226                *ION
026227             FI
026227             "N500DF"=:B; GO 5RLREG
026232          FI; EXIT
026233   RBUS
026245
026245
026245   %==============================================================================
026245   %       ( 5 )    I N 5 F U D M A
026245   %
026245   % LOCAL SUBROUTINE TO INITIALIZE THE FAST-UDMA-OPTION
026245   %
026245   SUBR IN5FUDMA
026245   INTEGER INZRTP,C5FUDMA
026247   INTEGER 5FUDLREG
026250   IN5FUDMA:
026250   *"8N500 8F5UD -8MTRA
"026250             A:=L=:5FUDLREG
026252             X:="9FPUD"; 5FUDMA-=:C5FUDMA
026256             DO WHILE X<<="9LPUD"-5RTSIZE AND C5FUDMA><0
026264                X.STATUS BZERO 5RTOFF=:X.STATUS
026267                "A5PIT"=:D; CALL DALTON            % SET 5PIT AS ALT.PIT
026272                X=:INZRTP; A:="INZPRT"; *MON 2RT   % START UDMA-TRANSFER PROGRAM
026275                CALL ALTOFF                        % SET DPIT AS ALT.PIT
026276                X+5RTSIZE; MIN C5FUDMA; 0/\0
026301             OD
026302             5FUDLREG=:L
026304   *"8N500 8F5UD -8MTRA
"026304           EXIT
026305   RBUS
026314
026314   @LIB FALSE
026314   %============================================================
026314   %       ( 5 )    R L M B P A G E S
026314   %
026314   % LOCAL SUBROUTINE TO LINK BUFFER PAGES TO FREELIST
026314   %
026314   % ENTRY:     B=N500DF
026314   %            A=FIRST PAGE IN LINKED LIST
026314   % EXIT:      ERROR RETURN
026314   % EXIT+1:    A=NUMBER OF PAGES RELEASED
026314   %
026314   SUBR RLMBPAGES
026314   INTEGER IZPAGLINK,CURSGSTAT,NPG
026317   INTEGER POINTER RLMBLREG
026320
026320   RLMBPAGES:
026320          IF A=0 THEN EXITA FI
026322          A=:T; MLEV; *MCL PIE                                %%%%
026325          A:=L=:"RLMBLREG"
026327          T SH 2=:IZPAGLINK; 0=:NPG                           %%%% USES 5PGLINK AS SAVE AREA
026332          X:=SEGSTART+5SEGSIZE; T:=SEGTBANK; *SGSTA@3 LDATX   %%%% GETS SEGMENT STATUS WORD OF SEGMENT NO.1
026336          A=:CURSGSTAT; X:=IZPAGLINK; T:=CORMBANK             %%%%
026341          7=:D                                                %%%%
026343          DO                                                  %%%% BUILDS PREVIOUS LINK
026343             A:=D; *PREVI@3 STATX                             %%%% PREVIOUS IN FIRST ELEMENT IS SET TO INDICATE SEGMENT NO.1
026345             CURSGSTAT; *PROTE@3 STATX                        %%%% PROTECT WORD IS SET TO SEGMENT STATUS OF SEGMENT NO.1
026347             A:=X SHZ -2=:D; CALL ENPCACHE; 0/\0              %%%%
026354             X=:D; *PAGLI@3 LDXTX                             %%%% D-REG POINTS TO LAST ELEMENT ON EXIT FROM LOOP
026356             MIN NPG; 0/\0                                    %%%%
026360          WHILE X><0                                          %%%% END OF PAGELINK?
026361          OD                                                  %%%%
026362          X:=SEGSTART+5SEGSIZE; T:=SEGTBANK; *BPAGL@3 LDXTX   %%%% GETS FIRST PAGE IN PAGELINK OF SEGMENT NO.1
026366          T:=CORMBANK; A:=D                                   %%%%
026370          IF X><0 THEN                                        %%%% IF PAGES ALREADY LINKED TO SEGMENT NO.1
026371            *PREVI@3 STATX                                    %%%%    SET FIRST PAGE IN EXISTING LIST TO POINT BACK
026372          FI                                                  %%%%    ON LAST PAGE IN LIST TO BE INSERTED
026372          A:=:X; *PAGLI@3 STATX                               %%%% LET LAST PAGE IN LIST TO BE INSERTED TO POINT
026374          IZPAGLINK; X:=SEGSTART+5SEGSIZE                     %%%%    PAGE IN EXISTING LIST
026377          T:=SEGTBANK; *BPAGL@3 STATX                         %%%% LET PAGELINK OF SEGMENT NO.1 POINT TO FIRST ELEMENT
026401          *SEGLI@3 LDATX                                      %%%%
026402          IF A=0 THEN                                         %%%% IF SEGMENT NO.1 IS NOT IN SEGMENT LINK
026403             "1INSEGLINK"; *IOF; IRW SLEVB DP                 %%%%    CALL SEGMENT ADM.ROUTINE TO INSERT IT
026406             SLEV; *MST PID; ION                              %%%%
026411          FI; MLEV; *MST PIE                                  %%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
026413          NPG; MIN "RLMBLREG"; GO RLMBLREG
026416   RBUS
026423
026423
026423
026423   %============================================================
026423   %       ( 5 )    R E L M B P A G E S
026423   %
026423   % LOCAL SUBROUTINE TO RELEASE BOTH ALL BUFFERS
026423   %
026423   % ENTRY:     B=N500DF
026423   % EXIT:      ERROR RETURN
026423   % EXIT+1:    OK RETURN
026423   %
026423   SUBR RELMBPAGES
026423
026423   INTEGER POINTER RLMBLREG
026424
026424   RELMBPAGES: A:=L=:"RLMBLREG"
026426          5FPMAILBOX; CALL RLMBPAGES; 0/\0
026431          A-+5NPAGES=:5NPAGES; 0=:5FPMAILBOX
026435          5FBUM60; CALL RLMBPAGES; 0/\0
026440          A-+5NPAGES=:5NPAGES; 0=:5FBUM60=:5BUBANK=:5BUSTART
026446          5FPACCPBUFF; CALL RLMBPAGES; 0/\0
026451          A-+5NPAGES=:5NPAGES; 0=:5FPACCPBUFF
026455          5FPHWBUFF; CALL RLMBPAGES; 0/\0
026460          A-+5NPAGES=:5NPAGES; 0=:5FPHWBUFF
026464          5FPFUDMA; CALL RLMBPAGES; 0/\0
026467          A-+5NPAGES=:5NPAGES; 0=:5FPFUDMA
026473          MIN "RLMBLREG"; GO RLMBLREG
026475   RBUS
026502
026502
026502   %============================================================
026502   %       ( 5 )    C H M E M D E F
026502   %
026502   % LOCAL SUBROUTINE TO SET UP MEMORY CONFIGURATION
026502   %
026502   % ENTRY:     B=N500DF
026502   % EXIT:      ERROR IN SETTING UP MEMORY CONFIGURATION
026502   % EXIT+1:    MEMORY CONFIGURATION SET UP
026502   %
026502   SUBR CHMEMDEF
026502
026502   INTEGER POINTER CHMLREG=?
026502
026502   CHMEMDEF:
026502          A:=L=:"CHMLREG"
026504          IF 5FUNCTION><MEMDEF THEN
026510             IF ADRZERO=-1 THEN
026514                IF 5FUNCTION=N5RES THEN
026520                   CALL 5RESWORKA
026521                   IF BACKGROUND=0 THEN T:="RTPWORKA" ELSE T:="WORKA" FI
026526                   "VERSION"=:D; 5=:L; *MOVNA
026533                   T:=5P2; X:=5; A=:D; OLDPAG; K:=1; CALL MOVUS
026541                   CALL 5RELWORKA
026542                ELSE IF A=N5REL THEN
026546                   GO 5OKRET
026547                FI FI
026547                EMDFCOM; GO CHMLREG
026551             FI
026551             EXITA
026552          FI; GO CHM1; *)FILL
026564
026564   INTEGER MPARTS               % NUMBER OF MEMORY PARTS
026565   INTEGER CINDX
026566   INTEGER ARRAY POINTER PAMEMTABLE
026567   DOUBLE  ARRAY POINTER DPWORKA
026570   INTEGER TYPADDR
026571   INTEGER CMSTART
026572   INTEGER MXADDR
026573   INTEGER POINTER CHMLREG
026574
026574   CHM1:  CALL XTUSON; GO CHM2
026576          CALL ESCOFF
026577          *IOF
026600          "RS5CPU"; *IRW LV12B DP
026602          LV12;     *MST PID
026604          *ION
026605          X:="S5CPUDF"
026606          DO WHILE X<<="E5CPUDF"
026611             A:=-1=:X.MAIL1LINK=:X.MAILINK
026614             X+5CPUDFSZ
026615          OD
026616          "5D11"+B=:D; X:=ZAREG; OLDPAGE; X+1=:B; T:=3; CALL GND5PAR
026627          "N500DF"=:B
026631          IF 5D22>MXMPARTS GO CHMEEILPAR
026635          A=:MPARTS
026636          CALL 5RESWORKA
026637          IF BACKGROUND=0 THEN T:="RTPWORKA" ELSE T:="WORKA" FI
026644          T=:"DPWORKA"
026645          5P3=:D; 5D22 SH 1=:X; OLDPAG; K:="0"; CALL MOVUS
026655          "AMEMTABLE"+B=:"PAMEMTABLE"; A+"TYPMTAB-AMEMTABLE"=:TYPADDR
026662          0=:CINDX=:CMSTART
026664          FOR CINDX DO WHILE CINDX<MPARTS SH 1
026671             A=:T SHZ -1 =:X; CMSTART=:PAMEMTABLE(X)
026676             X:=:T; DPWORKA(X); A+CMSTART=:CMSTART
026702             T=:X:=TYPADDR; A:=D; *SBYT
026706             MIN CINDX
026707          OD; X+1; CMSTART=:PAMEMTABLE(X)
026714          T:=TYPADDR; A:=0; *SBYT
026717          X+1
026720          FOR X TO MXMPARTS-1 DO
026724             0=:PAMEMTABLE(X); T:=TYPADDR; A:=0; *SBYT
026730          OD
026732          CALL 5RELWORKA
026733          5D12=:ADRZERO
026735          CCPUDF.5INITFLAG BZERO BMDEFOK=:X.5INITFLAG
026741          SYSINITFLAG BZERO BMDEFOK=:SYSINITFLAG
026744          CALL RELMBPAGES; 0/\0
026746          5MSINIT BZERO 5ALBUF BZERO 5INBUF=:5MSINIT
026752          MIN "CHMLREG"
026753   CHM2:  CALL ESCON
026754          GO CHMLREG
026755   CHMEEIPAR:
026755          CALL ESCON; GO EEILPAR
026757   RBUS
027004
027004
027004   %============================================================
027004   %            I N Z 5 0 0
027004   %
027004   % LOCAL SUBROUTINE TO INITIALIZE THE ND-500 SYSTEM.
027004   % CALLED WITH MLEV DISABLED AND RETURN WITH MLEV DISABLED, BUT
027004   % MLEV IS ENABLED IN THIS SUBROUTINE TO EXECUTE MONITOR CALLS.
027004   %
027004   % ENTRY:     B=N500DF
027004   % EXIT:      ERROR IN INITIALIZING ND-500
027004   % EXIT+1:    ND-500 INITIALIZED
027004   %
027004   SUBR INZ500,INZERET
027004
027004   INTEGER CNP=?
027004   INTEGER POINTER INZ5LREG=?
027004
027004   INZ500:
027004           A:=L=:"INZ5LREG"
027006           MLEV; *MST PIE
027010           IF 5MSINIT NBIT 5CHALIVE THEN
027013              CALL 5CONOMD                            % On return A-reg is number of Samson CPUs
027014              5MSINIT BONE 5CHALIVE=:5MSINIT
027017           FI
027017           IF N5CPU=0 THEN ENOCPU; GO FAR INZRET FI
027023           CALL CHMEMDEF; GO FAR INZRET
027025           IF 5MSINIT BIT 5ALBUF GO FAR INZ2
027030           IF NSAMSON><0 THEN
027032              MAXOCTBUF+1 SH -1 + MAXACCPBUFF+2000 SH -12
027040              A*NCPU; CALL 5GBUFF; GO FAR 0INZERET
027043              A=:5FPACCPBUF
027044              2*NCPU; CALL 5GBUFF; GO FAR 0INZERET
027050              A=:5FPHWBUF
027051           FI
027051           % - Allocate buffer for Nd500 message blocks
027051           X:=NCPU+1                                          % One cpu df per cpu + one global df
027053           MX5PROCS SH 1+177 SHZ -7; X+A+A                    %   + Comm. + Swap-wait fifo buffers
027061           2000=:D; A:=0; T:=55MESSIZE; *RDIV ST              % Number of messages per page
027066           A=:T; MX5PROCS+NCPU+X+5=:D; A:=0; *RDIV ST         % Number of pages for all messages
027076           IF D><0 THEN A+1 FI; A=:5NPMAILBOX                 %   + Mailboxes
027102           CALL 5GBUFF; GO FAR 0INZERET
027104           A=:5FPMAILBOX
027105           % - Allocated Mon60-buffers
027105           MX5PROCS+1=:5NPM60BUF
027110           CALL 5XGBUFF; GO FAR 0INZERET
027112           A=:5FBUM60=:D; A:=0; AD SH 12=:DBUSTART
027117   *"8N500 8F5UD
"027117           % - Allocate buffers for Fast UDMA
027117           MX5PROCS*"MFANT*MFSIZE+MFNCP"                      % COMPUTE REQUIRED MEMORY AREA FOR ND-500 FAST UDMA OPTION TABLES
027121           D:=0; AD SHZ -12; IF D><0 THEN A+1 FI              % A=NUMBER OF PAGES REQUIRED
027126           A=:CNP; CALL 5GBUFF; GO INZERET
027131           A=:5FPFUDMA                                        % SAVE FIRST PAGE REF. IN ALLOCATED PAGE LIST
027132           A=:D:=CNP+5NPAGES=:5NPAGES
027136           A:=0; AD SH 12=:D5FXBNK                            % ADDR OF FIX-INFO TABLE.
027141           MX5PROCS*"MFANT*MFSIZE"+D=:5DSPS                   % ADDR OF CAPABILITY TABLE.
027145   *"8N500
"027145           GO INZ1; *)FILL
027201
027201   INTEGER POINTER INZ5LREG
027202   INTEGER INZRTP,CNP
027204   INTEGER INZPRT:=INZRTP
027205
027205   INZ1:
027205           5MSINIT BONE 5ALBUF=:5MSINIT
027210           IF NPHSEGS>>5000 THEN                              % 5000B PHYS.SEGS ARE MAX FOR SYS.MON.
027214              IF MX5PROCS*12>>T THEN A:=T FI                  % DEFAULT MAX IS 10 SEGS PER PROC.
027221              A=:NPHSEGS
027222           FI
027222   INZ2:   IF 5MSINIT NBIT 5INBUF THEN
027225              CALL MSINIT; 5MSINIT BONE 5INBUF=:5MSINIT       % SETUP THE ND-500 MESSAGE BUFFERS
027231              "S5CPUDF"=:NCCPUDF                                 % Initialize "last CPU logged in on"
027233           FI
027233           IF 5MSINIT NBIT 5RTSTART THEN
027236              CALL IN5FUDMA                                   % INITIALIZE FAST-UDMA-OPTION
027237              "A5PIT"=:D; CALL DALTON
027242              "5SWAP"=:INZRTP; A:="INZPRT"; *MON 2RT
027246              CALL ALTOFF
027247              5MSINIT BONE 5RTSTART=:5MSINIT
027252           FI
027252           MIN "INZ5LREG"                                     % OK, SKIP RETURN
027253   INZRET: A=:T; MLEV; *MCL PIE
027256           A:=T; GO INZ5LREG
027260
027260   INZERET: CALL RELMBPAGES; 0/\0                             % RELEASE ALL MEMORY RESERVED FOR ND-500 BUFFERS
027262            5MSINIT BZERO 5ALBUF BZERO 5INBUF=:5MSINIT
027266   0INZERET: EN5BUFFR; GO INZRET
027270   RBUS
027306
027306   %=====================================================================
027306   %      ( 5 )     R E S C P U
027306   %
027306   % SUBROUTINE TO RESERVE CPU FOR SPECIAL USE
027306   % MUST BE CALLED IN IOF WITH GENERAL N5000 SEMAPHORE LOCKED!!!
027306   %
027306   % ENTRY:     B=N500DF
027306   %            X=CPU DF
027306   % EXIT:      ERROR
027306   % EXIT+1:    OK
027306   %
027306   SUBR RESCPU
027306
027306   RESCPU: X=:D
027307           RTREF=:D.SPREF
027312           X:=X.MAILINK; T:=5MBBANK; A:=0 BONE CPRES; *AAX X5CPF; STATX
027320           D.CPUNO; T:=5MBBANK; X:=5PRDESCR.MESSBUF
027325           *AAX 5CPUN; STATX; AAX 5MSFL-5CPUN; LDATX
027331           *5CPUB@3 BLDA DA; 5SCPU@3 BSTA DA
027333           A BONE 5CPUBOUND; *STATX
027335           EXITA
027336   RBUS
027340
027340
027340   %=====================================================================
027340   %      ( 5 )     R E L C P U
027340   %
027340   % SUBROUTINE TO RELEASE CPU FOR SPECIAL USE
027340   %
027340   % ENTRY:     B=N500DF
027340   %            X=CPU DF
027340   % EXIT:      OK
027340   %
027340   SUBR RELCPU
027340   RELCPU: 0=:X.SPREF; X=:D
027342           IF X.CPUAVAILABLE NBIT 5EXCLUDE THEN
027345              X:=X.MAILINK; T:=5MBBANK; *AAX X5CPF; LDATX
027351              A BZERO CPRES; *STATX
027353           FI
027353           *IOF
027354           T:=D.5INITFLAG BZERO BRESPLACE=:X.5INITFLAG
027360           T:=5MBBANK; X:=5PRDESCR.MESSBUF
027363           *AAX 5MSFL; LDATX; 5SCPU@3 BLDA DA; 5CPUB@3 BSTA DA
027367           A BZERO 5SCPUBOUND; *STATX
027371           *ION
027372           EXIT
027373   RBUS
027374
027374   %=====================================================================
027374   %      ( 5 )     R L H I L O G
027374   %
027374   % RELEASE HISTOGRAM LOG
027374   %
027374   % ENTRY:     NONE
027374   % EXIT:      OK
027374   %
027374   SUBR RLHILOG
027374   INTEGER POINTER RLHLREG
027375
027375   RLHILOG:
027375          A:=L=:"RLHLREG"
027377          *IOF
027400          0=:5HIFLAG                                       % YES, MARK THAT IT IS STOPPED
027401          CALL HIMFEXQUEUE; GO RLHI1                       % REMOVE HISTOGRAM MESSAGE FROM EX-QUEUE
027403          CALL SLOCK; 0/\0
027405          X:=HIMESS; CALL GCPUDF; GO RLHI1
027410          X:=A.MAILINK; T:=5MBBANK; *AAX WANTP; LDATX
027415          A-5SWPROC*5PRDSIZE+"S500S"
027420          X:=A.MESSBUFF; *AAX 5MSFL; LDATX
027424          *5SCPU@3 BLDA DA
027425          A BZERO 5CPUBOUND BZERO 5SCPUBOUND
027427          *5CPUB@3 BSTA DA; STATX
027431          CALL SUNLOCK
027432   RLHI1: *ION
027433          GO RLHLREG
027434   RBUS
027446
027446
027446   %=====================================================================
027446   %      ( 5 )     C H F O R G E T
027446   %
027446   %  SUBROUTINE TO CHECK AND WAIT UTILK ALL PROCESSES IS LOGGED OUT OF
027446   %  THE ND-500 SYSTEM.
027446   %
027446   % ENTRY:     NONE
027446   % EXIT:      OK
027446   %
027446   SUBR CFORGET
027446   INTEGER CHFPROCNO
027447   INTEGER POINTER LREG
027450
027450   CFORGET:  A:=L=:"LREG"
027452          DO
027452             5SWPROC+1=:CHFPROCNO; X:="S500S"+5PRDSIZE
027457             FOR CHFPROCNO DO WHILE CHFPROCNO<<=MX5PROCS
027463                IF X.RTRES><0 AND A><RTREF THEN                  % OTHER USING ND-500?
027470                   CALL DOHOLD(2,2); GO ECHF
027474                FI; X+5PRDSIZE
027475             OD
027477             GO LREG
027500   ECHF:  OD
027501   RBUS
027506
027506
027506   %=====================================================================
027506   %      ( 5 )     X T U S O N     T U S O N
027506   %  XTUSON: CHECK IF OTHER USERS ARE LOGGED ON THE 500 SYSTEM
027506   %  TUSON:  CHECK IF OTHER USERS ARE LOGGED ON THE ACTUAL 500 CPU
027506   %
027506   %          IF NONE ARE LOGGED ON THEN RESERVE THE ACTUAL ND-500 FOR
027506   %          SPECIAL USE FOR THE CALLING PROGRAM
027506   %
027506   % ENTRY:     B=N500DF
027506   % EXIT:      OTHER USERS ARE LOGGED ON
027506   % EXIT AD1:  XTUSON: THE ND-500 SYSTEM IS RESERVED FOR SPECIAL USE
027506   %            TUSON:  THE ACTUAL ND-500 IS RESERVED FOR SPECIAL USE
027506   %
027506   SUBR XTUSON,TUSON
027506   INTEGER TUSOPROCNO,TUSOFLG
027510   INTEGER POINTER LREG
027511
027511   XTUSON: A:=1; GO FELLS
027513   TUSON:  A:=0
027514   FELLS: A=:TUSOFLG; A:=L=:"LREG"
027517          *IOF
027520          CALL SLOCK; 0/\0
027522          5SWPROC+1=:TUSOPROCNO; X:="S500S"+5PRDSIZE
027527          FOR TUSOPROCNO DO WHILE TUSOPROCNO<<=MX5PROCS
027533             IF X.RTRES><0 AND A><RTREF THEN                  % OTHER USING ND-500?
027540                IF TUSOFLG><0 GO TUSERET
027542                X=:D
027543                X:=X.MESSBUF;  CALL GCPUDF; CALL ERRFATAL     % YES, USING THIS ND-500 CPU?
027546                IF A=CCPUDF GO TUSERET
027551                X:=D
027552             FI; X+5PRDSIZE
027553          OD
027555          IF TUSOFLG><0 THEN
027557             RTREF=:NSPREF
027561          ELSE
027562             X:=CCPUDF; CALL RESCPU; GO TUSERET
027565          FI
027565          MIN "LREG"
027566   TUSRET: CALL SUNLOCK
027567          *ION
027570          GO LREG
027571
027571   TUSERET:  EUSRON; GO TUSRET
027573   RBUS
027605
027605
027605   %===============================================================
027605   %       ( 5 )    F R U S M O V E   -   X F R U S M O V E
027605   %
027605   % LOCAL SUBROUTINE TO COPY WORDS FROM THE USER'S AREA
027605   % TO THE MON60 BUFFER. THE MON60 BUFFER IS SETUP IN THE DATA-BUFFER
027605   % WINDOW IN RT-DESCRIPTION AND THE LOGICAL ADDR IS FOUND IN LOGBADR
027605   % IN N500DF DATAFIELD.
027605   %
027605   % ENTRY:     T=NUMBER OF BYTES
027605   %            A=SOURCE ADDRESS
027605   % IF XFRUSMOVE: X=DISP IN DATA-BUFFER TO MOVE INTO
027605   %
027605   % EXIT:      ALL REGISTERS, EXCEPT B, ARE DESTROYED
027605   %
027605   SUBR FRUSMOVE,XFRUSMOVE
027605   INTEGER POINTER FRUSLREG
027606   XFRUSMOVE: T+1 SHZ -1:=:X                        % X=NUMBER OF BYTES
027611          A=:D:=LOGBADR; T+A                        % T=DESTINATION ADDR; D=SOURCE ADDR
027614          A:=L=:"FRUSLREG"
027616          GO FRUSFELLS
027617   FRUSMOVE: T+1 SHZ -1=:X                          % NUMBER OF WORDS TO COPY
027622          T:=L=:"FRUSLREG"
027624          T:=LOGBADR; A=:D                          % T=DESTINATION ADDR; D=SOURCE ADDR
027626   FRUSFELLS:
027626          A:=OLDPAGE; K:="0"; CALL MOVUS            % COPY FROM USER TO DPIT
027631          GO FRUSLREG
027632   RBUS
027633
027633
027633   %===========================================================================
027633   %       ( 5 )    T O U S M O V E
027633   %
027633   % LOCAL SUBROUTINE TO COPY WORDS TO THE USER'S AREA
027633   % FROM THE MON60 BUFFER. THE MON60 BUFFER IS SETUP IN THE DATA-BUFFER
027633   % WINDOW IN RT-DESCRIPTION AND THE LOGICAL ADDR IS FOUND IN LOGBADR
027633   % IN N500DF DATAFIELD.
027633   %
027633   % ENTRY:     A=NUMBER OF BYTES
027633   %            T=DESTINATION ADDRESS
027633   %
027633   SUBR TOUSMOVE
027633   INTEGER POINTER TOUSLREG
027634   TOUSMOVE: A+1 SHZ -1=:X                          % X=NUMBER OF WORDS
027637          A:=L=:"TOUSLREG"; LOGBADR=:D              % D=SOURCE ADDR
027643          A:=OLDPAGE; K:=1; CALL MOVUS              % COPY FROM DPIT TO USER'S ALT.PIT
027646          GO TOUSLREG
027647   RBUS
027650
027650
027650   %=======================================================================
027650   %       ( 5 )    S T D S 0
027650   %
027650   % LOCAL SUBROUTINE TO STORE A DOUBLE WORD IN USER'S AREA
027650   %
027650   % ENTRY:     AD=DOUBLE WORD TO STORE
027650   %            X=ADDRESS IN USER AREA
027650   %            B=N500DF
027650   %
027650   SUBR STDS0
027650   INTEGER POINTER STDSLREG
027651   STDS0: T:=L=:"STDSLREG"; X=:L
027654          A=:T:=OLDPAGE/\3600\/"N5PIT+ALEVB+ERNG2"; *IOF
027661          A=:RTREF.ACTPRI; *TRR PCR; ION
027665          A:=T; AD=:L.DS0
027670          "N5PIT+ADPIT+ALEVB+ERNG2"; *IOF; TRR PCR
027673          A=:RTREF.ACTPRI; *ION
027676          GO STDSLREG
027677   RBUS
027703
027703
027703   %=====================================================
027703   %       ( 5 )    G O O D R T
027703   %
027703   % LOCAL SUBROUTINE TO CHECK FOR LEGAL RT-DESCRIPTION ADDRESS
027703   % ENTRY:     X=RT-DESCRIPTION ADDRESS
027703   % EXIT:      ERROR
027703   % EXIT+1:    OK, LEGAL RT-DESCRIPTION ADDRESS
027703   %
027703   SUBR GOODRT
027703   GOODRT: IF X>>=RTEND OR X<<RTSTART GO NGOOD
027711           A:=X-RTSTART; A=:D:=0; T:=5RTSIZE; *RDIV ST
027717           IF D><0 GO NGOOD; EXITA
027722   NGOOD:  EXIT
027723   RBUS
027725
027725
027725   %============================================================================
027725   %       ( 5 )    H I M F E X Q U E U E
027725   %
027725   % LOCAL SUBROUTINE TO REMOVE THE HISTOGRAM MESSAGE FROM
027725   % THE ND-500 EX-QUEUE.
027725   %
027725   % ENTRY:     B=N500DF DATAFIELD
027725   %            INTERRUPT IS OFF
027725   %
027725   %            ALL REGISTERS EXCEPT B ARE DESTROYED
027725   %            INTERRUPT IS OFF
027725   % EXIT:      NOT IN EXECUTION QUEUE
027725   % EXIT+1:    REMOVED FROM EXECUTION QUEUE
027725   %
027725   %
027725
027725   SUBR HIMFEXQUEUE
027725   INTEGER POINTER HIMFLREG
027726   HIMFEXQUEUE:
027726          X:=HIMESS; T:=5MBBANK; *AAX 5MSFL; LDATX
027732          IF A BIT 5IEXQUEUE THEN                       % IS HISTOGRAM MESSAGE IN EX-QUEUE?
027734             A:=L=:"HIMFLREG"
027736             CALL SLOCK; 0/\0
027740             X:=HIMESS; CALL GCPUDF; CALL ERRFATAL
027743             A=:B
027744   *NNT40=*
027744             CALL TER500; 0/\0
027746             CALL IFM500XQ                              % REMOVE HIMESS FROM EX-QUEUE AND RESTART ND-500
027747             CALL SUNLOCK
027750             CALL LOWACT500
027751             "N500DF"=:B
027753             "HIMFLREG"+1=:L
027756          FI; EXIT
027757   RBUS
027771
027771
027771   %======================================================================
027771   %       ( 5 )    F R Q U E S
027771   %
027771   % LOCAL SUBROUTINE TO REMOVE A MESSAGE FROM THE EXECUTION QUEUE
027771   % AND FROM THE TIME QUEUE
027771   % ENTRY:     B=ND-500 CPU DATAFIELD
027771   %            X=ND-500 MESSAGE
027771   %            INTERRUPT IS OFF
027771   %
027771   % EXIT:      ALL REGISTER EXCEPT B ARE DESTROYED
027771   %            INTERRUPT IS OFF
027771   %
027771   SUBR FRQUES
027771   INTEGER POINTER FRQRET
027772   INTEGER BREG
027773
027773   FRQUES: A:=L=:"FRQRET"; A:=B=:BREG
027777          CALL SLOCK; 0/\0
030001          CALL GCPUDF; GO EFRQUE; A=:B
030004          T:=5MBBANK; *AAX 5MSFL; LDATX; AAX -5MSFL
030010          IF A BIT 5ITMQUEUE THEN
030012             CALL FR5TMQ                                % REMOVE MESSAGE FROM ND-500 TIME QUEUE
030013             A:=MFREE; CALL WN5STATUS
030015             CALL SUNLOCK
030016          ELSE IF A BIT 5IEXQUEUE THEN                  % IS MESSAGE IN ND-500 EX-QUEUE?
030021             CALL GETC5PROC; A=:D                       % CHECK IF IT IS NECESSARY TO TERMINATE ND-500(0)
030023             T:=5MBBANK; *SENDE@3 LDATX
030025             IF A=D OR MIFLAG NBIT MUDOM THEN
030032                CALL TER500; 0/\0
030034             FI
030034             CALL IFM500XQ                              % REMOVE MESSAGE FROM EX-QUEUE
030035             A:=MFREE; CALL WN5STATUS
030037             CALL SUNLOCK
030040             IF 5CPUSTOPPED><0 THEN
030042                N100KICK; CALL KICK500
030044                CALL LOWACT500
030045             FI
030045          ELSE
030046   EFRQUE:   CALL SUNLOCK
030047          FI FI
030047          T:=5MBBANK; X=:D; *AAX XADPR; LDXTX
030053          X.PSTAT BZERO 5REWA=:X.PSTAT; X:=D
030057          BREG=:B; GO FRQRET
030062   RBUS
030075
030075
030075   %=============================================================================
030075   %       ( 5 )    R E L R E F S
030075   %
030075   % LOCAL SUBROUTINE TO
030075   %      1. RELEASE THE ND-500 CPU FROM SPECIAL USE IF RESERVED FOR SPECIAL USE
030075   %         BY THE CALLING PROGRAM.
030075   %      2. RELEASE ALL LOGGING FACILITIES RESERVED BY THE CALLING PROGRAM
030075   %
030075   % ENTRY:     B=N500DF DATAFIELD
030075   %
030075   % EXIT:      ALL REGISTERS EXCEPT B ARE DESTROYED
030075   %
030075   SUBR RELREFS
030075   INTEGER RELRLREG
030076   RELREFS: *IOF
030077          A:=L=:RELRLREG
030101          IF NSPREF=RTREF THEN 0=:NSPREF FI
030106          IF CCPUDF.SPREF=RTREF THEN CALL RELCPU FI             % RELEASE ND-500 CPU RESERVED FOR SPECIAL USE
030114          IF 5HRTP=RTREF THEN CALL RLHILOG; 0=:5HRTP FI         % HISTOGRAM RESERVED BY THE CALLER?
030122          IF 5MLOG=RTREF THEN 0=:5MLOG=:5MLOPROC FI             % IF MONCALL LOG RESERVED BY THE CALLER THEN RELEASE IT
030130          RELRLREG=:L
030132          *ION
030133          EXIT
030134   RBUS
030142
030142
030142   %===============================================================
030142   %       ( 5 )    S M L V R O U T I N E
030142   %
030142   % START MONITOR LEVEL.
030142   % A=MONITOR ROUTINE ENTRY ADDR. INTERRUPT IS OFF
030142   % B=ND500DF DATAFIELD
030142   %
030142   SUBR SMLVROUTINE
030142   SMLVROUTINE:
030142          *IRW MLEVB DP
030143          X:=RTREF; CALL BRELEASE                % RELEASE N500DF DATAFIELD
030145          MLEV; *MST PID; MST PIE; ION           % START MONITOR LEVEL ROUTINE
030151          CALL ERRFATAL                          % SHOULD NOT CONTINUE HERE ON LEVEL 1
030152   RBUS
030155
030155
030155   %============================================================================
030155   %       ( 5 )    A L L R E L E A S E
030155   %
030155   % RELEASE ALL ND-500 SEMAPHORES
030155   %
030155   SUBR ALLRELEASE
030155   INTEGER RLRG,RRXRG,RTRG,RBRG,RSTS
030162   ALLRELEASE: MLEV; *MCL PIE
030164          A:=L=:RLRG; X=:RRXRG:=B=:RBRG
030171          CALL CLEAN
030172          FOR X:=0 TO 11 DO                                    % RELEASE ALL ND-500 LOCKS
030176             X=:RTRG:=XSEMS(X)
030200             IF X.RTRES=RTREF THEN X=:B:=A; CALL BRELEASE FI
030207             X:=RTRG
030210          OD
030212   ALLR1: RTREF.BRESLINK
030214          DO WHILE A><RTREF
030217             IF A>>="9SFIS" AND A<<"9EFIS" THEN               % RELEASE ALL FILESYS. LOCKS IF
030225   ALLR2:       X:=RTREF; A=:B; CALL BRELEASE; GO ALLR1       % IF RESERVED BY THE CALLER
030231             ELSE
030232                IF A>>="DEVBU" AND A<<ENDBU GO ALLR2          % RELEASE ALL DEVICE BUFFERS RESERVED
030240                IF A>>="9FDFD" AND A<<"9EDFD" GO ALLR2
030246                IF T:="MXSIBAS"><0 THEN
030251                   IF A>>="DSI0" AND A<<"EDSIBAS" GO ALLR2    % RELEASE ALL SIBAS MONCALL WORKING FIELDS RESERVED
030257                FI
030257             FI; A.RESLINK
030261          OD
030262          X:=RBRG=:B:=RRXRG
030265          *TRA STS; IOF
030267          A=:RSTS; CALL CLINONMSG
030271          T:=RLRG; X:=RRXRG
030273          MLEV; *MST PIE
030275          IF RSTS BIT 17 THEN
030300             *ION
030301          FI
030301          T=:P
030302   RBUS
030320
030320
030320   %==================================================================
030320   %       ( 5 )    S U P D W I N D O W   -   X S U P D W I N D O W
030320   %
030320   % LOCAL SUBROUTINE TO SETUP WINDOW TO MON60 BUFFER.
030320   %
030320   % ENTRY:     B=N500DF
030320   % IF XSUPDWINDOW: AD=PHYS. ADDR TO SET WINDOW TO
030320   %
030320   % EXIT:      BUFFER PAGE IN PIT, LOGICAL ADDR IN LOGBADR
030320   %            ALL REGISTERS EXCEPT B ARE DESTROYED
030320   SUBR SUPDWINDOW,XSUPDWINDOW
030320   XSUPDWINDOW:
030320          T:=1777/\D; GO INSUPWINDOW
030323   SUPDWINDOW:
030323          T:=5MBBANK; X:=5PRDESCR.MESSBUFF; *AAX ABUFA; LDDTX  % AD=PHYS ADDR OF MON60 BUFFER
030330          T:=0
030331   INSUPWINDOW:
030331          AD SHZ 6; A=:CURPROG.BUFWINDOW                       % PHYS.PAGE OF MON60 BUFFER TO RT-DESCR
030334          A=:D:="WNDBF*2000"+T=:LOGBADR
030340          X:="WNDBF+WNDBF+174000"; T:=0; A:=142000; *STDTX     % SET PIT ENTRY
030344          EXIT
030345   RBUS
030353
030353
030353   %==============================================================================
030353   %       ( 5 )    C L I N O N M S G
030353   %
030353   % SUBROUTINE TO CLEAR ALL IN5MSG/ON5MSG IN ALL TERMINAL/TAD DATAFIELD
030353   % RESERVED BY THE CALLING PROCESS
030353   %
030353   % ENTRY:     INTERRUPT MUST BE OFF
030353   %
030353   SUBR CLINONMSG
030353   INTEGER POINTER CLIOLREG
030354   CLINONMSG: A:=L=:"CLIOLREG"
030356          RTREF+"BRESLINK"=:X
030361          DO WHILE X:=X.RESLINK><RTREF
030365             IF X.TYPRING BIT 5TERM THEN
030370                A:=X-"DT01R"=:D:=0; T:="5TTSZ"; *RDIV ST
030376                IF D=0 THEN
030400                   T:="IN5MSG"                   % INPUT
030401                ELSE
030402                   T:="ON5MSG"                   % OUTPUT
030403                FI; A:=0; CALL XSTDFADDR         % CLEAR IN5MSG/ON5MSG
030405             FI
030405          OD; GO CLIOLREG
030407   RBUS
030416
030416
030416   % *****************************************************************************
030416   %       ( 5 )    N 5 0 0 M
030416   %
030416   % ENTRY POINT OF MONITOR CALL 60
030416   %
030416   % *****************************************************************************
030416
030416   SUBR N500M,TOSYMON,5OKRET,EEILPAR
030416   N500M: CALL GET1                                           % GET THE FUNCTION PARAMETER
030417          IF X:=5FUNCTION BZERO COMAUTO>>FUNCMAX GO FAR ILLFUNC  % TOO LARGE FUNCTION CODE
030424          *1BANK;
030425          5IFUNC(X); *2BANK
030427          IF A=0 OR A="ILLFUNC" GO FAR ILLFUNC                % UNDEFINED FUNCTION
030433          IF RTREF><NSPREF AND T><0 THEN
030441             A=:D; CALL FSEMA; GO FAR EESPRES
030444          FI
030444          IF BACKGROUND><0 THEN
030446             5ND5US=:5TTIFIELD.BSTATE                         % SET ND500 STATUS IN BSTATE
030451             CALL ESCOF
030452          FI
030452          CALL INZ500; GO FAR ERET                            % INITIALIZE ND-500 SYSTEM IF NOT DONE
030454          -1=:N5FUNCTION
030456          RTREF=:D; CALL FSEMA; GO N5PDRES                    % FIND PROCESS DESC IF RESERVED
030462          X=:5PRDESCR                                         % 5PDRES EXPECTS MSG IN X
030463          IF 5FUNCTION BZERO COMAUTO=MEMDEF GO FAR CLRMS
030470          IF A=N5RES OR A=MRESSPES OR A=SSTDOM THEN           % NECESSARY TO RELEASE PROCESS BEFORE NEW RESERVE?
030501             N5REL=:N5FUNCTION
030503          FI
030503          X:=X.MESSBUFF
030504          GO FAR 5PDRES
030505   N5PDRES:                                                   % NO PROCESS DESCRIPTION RESERVED
030505          IF X:=5FUNCTION BZERO COMAUTO=N5REL GO FAR 5OKRET   % NOT NECESSARY TO RESERVE WHEN FUNCTION IS RELEASE
030512          IF X><N5RES AND X><MRESSPES AND X><SSTDOM THEN      % MAY BE ILL.FUNC. WHEN PROC.DESCR NOT RESERVED
030523             T:="PARANT"; *1BANK; LBYT; 2BANK
030527             IF A BIT COMSPEC THEN A/\7=:T; GO FAR 5PDR2 FI   % T=NUMBER OF PARAMTERS IN MON60 FUNCTION
030534             N5RES=:N5FUNCTION
030536          FI
030536          IF BACKGROUND><0 AND 5BCHFLAG=1 THEN
030544             MX5PROCS*5PRDSIZE+"S500S"=:X                     % IF BATCH TRY TO RESERVE LAST PROC.DESCR.
030550             IF X.RTRES=0 THEN
030552                A:=MX5PROCS=:5D32                             % RESERVE LAST PROC IF NOT
030554                GO PRFOUND; *)FILL                            % ALREADY RESERVED BY ANOTHER BATCH
030602             FI
030602          FI
030602          5SWPROC+1=:5D32; X:="S500S"+5PRDSIZE
030607   LOOP:  IF 5D32>>=MX5PROCS GO FAR EENPROC
030613            X=:D:=X.MESSBUFF; T:=5MBBANK; *AAX 5MSFL; LDATX
030620            IF A BIT 5SYSRES OR D.RTRES><0 GO FAR EFI         % FREE PROC.DESC?
030626   PRFOUND:    X=:B:=RTREF; CALL BRESERVE                     % RESERVE THE PROCESS DESC.
030631               *IOF
030632               ENOCPU=:"N500DF".N5SERROR
030635               X:=X.NCCPUDF; -1=:L; D:=0
030641               DO WHILE X><0                                  % FIND A CPU (WITH SHORTEST EXEC. QUEUE )
030642                  IF X.CPUAVAILABLE BIT 5ALIVE THEN
030645                     IF X.FERROR=0 OR A=N5STOPPED THEN
030652                        IF X.CPUAVAILABLE NBIT 5EXCLUD THEN
030655                           IF X.SPREF=0 THEN
030657                              IF X.MIFLAG NBIT MUDOM THEN
030662                                 IF X.LEXQUEUE<<L THEN A=:L; X=:D FI
030667                              ELSE
030670                                 X=:D; GO PRF1
030672                              FI
030672                           ELSE
030673                              X=:T; ESPRES=:"N500DF".N5SERROR; X:=T
030700                           FI
030700                        ELSE
030701                           IF D=0 THEN X=:D FI
030704                        FI
030704                     ELSE
030705                        IF D=0 THEN X=:D FI
030710                     FI
030710                  FI
030710                  IF X+5CPUDFSZ>>"E5CPUDF" THEN X:="S5CPUDF" FI
030715                  X=:T
030716                  WHILE T><X:="N500DF".NCCPUDF
030722                  T=:X
030723               OD
030724   PRF1:       *ION
030725               IF X:=D=0 THEN
030730                  X:=RTREF; CALL BRELEASE
030732                  "N500DF".N5SERROR; GO FAR ERET              % NO ND-500 CPU AVAILABLE
030735               FI
030735               A:="N500DF":=:B; X=:CCPUDF
030740               IF X+5CPUDFSZ>>"E5CPUDF" THEN X:="S5CPUDF" FI  % NEXT CPU
030745               X=:NCCPUDF
030746               A=:5PRDESCR                                    % CURRENT PROCESS DESCRIPTION ADDR
030747               A:=0 BONE 55BRKPRIOR BONE SLICE=:5PRDESCR.PSTAT % ALL PROCS ARE DEFAULT TIMESLICED
030754   % - CLEAR MESSAGE
030754   CLRMS:      T:=5MBBANK; X:=X.MESSBUFF; *AAX -55MSN         % MESSAGE START
030757               A:=X+55MESSIZE-1=:L; A:=0; D:=0                % MESSAGE END
030765               DO WHILE X<<L
030767                   *STDTX; AAX 2                              % CLEAR MESSAGE
030771               OD
030772               GO BYPASS; *)FILL
031014
031014
031014   % - INITIALIZE MESSAGE
031014   BYPASS:     X:=5PRDESCR.MESSBUFF=:L
031017               5PRDESCR; *AAX XADPR; STATX                   % SET ADDR OF PROC.DESCR IN MESSAGE
031022               5BRKPRIOR; *AAX 5PRIO-XADPR; STATX            % SET START PRIORITY
031025               A:=-1; *AAX 5TSLC-5PRIO; STATX                % TIMESLICER "COUNTER"
031030               CCPUDF.CPUNO; X:=L; *AAX 5CPUN; STATX         % SET THE CPU USED BY CURRENT PROC.
031035               A:=5FBUM60+5D32=:D:=0; AD SH 12
031042               *AAX ABUFA-5CPUN; STDTX; AAX -ABUFA           % ADDR OF PROC'S DATA BUFFER.
031045               5D32; *SENDE@3 STATX; AAX 5MSFL; LDATX
031051               IF T:=BACKGROUND=0 OR T:=5PASSTYPE><0 THEN    % BACGROUND USER RT AND SYSTEM OR RT-PROGR?
031057                  A BONE SPROK                               % YES, SPRIO MONCALL ALLOWED
031060               FI
031060               A/\154000; T:=5MBBANK; *STATX
031063               T SH 6; 5PRDESCR.MESSBUFF SHZ -12+T
031070               A=:RTREF.N5WINDOW                             % SET WINDOW TO MESSAGE
031072               IF BACKGROUND><0 AND 5BCHFLAG=0
031075               AND 5TTIFIELD.TYPRING BIT 5TERM OR A BIT 5BAD THEN
031104                  X.DFOPP.TDRADDR
031106               ELSE
031107                  A:=0
031110               FI; T:=5MBBANK; X:=5PRDESCR.MESSBUFF
031113               *AAX OUTDF; STATX; AAX -OUTDF                  % SET ADDR OF TERMINAL OUTPUT DF. WHEN TTIFIELD IS TERMINAL
031116               GO 5PDRES
031117   EFI:     MIN 5D32; 0/\0; X:=D+5PRDSIZE
031123          GO FAR LOOP
031124   *)FILL
031134
031134   INTEGER XPARANT                                               % CURRENT FUNCTION DECRIPTION
031135   5PDRES:                                                       % PROC.DESC IS RESERVED, X=MSG
031135          CALL GCPUDF; CALL ERRFATAL                             % X=CURRENT ND-500 CPU TO USE
031137          A=:CCPUDF; A.SPREF=:D
031143          IF D><0 AND D><RTREF AND 5FUNCTION><N5REL AND A><ABLOG AND A><MO5RT THEN
031162             CALL FSEMA; GO 5PDR1                                % ND-500 CPU IS RESERVED FOR SPECIAL USE
031164             GO FAR EESPRES                                      % AND THE RESERVING PROGR IS STILL ACTIVE
031165   5PDR1:    X:=CCPUDF; CALL RELCPU                              % THE RESERVING PROGR IS NO LONGER ACTIVE
031167          FI
031167          CALL SUPDWINDOW                                        % SET WINDOW ADDR TO MON60 BUFFER
031170          MLEV; *MST PIE
031172          X:=5FUNCTION BZERO COMAUTO; T:="PARANT"
031175          *1BANK; LBYT; 2BANK                                 % FETCH MON60 FUNCTION DESCR.
031200          A=:D/\7=:XPARANT
031203          IF BACKGROUND=0 AND D NBIT RTPLEGAL GO FAR EEFUNRTP % FUNCTION NOT LEGAL FROM RT-PROGR.
031207          IF D BIT COMPROT THEN                               % PROTECTED COMMAND
031211             IF BACKGROUND><0 AND 5PASSTYPE><2 AND 5FUNCTION NBIT COMAUTO GO FAR EENAUTHORIZED
031222             IF D BIT BSPRES THEN                             % FUNCT. REQUIRE ND500 CPU RESERVED FOR SPECIAL USE
031224                CALL TUSON; GO FAR ERET                       % TEST IF OTHER USES THIS ND-500 CPU
031226             FI
031226          FI; 5FUNCTION BZERO COMAUTO=:5FUNCTION
031231          IF BACKGROUND><0 THEN
031233             CALL ESCOFF; *IOF                                % SET ESCAPE OFF
031235             RTREF.STATUS BZERO 5REP=:X.STATUS; *ION          % CLEAR 5REP BIT
031242          FI; T:=XPARANT
031243   5PDR2: GO SKPLG
031244          T=:XPARANT
031245          A:=B+"FSYVARIABLES"=:X=:T                        % LOG MON 60 CALLS
031251          IF X.S0 << X OR A >>= T+70 THEN                  % POINTER OUTSIDE RING BUFFER ?
031257             A:=X+1=:X.S0                                  % RESET POINTER
031261          FI                                               % LOG : 2 WORDS
031261          X=:D; T:=5PRDESCR=:A.S0; 5FUNCTION=:X.S1         % 1. PROCESS DESCR.
031267          A:=X+2=:D.S0                                     % 2. M60 FUNCTION
031273          T:=XPARANT
031274   SKPLG :"5D11"+B=:D; X:=ZAREG
031300          A:=OLDPAGE; X+1=:B; CALL GND5PAR                    % FETCH MON-60 PARAMETERS
031304          "N500DF"=:B; X:=5FUNCTION; *1BANK
031310          A:=5IFUNC(X); *2BANK           % ADDR OF FUNCTION DEPENDANT "ROUTINE"
031312          A=:P                           % GO TO ENTRY POINT ACCORDING TO FUNCTION CODE
031313   *)FILL
031340
031340   EENIMPLEMENT: ENIMPLEMENT; GO ERET
031342   EEFUNRTP: EFUNRTP; GO ERET
031344   EENPROC:  ENOPROC; GO ERET
031346   EENPCOMMU: ENOPCOMMU; GO ERET
031350   EENAUTHORIZED: ENAUTHORISED; GO ERET
031352   EESPRES: ESPRES; GO ERET
031354   EEILPAR: EC174; GO ERET
031356   ILLFUNC: EILFUNC
031357   ERET:    T:="N500DF"=:B; A=:ZAREG; MLEV; *MST PIE          % SET B=N500DF AND SAVE ERROR CODE
031364            IF 5FUNCTION=N5REL AND SYSINITFLAG NBIT BFIRSTACCESS GO FAR 5OKRET  % WM476: Leave?
031373            GO FAR RET5
031374   *)FILL
031405
031405
031405   %================================================================
031405   %      ( 5 )     5 I F U N C
031405   %
031405   % TABLE OF ENTRY POINTS TO GOTO BEFORE CALLING THE SYSTEM MONITOR
031405   % THE FUNCTION CODE IS INDEX IN THE TABLE
031405   @ICR
031405   INTEGER ARRAY 5IFUNC:=(
031405          5NOPAR,5NOPAR,5NOPAR,5NOPAR,IPMWRITE,IDMWRITE,ISEGLOAD,IPLSWAPPER,    % 000 - 007
031415          5NOPAR,IWRGS,5NOPAR,ICONNFI,5NOPAR,5NOPAR,5NOPAR,5NOPAR,              % 010 - 017
031425          5NOPAR,5NOPAR,5NOPAR,5NOPAR,IWCNT,5NOPAR,5NOPAR,5NOPAR,               % 020 - 027
031435          5NOPAR,5NOPAR,5NOPAR,IDAMW,5NOPAR,5NOPAR,5NOPAR,ICSLOAD,              % 030 - 037
031445          5NOPAR,5NOPAR,5NOPAR,ISRES,ISREL,5NOPAR,IDEFSWAP,IDELSWAP,            % 040 - 047
031455          5NOPAR,5NOPAR,5NOPAR,5NOPAR,5NOPAR,ISPLACE,IEPLACE,5NOPAR,            % 050 - 057
031465          5NOPAR,IMRESSPES,IDEFHIST,ISTAHIST,ISTOHIAT,IREAHIST,IRELHIST,ISPRTE, % 060 - 067
031475          5NOPAR,ISSGTE,5NOPAR,5NOPAR,ISPRNM,ITSTUSER,ITOSWP,IRMESS,            % 070 - 077
031505          RRFLAG,WWFLAG,IFORGET,IRSYSP,IWSYSP,5NOPAR,5NOPAR,5NOPAR,             % 100 - 107
031515          IWPHSG,ISTAPRLOG,ISTOLOG,IPRILOG,IRELLOG,ISTLAPR,5NOPAR,IPRABORT,     % 110 - 117
031525          5NOPAR,5NOPAR,ILOGOFF,IMRELSPES,ISTAMLOG,IPRIMLOG,ISTOMLOG,IDFSYDOM,  % 120 - 127
031535          ISFSYDOM,IDLSYDOM,5NOPAR,ILI5EXQ,IPLDEB,IABLOG,IPRACTIVE,0,           % 130 - 137
031545          5NOPAR,5NOPAR,5NOPAR,IMO5RT,ICHACPU,ISSTDOM,ILLFUNC,ILLFUNC,          % 140 - 147
031555          ILI5TQU,ILLFUNC,ILLFUNC,ILLFUNC,5NOPAR,5NOPAR,5NOPAR,IWCNT,           % 150 - 157
031565          IN5SEGLOAD,INDFSYDOM,5NOPAR,5NOPAR,5NOPAR,5NOPAR,5NOPAR,5NOPAR,       % 160 - 167
031575          5NOPAR,5NOPAR,5NOPAR,ICPUSTAT,ILLFUNC,ILLFUNC,ILLFUNC,5NOPAR);        % 170 - 177
031605   @CR;
031605
031605   %-------------------------------------------------------------------------------
031605   IPMWRITE:    % FUNCTION=004: LOGICAL PROGREAM MEMORY WRITE
031605   IDMWRITE:    % FUNCTION=005: LOGICAL DATA MEMORY WRITE
031605   IDAMW:       % FUNCTION=033: PHYSICAL DATA MEMORY WRITE
031605          AD:=5DD1
031606          IF A><0 OR D>>4000 THEN
031612             EBIGBUF; GO FAR ERET                   % 4000B BYTES ARE MAX DUE TO BUFFER SIZE
031614          FI
031614          T:=5D12; A:=5P3; CALL FRUSMOVE            % COPY DATA FROM USER TO MON60 BUFFER
031617          GO FAR 5NOPAR; *)FILL
031625
031625   %-------------------------------------------------------------------------------
031625   ISEGLOAD:    % FUNCTION=006: LOAD (PLACE), ONE SEGMENT
031625          A:=5P1; T:=200; CALL FRUSMOVE             % COPY SEGMENT NAME FROM USER TO MON60 BUFFER
031630          IF 5D51><0 THEN                           % ANY ND-100/ND-500 SHARED PARTS?
031632             A:=5P5; T:=40; X:=100; CALL XFRUSMOVE  % YES, COPY SHARD-INFO FROM USER TO MON60 BUFFER
031636          FI; GO FAR 5NOPAR; *)FILL
031643
031643   %-------------------------------------------------------------------------------
031643   IPLSWAPPER:  % FUNCTION=007: PLACE SWAPPER
031643   IDEFSWAP:    % FUNCTION=046: DEFINE SWAP FILE
031643   IDELSWAP:    % FUNCTION=047: DELETE SWAP FILE
031643   ISPRTE:      % FUNCTION=067: READ PROCESS ENTRY FROM NAME SEGMENT
031643   ISSGTE:      % FUNCTION=071: READ PHYS.SEGMENT ENTRY FROM NAME SEGMENT
031643   ISFSYDOM:    % FUNCTION=130: START STANDARD DOMAIN
031643   IDLSYDOM:    % FUNCTION=131: DELETE STANDARD DOMAIN
031643   ICOPF: A:=5P1; T:=200; CALL FRUSMOVE             % COPY FILE NAME TO MON60 BUFFER
031646          GO FAR 5NOPAR; *)FILL
031652
031652   %-------------------------------------------------------------------------------
031652   ICONNFI:     % FUNCTION=013: CONNECT FILE
031652          T:=200; A:=5P1; CALL FRUSMOVE             % COPY FILE NAME TO MON60 BUFFER
031655          T:=4; A:=5P3; X:=100; CALL XFRUSMOVE      % COPY FILE TYPE TO MON60 BUFFER
031661          GO FAR 5NOPAR; *)FILL
031666
031666   %-------------------------------------------------------------------------------
031666   IWCNTS:      % FUNCTION=024 : WRITE CONTROL STORE (equal for func=157)
031666          IF 5D22>>2000 GO FAR ERET                    % 2000B CS-WORDS ARE MAX.
031672          T:=5D22 SH 1; A:=5P3; CALL FRUSMOVE       % COPY CONTROL STORE DATA FROM USER TO MON60 BUFFER
031676          GO FAR 5NOPAR; *)FILL
031703
031703   %-------------------------------------------------------------------------------
031703   ICSLOAD:     % FUNCTION=037: LOAD CONTROL STORE (LOAD A FILE INTO CS)
031703            A:=5P3; T:=200; CALL FRUSMOVE           % COPY FILE NAME TO MON60 BUFFER
031706            GO FAR 5NOPAR; *)FILL
031712
031712   %-------------------------------------------------------------------------------
031712   ISRES:            % FUNCTION=043: RESERVE ND-500 CPU/SYSTEM FOR SPECIAL USE
031712          IF 5D11=0 AND 5D12=0 THEN
031716             IF CCPUDF.SPREF=RTREF GO FAR 5OKRET                    % ALREADY RESERVED FOR SPECIAL USE BY CALLER?
031723             CALL TUSON; GO FAR ERET                                % IF ANYONE ELSE USING THIS ND-500 CPU NOW ?
031725          ELSE
031726             IF RTREF=NSPREF GO FAR 5OKRET                          % ALREADY RESERVED FOR SPECIAL USE BY CALLER?
031732             A=:NSPREF; CALL XTUSON; GO FAR ERET                    % SET UNAVAILABLE AND CHECK FOR OTHER USERS ALREADY LOGGED ON
031735          FI
031735          GO FAR 5OKRET
031736
031736   %-------------------------------------------------------------------------------
031736   ISREL:       % FUNCTION=044: RELEASE ND-500 CPU/SYSTEM FROM SPECIAL USE
031736          IF CCPUDF.SPREF=RTREF THEN CALL RELCPU FI             % RESERVED FOR SPECIAL USE BY THE CALLER?
031744          IF NSPREF=RTREF THEN 0=:NSPREF FI
031751          GO FAR 5OKRET; *)FILL
031760
031760   %-------------------------------------------------------------------------------
031760   ISPLACE:     % FUNCTION=055: START-PLACE
031760          T:=5MBBANK; X:=5PRDESCR.MESSBUFF; *AAX 5MSFL; IOF; LDATX
031766          A BZERO 55REP; *STATX; ION                % CLEAR 55REP BIT
031771          GO FAR 5NOPAR; *)FILL
031774
031774   %-------------------------------------------------------------------------------
031774   IEPLACE:     % FUNCTION=056: END-PLACE
031774   IWRGS:       % FUNCTION=011: WRITE REGISTERS
031774          T:=NREGS SH 2; A:=5P1; CALL FRUSMOVE  % COPY REGISTER CONTENTD FROM USER TO MON60 BUFFER
032000          GO FAR 5NOPAR; *)FILL
032003
032003   %-------------------------------------------------------------------------------
032003   IMRESSPES:   % FUNCTION=061: RESERVE MEORY FOR THE ND-500 TEST-MONITOR
032003          IF 5D11 = -1 AND 5D12 >< -1 THEN
032013             IF A-1>>NCPU-1 GO FAR EEILPAR             % CHECK IF ND-500 CPU SPECIFIED?
032020             A*5CPUDFSZ+"S5CPUDF"=:X=:CCPUDF           % CCPUDF=CURRENT ND-500 CPU DF.
032024             IF X.SPREF><0 AND A><RTREF GO FAR EESPRES % ERROR IF THIS CPU IS ALREADY RESERVED FOR SPECIAL USE BY ANOTHER
032031          FI
032031   OUT:   CALL TUSON; GO FAR ERET                      % ANY OTHER USING THIS CPU NOW?
032033          GO FAR 5NOPAR; *)FILL                        % NO, RESERVED FOR SPECIAL USE BY THE CALLER
032044
032044   %-------------------------------------------------------------------------------
032044   IDEFHIST:    % FUNCTION=062: DEFINE HISTOGRAM
032044          IF 5HRTP><0 AND A><RTREF THEN EHIUSED; GO FAR ERET FI  % HISTOGRAM ALREADY IN USE BY ANOTHER
032053          RTREF=:5HRTP; 0=:5HIFLAG                            % SET ND-500 CPU AND SPECIFY PROC TO RUN HISTOGRAM ON
032056          AD:=5DD3                                            % AD=NUMBER OF INTERVALS
032057          IF A><0 OR D>>100 OR D=0 GO FAR EEILPAR             % 100B INTERVAL IS MAX
032065          A:=D=:5HICHANNELS
032067          AD:=5DD2                                            % AD=INTERVAL SIZE
032070          IF A><0 OR D=0 GO FAR EEILPAR
032073          A:=D=:5HINTERVAL; AD:=5DD1=:5HISTART                % 5HISTART=START OF FIRST INTERVAL
032077          X:="5HIDATA"
032100          DO WHILE X<<="5HIOUTSIDE"+1; 0=:X.S0; X+1; OD       % CLEAR HISTOGRAM SAMPLING BUFFER
032107          GO FAR 5OKRET; *)FILL
032124
032124   %-------------------------------------------------------------------------------
032124   ISTAHIST:    % FUNCTION=063: START HISTOGRAM
032124          IF RTREF><5HRTP THEN EHNRESERVED; GO FAR ERET FI    % HISTOGRAM RESERVED BY CALLER?
032132          IF 5HIFLAG><0 GO FAR 5OKRET
032135          5PRDESCR-"S500S"; A=:D:=0; T:=5PRDSIZE; *RDIV ST    % COMPUTE PROC.NO OF CALLER
032143          A-1*2+"F5DSG"=:T; X:="5RPRONO"
032150          CALL GET1L; CALL ERRFATAL; A=:5D12
032153          IF 5HIFLAG=0 THEN
032155             *IOF                                             % YES
032156             CALL HIMFEXQUEUE; 0/\0                           % MAKE SURE HISTOGRAM MESS IS NOT IN EX-QUEUE
032160             CALL SLOCK; 0/\0;
032162             5D12-5SWPROC*5PRDSIZE+"S500S"; X:=A.MESSBUFF
032170             T:=5MBBANK; *AAX 5MSFL; LDATX; 5CPUB@3 BLDA DA
032174             A BONE 5CPUBOUND; *5SCPU@3 BSTA DA; STATX
032177             *AAX 5CPUN-5MSFL; LDATX
032201             X:=HIMESS; *AAX 5CPUN; STATX
032204             5D12; *AAX WANTP-5CPUN; STATX; AAX -WANTP        % PROC.NO. INTO HISTOGRAM MESSAGE
032210             CALL GCPUDF; CALL ERRFATAL; A=:B
032213             CALL ITO500XQ                                    % INSERT HISTOGRAM MESS IN EX-QUEUE
032214             CALL SUNLOCK
032215             1=:5HIFLAG                                       % MARK THAT HISTOGRAM SAMPLING IS STARTED
032217             *ION
032220             "N500DF"=:B
032222          FI
032222          GO FAR 5OKRET; *)FILL
032251
032251   %-------------------------------------------------------------------------------
032251   ISTOHIST:    % FUNCTION=064: STOP HISTOGRAM
032251          IF RTREF><5HRTP THEN EHNRESERVED; GO FAR ERET FI    % ERROR WHEN HISTOGRAM NOT RESERVED BY CALLER
032257          IF 5HIFLAG=1 THEN CALL RLHILOG FI; 0=:5HRTP         % IS HISTOGRAM STARTED?
032265          GO FAR 5OKRET; *)FILL
032275
032275   %-------------------------------------------------------------------------------
032275   IREAHIST:    % FUNCTION=065: READ HISTOGRAM
032275          IF RTREF><5HRTP THEN EHNRESERVED; GO FAR ERET FI    % ERROR WHEN HISTOGRAM NOT RESERVED BY CALLER
032303          T:=5P1; A:="5HIDATA"=:D; X:=200; A:=OLDPAGE         % COPY HISTOGRAM DATA TO USER FROM "5HIDATA"
032310          K:=1; CALL MOVUS                                    %   SIZE EQUALS 2 WORDS PER CHANNEL (=64)
032312          5P1+200=:X; AD:=5HIOUTSIDE; CALL STDS0              % COPY SAMPLING OF 'OUSIDE CHANNELS' SEPARATELY
032317          GO FAR 5OKRET; *)FILL
032332
032332   %-------------------------------------------------------------------------------
032332   IRELHIST:    % FUNCTION=066: STOP AND RELEASE HISTOGRAM
032332          IF RTREF><5HRTP THEN EHNRESERVED; GO FAR ERET FI    % ERROR WHEN HISTOGRAM NOT RESERVED BY CALLER
032340          IF 5HIFLAG><0 THEN CALL RLHILOG FI; 0=:5HRTP
032344          GO FAR 5OKRET; *)FILL
032354
032354   %-------------------------------------------------------------------------------
032354   ISPRNM:      % FUNCTION=074: SET NAME ON CURRENT PROCESS
032354   IPRACTIVE:   % FUNCTION=136: ACTIVATE STOPPED PROCESS
032354          A:=5P1; T:=50; CALL FRUSMOVE          % COPY PROCESS NAME TO MON60 BUFFER
032357          GO FAR 5NOPAR; *)FILL
032362
032362   %-------------------------------------------------------------------------------
032362   ITSTUSER:    % FUNCTION=075: CHECK IF CURRENT USER IS USER SYSTEM
032362          IF 5PASSTYPE=2 GO FAR 5OKRET              % PASSTYPE=2 MEANS USER SYSTEM
032366          GO FAR EENAUTORISED; *)FILL               % ERROR, NOT USER SYSTEM
032372
032372   %-------------------------------------------------------------------------------
032372   ITOSWP:      % FUNCTION=076: MESSAGE TO SWAPPER
032372          A:=5P1+7; T:=55MESSIZE-7 SH 1; CALL FRUSMOVE % COPY MESSAGE TO MON60 BUFFER
032400          GO FAR 5NOPAR; *)FILL
032404
032404   %-------------------------------------------------------------------------------
032404   IRMESS:      % FUNCTION=077: READ MESSAGE
032404          IF 5D11=0 AND 5D12>>=5SWPROC THEN         % READ ONE SPECIFIC MESSAGE?
032412              IF A=-1 THEN                          % YES, -1= OWN PROCES
032415                 X:=5PRDESCR
032416              ELSE
032417                 IF A>>MX5PROCS GO FAR EEILPAR      % LEGAL PROC. NO?
032422                 A-5SWPROC*5PRDSIZE+"S500S"=:X      % X=ADDR OF ACTUAL PROC.DESCR.
032426              FI; X.MESSBUFF
032427           ELSE
032430              GO FAR ILLFUNC                        % NOT IMPLEMENTED (ILLEGAL FUNCTION CODE)
032431           FI; A=:D; A:=5MBBANK; CALL XSUPDWINDOW   % SETUP WINDOW TO MESSAGE
032434           A:="55MESSIZE-55MSNEGSIZE-3" SH 1; T:=5P2
032437           CALL TOUSMOVE                            % COPY MESSAGE TO USER'S BUFFER
032440           GO FAR 5OKRET; *)FILL
032453
032453   %-------------------------------------------------------------------------------
032453   RRFLAG:      % FUNCTION=100: READ FLAGS FROM ND-500 DATA SEGMENT
032453   WWFLAG:      % FUNCTION=101: WRITE FLAGS INTO ND-500 DATA SEGMENT
032453          IF 5D12=-1 THEN                                     % 5D12 = PROCESS NUMBER
032457             X:=5PRDESCR                                      % -1 MEANS OWN PROCESS
032460          ELSE
032461             IF A<<=5SWPROC OR A>>MX5PROCS GO FAR EEILPAR     % LEGAL PROCESS NUMBER ?
032467             A-5SWPROC*5PRDSIZE+"S500S"=:X                    % X=ACTUAL PROCESS DESCR
032473             IF X.RTRES=0 GO FAR EEILPAR                      % ERROR IF PROC. NOT IN USE
032476          FI; X=:5D32
032477   %      A:=X-"S500S"=:D:=0; T:=5PRDSIZE; *RDIV ST           % COMPUTE PROCESS NUMBER
032477   %      A-1*2+"F5DSG"=:T; CALL M1MEXY; T=:5OLDSEG           % GET ACTUAL ND-500 DATA SEGMENT
032477          X:=X.MESSBUFF; T:=5MBBANK; *SENDE@3 LDATX
032502          A-5SWPROC*2+"F5DSG"=:T; CALL M1MEXY; T=:5OLDSEG           % GET ACTUAL ND-500 DATA SEGMENT
032510          IF 5FUNCTION=5RFLAG THEN                            % WHICH FUNCTION?
032514             AD:="FF500".DS0=:5DD2                            % READ FLAGS
032517          ELSE
032520             IF 5D32><5PRDESCR THEN                           % WRITE TO OWN PROC?
032524                IF BACKGROUND><0 AND 5PASSTYPE=0 GO FAR EENAUTHORISED % ONLY ALLOWED FROM RT-PROGR OR USER SYSTEM
032531             FI; AD:=5DD2=:"FT500".DS0                        % WRITE FLAGS
032534          FI; T:=5OLDSEG; CALL M1MEXY                         % GET CALLER'S ORIGINAL SEGMENTS
032536          IF 5FUNCTION=5RFLAG THEN
032542             X:=5P2; AD:=5DD2; CALL STDS0                     % COPY FLAGS TO USER
032545          FI; GO FAR 5OKRET; *)FILL
032566
032566   INTEGER FORGPRNO
032567   %-------------------------------------------------------------------------------
032567   IFORGET:     % FUNCTION=102: STOP ND-500 SYSTEM (ABORT ALL ACTIVE PROCS, AND RELEASE MON60 BUFFERS)
032567          *IOF
032570          RTREF=:NSPREF                                       % ND-500 CPU RESERVED FOR SPECIAL USE
032572          SYSINITFLAG BONE B5STOP=:SYSINITFLAG                % MARK SYSTEM IN STOP-ND-500 MODE
032575          CCPUDF=:B; N5STOPPED; CALL RSTARTALL                % REMOVE ALL PROCS. FROM EX-QUEUE
032601          *ION
032602          "N500DF"=:B; 5SWPROC+1=:FORGPRNO
032607          X:="S500S"+5PRDSIZE
032611          FOR FORGPRNO DO WHILE FORGPRNO<<=MX5PROCS           % ABORT ALL ACTIVE PROCS.
032615             IF X.RTRES><0 AND A><RTREF THEN                  % PROC USED AND NOT USED BY CALLER?
032622                *IOF                                          % YES
032623                X=:D
032624                IF A.STATUS BIT 5BACKGROUND THEN              % BACKGROUND PROCESS?
032630                   A BONE 5ESCF=:X.STATUS                     % YES, SET ESCAPE PRIORITY OF SHADOW PROCESS
032632                FI
032632                D.PSTAT BONE 5SYSABORT=:X.PSTAT               % MARK THAT PROC SHOULD BE ABORTED
032636                CALL SLOCK; 0/\0
032640                T:=5MBBANK; X:=X.MESSBUFF; *AAX 5MSFL; LDATX
032644                A BONE 5IBRK BONE 52ESCSET; *STATX            % MARK THAT PROC SHOULD BE ABORTED
032647                *AAX XADPR-5MSFL; LDXTX
032651                CALL SUNLOCK
032652                X.RTRES; *IRW MLEVB DX
032654                "SYSABORT"; *IRW MLEVB DP
032656                MLEV; *MST PIE; MST PID; ION
032662             FI; X+5PRDSIZE
032663          OD; GO FAR 5NOPAR; *)FILL
032702
032702   %-------------------------------------------------------------------------------
032702   IRSYSP:      % FUNCTION=103: READ SYSTEM VARIABLES
032702          "N500DF+SYSPAR"=:D; T:=5P1; X:=16
032706          A:=OLDPAGE; K:=1; CALL MOVUS              % COPY SYSTEM PARAMETERS TO USER
032711          GO FAR 5OKRET; *)FILL
032715
032715   %-------------------------------------------------------------------------------
032715   IWSYSP:      % FUNCTION=104: WRITE SYSTEM PARAMETERS
032715          T:="N500DF+SYSPAR"; X:=16; 5P1=:D; A:=OLDPAGE
032722          K:="0"; CALL MOVUS                        % COPY SYSTEM PARAMETERS TO N500DF
032724          GO FAR 5NOPAR; *)FILL
032730
032730   %-------------------------------------------------------------------------------
032730   IWPHSG:      % FUNCTION=110: WRITE INTO A PHYSICAL SEGMENT
032730          AD:=5DD3
032731          IF A><0 OR D>>4000 THEN              % 4000B BYTES ARE MAX TO WRITE
032735             EBIGBUFF; GO FAR ERET
032737          FI
032737          T:=5D32; A:=5P4; CALL FRUSMOV        % COPY FROM USER TO MON60 BUFFER
032742          GO FAR 5NOPAR; *)FILL
032750
032750   %-------------------------------------------------------------------------------
032750   ISTAPRLOG:   % FUNCTION=111: START PROCESS LOG ONE
032750          IF 5HRTP><0 AND A><RTREF THEN ELOGINUSE; GO FAR ERET FI % LOGGING FACILITY RESERVED BY ANOTHER?
032757          IF 5HIFLAG><0 AND A><2 THEN ELOGINUSE; GO FAR ERET FI   % ANOTHER LOGGING FUCTION ALREADY DEFINED BY CALLER
032766          AD:=5DD1; IF A><0 OR D>>MX5PROCS GO FAR EEILPAR         % LEGAL PROC.NO. TO LOG?
032773          A:=D=:5LOGPROC; RTREF=:5HRTP                            % SPECIFY PROC.NO. TO LOG,
032777                                                                  %   WHO IS LOGGING AND ON WHICH CPU
032777          2=:5HIFLAG; X:="5HIDATA"                                % MARK THAT LOGGING IS STARTED
033002          DO WHILE X<<="5HIOUTSIDE"+1; 0=:X.S0; X+1; OD           % CLEAR HISTOGRAM SAMPLING BUFFER
033011          GO FAR 5OKRET; *)FILL
033025
033025   %-------------------------------------------------------------------------------
033025   ISTOLOG:     % FUNCTION=112: STOP LOGGING
033025          IF 5HRTP><RTREF THEN ELOGNRESERVED; GO FAR ERET FI  % LOGGING FACILITY RESERVED BY CALLER?
033033          0=:5HIFLAG; GO FAR 5OKRET; *)FILL                    % YES, MARK THAT LOGGING IS STOPPED
033043
033043   %-------------------------------------------------------------------------------
033043   IPRILOG:     % FUNCTION=113: READ LOG DATA (PRINT LOG INFO)
033043          IF 5HRTP><RTREF THEN ELOGNRESERVED; GO FAR ERET FI  % LOGGING FACILITY RESERVED BY THE CALLER?
033051          IF 5HIFLAG=2 THEN                                   % YES, WHICH LOG FUNCTION?
033055             T:=16                                            % PROCESS-LOG-ONE
033056          ELSE                                                % PROCESS-LOG-ALL
033057             0=:"5HIDATA".S2; T:=MX5PROCS+1=:X.S3+3 SH 1
033066          FI; X:=T; "5HIDATA"=:D; T:=5P2; A:=OLDPAGE
033073          K:=1; CALL MOVUS                                    % COPY SAMPLED DATA TO USER FROM "5HIDATA"
033075          AD:=5DD1
033076          IF A=0 AND D=0 THEN                                 % SHOULD LOGGING-DATA-BUFFER BE CLEARED?
033101             X:="5HIDATA"                                     % YES, CLEAR LOGGING-DATA-BUFFER
033102             DO WHILE X<<="5HIOUTSIDE"+1; 0=:X.S0; X+1; OD    % YES, CLEAR LOGGING-DATA-BUFFER
033111          FI; GO FAR 5OKRET; *)FILL
033124
033124   %-------------------------------------------------------------------------------
033124   IRELLOG:     % FUNCTION=114: STOP LOGGING AND RELEASE LOGGING FACILITY
033124          IF 5HRTP><RTREF THEN ELOGNRESERVED; GO FAR ERET FI  % LOGGING FACILITY RESERVED BY THE CALLER?
033132          0=:5HIFLAG=:5HRTP; GO FAR 5OKRET; *)FILL             % YES, STOP LOGGING AND RELEASE LOGGING FACILITY
033143
033143   %-------------------------------------------------------------------------------
033143   ISTLAPR:     % FUNCTION=115: START PROCESS-LOG-ALL.
033143          IF 5HRTP><0 AND A><RTREF THEN ELOGINUSE; GO FAR ERET FI % LOGGING FACILITY RESERVED BY ANOTHER PROC?
033152          IF 5HIFLAG><0 AND A><3 GO FAR ILLFUNC      % NO, ALREADY USED BY THE CALLER FOR OTHER LOGGING FUNC.?
033157          RTREF=:5HRTP                               % NO, SPECIFY PROC. USING LOGGING FACILITY AN ON WHICH CPU
033161          X:="5HIDATA"
033162          DO WHILE X<<="5HIOUTSIDE"+1; 0=:X.S0; X+1; OD       % YES, CLEAR LOGGING-DATA-BUFFER
033171          3=:5HIFLAG                                          % MARK THAT PROCESS-LOG-ALL IS STARTED
033173          GO FAR 5OKRET; *)FILL
033205
033205   %-------------------------------------------------------------------------------
033205   IPRABORT:    % FUNCTION=117: ABORT PROCESS
033205   ILOGOFF:     % FUNCTION=122: LOGOFF PROCESS
033205          IF 5D11><0 OR 5D12<<=5SWPROC OR A>>MX5PROCS GO FAR EEILPAR  % LEGAL PROCESS NUMBER?
033216          A-5SWPROC*5PRDSIZE+"S500S"
033221          IF A.RTRES=0 GO FAR 5OKRET                          % YES, IS THIS PROC. RESERVED BY ANYONE?
033225          A=:D                                                % D=RT-DESCR. OF ACTUAL PROCS. OWNER.
033226          IF 5FUNCTION=PRSTOP THEN                            % IS FUNCTION ABORT-PROCESS?
033232             IF D=RTREF THEN                                  % ABORT CALLER?
033235                ABREL=:5FUNCTION; GO FAR 5NOPAR               % YES, CHANGE FUNCTION TO "ABREL"
033240             FI; *IOF
033241             X.PSTAT BONE 5SYSABORT BZERO SOFFLOGG            % MARK THAT PROC. SHOULD BE ABORTED
033244          ELSE                                                % LOGOFF PROC.
033245             IF D=RTREF THEN                                  % LOGOFF CALLER?
033250                *IOF
033251                X.PSTAT BONE SOFFLOGG BZERO 5SYSABORT=:X.PSTAT % MARK THAT PROC SHOULD BE LOGGED OFF
033255                *ION
033256                XN5REL=:5FUNCTION; GO FAR 5NOPAR              % YES, CHANGE FUNCTION TO "XN5REL"
033261             FI; *IOF
033262             X.PSTAT BONE SOFFLOGG BZERO 5SYSABORT            % MARK THAT PROC SHOULD BE LOGGED OFF
033265          FI
033265   INPRABORT:                                                 % A=PSTAT; INTERRUPT OFF
033265          A=:X.PSTAT                                          % MARK THAT PROC IS "IN-BREAK" STATUS
033266          CALL SLOCK; 0/\0
033270          T:=5MBBANK; X:=X.MESSBUFF; *AAX 5MSFL; LDATX
033274          A BONE 5IBRK BONE 52ESCSET; *STATX                  % MARK THAT PROC IS "IN-BREAK" STATUS
033277          *AAX XADPR-5MSFL; LDXTX
033301          CALL SUNLOCK
033302          X.RTRES; *IRW MLEVB DX
033304          "SYSABORT"; *IRW MLEVB DP
033306          MLEV; *MST PIE; MST PID; ION
033312          GO FAR 5OKRET; *)FILL
033327
033327   %-------------------------------------------------------------------------------
033327   IMRELSPES:   % FUNCTION=123: RELEASE ND-500 AND MEMORY FROM THE TEST-MONITOR
033327          IF CCPUDF.SPREF><RTREF GO FAR 5OKRET       % RESERVED FOR SPECIAL USE BY THE CALLER?
033334          GO FAR 5NOPAR; *)FILL                      % YES, CONTINUE TO RELEASE MEMORY
033340
033340   %-------------------------------------------------------------------------------
033340   ISTAMLOG:    % FUNCTION=124: START MONITOR CALL LOG
033340          IF 5MLOG><RTREF AND A><0 THEN ELOGINUSE; GO FAR ERET FI % MONCALL LOG IN USE BY ANOTHER PROC?
033347          AD:=5DD1                                            % NO, ITS FREE
033350          IF A><0 OR D><0 THEN                                % LOG MONCALLS FOR ALL PROCS OR FOR OWN?
033353             IF BACKGROUND><0 AND 5PASSTYPE><2 GO FAR EENAUTORISED % LOG FOR ALL, ALLOWED FOR USER SYSTEM ONLY
033361             A:= -1                                           % 5LOGPROC=-1 MEANS LOG MONCALLS FOR ALL PROCS
033362          ELSE                                                % LOG MONCALLS FOR OWN PROCESS ONLY
033363             5PRDESCR-"S500S"; A=:D:=0; T:=5PRDSIZE; *RDIV ST % COMPUTE PROC.NO OF CALLER
033371             A-1*2+"F5DSG"=:T; X:="5RPRONO"
033376             CALL GET1L; CALL ERRFATAL
033400          FI; A=:5MLOPROC; RTREF=:5MLOG                       % SPECIFY WHICH PROC TO LOG AND WHO IS USING MONCALL LOG
033403          A:=5FBUM60=:D:=0; AD SH 12; T:=A; X:=D              % FIRST MON60 BUFFER IS USED FOR MONCALL LOGGING
033411          A:=1776+X=:L                                        % L=END OF MON60 BUFFER -2
033414          A:=0; D:=0
033416          DO
033416             *STDTX                                           % CLEAR MONCALL LOG BUFFER
033417          WHILE X><L
033421             X+2
033422          OD; GO FAR 5OKRET; *)FILL
033444
033444   %-------------------------------------------------------------------------------
033444   IPRIMLOG:    % FUNCTION=125: READ MONCALL LOG DATA (PRINT MONCALL LOG)
033444          IF 5MLOG><RTREF THEN ELOGNRESERVED; GO FAR ERET FI  % MONCALL LOG RESERVED BY THE CALLER?
033452          A:=5FBUM60=:D:=0; AD SH 12; CALL XSUPDWINDOW    % YES, SETUP WINDOW TO MONCALL LOG DATA BUFFER
033457          A:=3000; T:=5P1; CALL TOUSMOVE                  % COPY MONCALL LOG DATA TO USER FROM MONCALL LOG DATA BUFFER
033462          GO FAR 5OKRET; *)FILL
033473
033473   %-------------------------------------------------------------------------------
033473   ISTOMLOG:    % FUNCTION=126: STOP AND RELEASE MONCALL LOG
033473          IF 5MLOG><RTREF THEN ELOGNRESERVED; GO FAR ERET FI  % MONCALL LOG RESERVED BY THE CALLER?
033501          0=:5MLOG=:5MLOPROC; GO FAR 5OKRET; *)FILL            % STOP AND RELEASE MONCALL LOG.
033512
033512   %-------------------------------------------------------------------------------
033512   INDFSYDOM:   % FUNCTION=161: DEFINE STANDARD DOMAIN (NEW DOMAIN FORMAT)
033512   IDFSYDOM:    % FUNCTION=127: DEFINE STANDARD DOMAIN
033512          A:=5P1; T:=4000; CALL FRUSMOVE        % COPY STANDARD-DOMAIN INFO TO MON60 BUFFER
033515          GO FAR 5NOPAR; *)FILL
033521
033521   %-------------------------------------------------------------------------------
033521   ILI5EXQ:     % FUNCTION=133: LIST ND-500 EX-QUEUE
033521          K:="0"; GO FAR ILI5F; *)FILL                  % K=ROUTINE SWITCH
033524
033524   %-------------------------------------------------------------------------------
033524   IPLDEB:      % FUNCTION=134: PLACE DEBUGGER
033524          A:=5P2; T:=200; CALL FRUSMOVE         % COPY DEBUGGER NAME TO MON60 BUFFER
033527          GO FAR 5NOPAR; *)FILL
033533
033533   %-------------------------------------------------------------------------------
033533   IABLOG:      % FUNCTION=135: LOGG OFF PROCESS AND ABORT RT-PROGRAM.
033533           IF 5D11><0 GO FAR EEILPAR                % 5DD1=RT-DESCR ADDR
033536           IF X:=5D12=0 THEN X:=RTREF FI            % RT-DESCR=0 MEANS CALLING PROGR
033542           IF X.STATUS BIT 5BACKGR GO FAR EEILPAR   % ONLY RT-PROGR. CAN BE ABORTED
033545           CALL GOODRT; GO FAR EEILPAR          % LEGAL RT-DESCR ADDR?
033547           *IOF
033550           5DD1; CALL FSEMA; GO FAR 5ABPROG         % YES, HAS IT RESERVED A ND-500 PROC?
033553           X.PSTAT BONE 5SYSABORT BONE SOFFLOG      % 5SYSABORT=1 AND SOFFLOG=1 MEANS LOGG OFF ND-500 PROC
033556           GO FAR INPRABORT; *)FILL                 % A=PSTAT; INTERRUPT OFF
033565
033565   %-------------------------------------------------------------------------------
033565   FPRACTIVE:   % FUNCTION=136: ACTIVATE STOPPED PROCESS
033565          ZPREG-1=:ZPREG                            % SKIP-RETURN ALREADY GIVEN BY SYS.MON??????????
033570          IF 5D12<<=5SWPROC OR A>>MX5PROCS GO FAR EEILPAR   % LEGAL PROCESS NUMBER?
033577          A-5SWPROC*5PRDSIZE+"S500S"=:X                     % X=ACTUAL PROC'S PROC.DESCR
033603          IF T:=X.RTRES=0 GO FAR EEILPAR            % ACTUAL PROCESS IN USE?
033606          T:=5MBBANK; X:=X.MESSBUFF; *AAX MAGNO; LDXTX
033612          IF X><5D11 GO FAR EEILPAR                 % CORRECT MAGNO?
033615          *IOF
033616          X:=A; CALL 5PRACTIVATE; 0/\0              % ACTIVATE ND-500 PROC (OR SET 55REP BIT)
033621          *ION
033622          GO FAR 5OKRET; *)FILL
033633
033633
033633   IIM5RT:  A:=153100; GO 5ABFELL
033635   5ABPROG: A:=153105
033636   5ABFELL: A=:D; MLEV; *MST PIE; ION
033642            "5D12"+B=:5D11
033645            A:="5D11"+B; *EXR SD               % EXECUTE MON RT/MON ABORT
033650            GO FAR 5OKRET; *)FILL
033656
033656   %-------------------------------------------------------------------------------
033656   IMO5RT:      % FUNCTION=143: ACTIVATE PROGRAM EITHER IN ND-500 OR IN ND-100
033656           AD:=5DD1; IF A><0 GO FAR EEILPAR         % AD=RT-DESCR ADDR
033661           IF D=0 THEN T:=RTREF=:D FI; AD=:5DD1     % RT-DESCR=0 MEANS CALLING PROGR
033666           X:=D; CALL GOODRT; GO FAR EEILPAR    % LEGAL RT-DESCR ADDR.?
033671           *IOF
033672           5DD1; CALL FSEMA; GO FAR IIM5RT          % YES, HAS IT RESERVED A ND-500 PROC.?
033675           CALL 5PRACTIVATE; 0/\0                   % YES, ACTIVATE ND-500 PROC.
033677           GO FAR IIM5RT; *)FILL
033706
033706   %-------------------------------------------------------------------------------
033706   ICHACPU:     % FUNCTION=144: CHANGE CPU
033706          IF 5D12-1>>NCPU-1 GO FAR EEILPAR
033714          A*5CPUDFSZ+"S5CPUDF"=:X                   % YES
033717          IF X.CPUAVAILABLE NBIT 5ALIVE THEN ENOCPU; GO FAR ERET FI
033724          IF T:=BACKGROUND><0 AND T:=5PASSTYPE=0 THEN % PUBLIC USER?
033732             IF A BIT 5EXCLUDE THEN ENAUTHORISED; GO FAR ERET FI
033736          FI
033736          IF X.SPREF><0 AND A><RTREF GO FAR EESPRES % RESERVED FOR SPECIAL USE BY ANYONE ELSE?
033743          X=:D:=5PRDESCR.MESSBUFF
033746          CALL GCPUDF; CALL ERRFATAL
033750          IF A.SPREF=RTREF AND X><D THEN            % CPU CHANGED AND OLD CPU-DF RESERVED FOR SPECIAL USE BY THE CALLER?
033757             CALL RELCPU                            % RELEASE OLD CPU-DF FROM SPECIAL USE
033760          FI
033760          5D12-1*5CPUDFSZ+"S5CPUDF"=:X
033765          A:=X.CPUNO; T:=5MBBANK; X:=5PRDESCR.MESSBUFF
033771          *IOF
033772          *AAX 5CPUN; STATX                         % SAVE ADDR OF NEW CPU-DF IN MESSAGE
033774          *AAX 5MSFL-5CPUN; LDATX                   % SET CPU-BOUND PROCESS
033776          A BONE 5CPUBOUND; *STATX
034000          *ION
034001          GO FAR 5OKRET; *)FILL
034020
034020   %-------------------------------------------------------------------------------
034020   ISSTDOM:          % FUNCTION=145: START SYSTEM DOMAIN FROM SIN3 COMMAND
034020          0=:5P1; GO FAR 5NOPAR; *)FILL              % MARK THAT RETURN TO SIN3 OPCOM ON ESCAPE
034023
034023   %-------------------------------------------------------------------------------
034023   ILI5TQU:     % FUNCTION=150: LIST ND-500 TIME-QUEUE
034023          K:=1; GO ILI5F; *)FILL                    % K=ROUTINE SWITCH
034025
034025   % ILI5EXQ:     K=0 -> FUNCTION=133: LIST ND-500 EX-QUEUE
034025   % ILI5TQU:     K=1 -> FUNCTION=150: LIST ND-500 TIME-QUEUE
034025   % Must both access X5PROC instead of C5PROC
034025   % and do so using "fool cache" ...
034025   %
034025   %
034025   ILI5F: *IOF                                                % THESE ROUTINES MUST BE EXECUTED IN IOF!
034026          CURPROG.BUFWINDOW=:D; A:=142000                     % PHYS.PAGE OF MON60 BUFFER TO RT-DESCR
034032          X:="WNDBF+WNDBF+174000"; T:=0; *STDTX               % SET PIT ENTRY
034035          IF K THEN
034037             AD:=5ATIME=:LOGBADR.DS0; X+2=:L                  % SAVE 5ATIME FIRST IN BUFFER
034044             X:=X500DF; *AAX X5BTI
034046          ELSE
034047             CCPUDF=:B; CALL GETC5PROC
034052             A=:"N500DF".LOGBADR.S0; X+1=:L                   % SAVE CURRENT ACTIVE PROC FIRST IN BUFFER
034057             X:=MAILINK; *AAX X5BEX                           % D=FIRST FREE ADDR IN BUFFER; X=START OF EX-QUEUE
034061          FI
034061          DO
034061             T:=5MBBANK; *LDDTX
034063          WHILE D><-1                                         % -1 IS END OF EX-QUEUE AND TIME-QUEUE
034066   *NNC40,   CNVBYADR
034071             IF X:=D><DUMMESS THEN
034075                T:=A; *SENDE@3 LDATX
034077                IF A=:D><-1 THEN                                 % PROC.NO=-1 IS HISTOGRAM OR WATCHDOG MESS
034103                   IF K THEN                                     % LIST-TIME-QUEUE
034105                      X:=:L; A=:X.S0; X+1:=:L                    % PROCESS NUMBER
034111                      T:=5MBBANK; *AAX D5TIM; LDDTX; AAX -D5TIM  % START-TIME OF PROC
034115                   ELSE                                          % LIST-EXEC-QUEUE
034116                      T:=5MBBANK; *AAX 5PRIO; LDATX; AAX -5PRIO
034122                      A:=:D
034123                   FI
034123                   X:=:L; AD=:X.DS0; X+2:=:L
034127                FI
034127             FI
034127   ILIEOD:OD
034130          -1=:L.S0                                            % -1 IN BUFFER IS END OF DATA
034133          "N500DF"=:B
034135          *ION
034136          A:=X-LOGBADR SH 1 + 2                               % NUMBER OF BYTES TO MOVE
034142          T:=5P1; CALL TOUSMOVE                               % COPY DATA (EX-QOUE OR TIME-QUEUE), TO USER
034144          GO FAR 5OKRET; *)FILL
034157
034157   %-------------------------------------------------------------------------------
034157   IDBUGSW:     % FUNCTION=154: DEBUG SWAPPER <ON/OFF>
034157          IF 5D12=1 THEN CALL XTUSON; GO FAR ERET FI
034165          GO FAR 5NOPAR; *)FILL
034171
034171   %-------------------------------------------------------------------------------
034171   IN5SEGLOAD:  % FUNCTION=160: LOAD (PLACE), ONE SEGMENT (NEW DOMAIN FORMAT)
034171          A:=5P1; T:=12; CALL FRUSMOVE              % COPY SEGMENT NAME FROM USER TO MON60 BUFFER
034174          IF 5D41><0 THEN                           % ANY ND-100/ND-500 SHARED PARTS?
034176             A:=5P4; T:=300; X:=5; CALL XFRUSMOVE   % YES, COPY SHARD-INFO FROM USER TO MON60 BUFFER
034202          FI; GO FAR 5NOPAR; *)FILL
034207
034207   %-------------------------------------------------------------------------------
034207   INTEGER IP1
034210   INTEGER ARRAY PIP:=(IP1)
034211
034211   ICPUSTAT:    % FUNCTION=173: SET CPU STATUS
034211          IF 5D11><0 GO FAR EEILPAR
034214          IF 5D12-1>>NCPU-1 GO FAR EEILPAR
034222          IF 5D41><0 OR 5D42>>3 GO FAR EEILPAR
034230          5D12-1*5CPUDFSZ+"S5CPUDF"+"CPUAVAILABLE"=:X
034236          IF 5D22><0 THEN
034240             T:=5IDPIT; CALL GET1L; CALL ERRFATAL; A=:T
034244             5D42; AD SHZ -1; *BLDA 170 DD
034247             IF A=0 THEN
034250                *5NOTP@3 BSTA DT
034251             ELSE
034252                *5EXCL@3 BSTA DT
034253             FI
034253             T=:A:=5IDPIT; CALL PUT1L; CALL ERRFATAL
034257             "A5PIT"=:D; CALL DALTON                      % SET 5PIT AS ALT.PIT
034262             5IDPIT=:IP1; "PIP"; *MON 2WSEG
034266             CALL ALTOFF
034267          FI
034267          IF 5D32><0 THEN
034271             T:=5SDPIT; CALL GET1L; CALL ERRFATAL; A=:T
034275             5D42; AD SHZ -1; *BLDA 170 DD
034300             IF A=0 THEN
034301                *5NOTP@3 BSTA DT
034302             ELSE
034303                *5EXCL@3 BSTA DT
034304             FI
034304             T=:A:=5SDPIT; CALL PUT1L; CALL ERRFATAL
034310             "A5PIT"=:D; CALL DALTON                      % SET 5PIT AS ALT.PIT
034313             5SDPIT=:IP1; "PIP"; *MON 2WSEG
034317             CALL ALTOFF
034320          FI
034320          GO FAR 5OKRET; *)FILL
034335
034335   %-------------------------------------------------------------------------------
034335   DOUBLE IOFEXIT(0); *IOF; EXIT
034337   % COMMON POINT TO GO THROUGH BEFORE CALLING THE SYSTEM MONITOR
034337   5NOPAR: A:=5PRDESCR-"S500S"=:D:=0; T:=5PRDSZIE; *RDIV ST   % COMPUTE PROCESS NUMBER
034345          T:=RTREF.RSEGM=:5RSEGM; 0=:X.RSEGM                  % SAVE USER'S RSEGM AND SET RSEGM=0
034351          A-1*2+"F5DSG"=:T; CALL M1MEXY; T=:5OLDSEG           % GET IN ND-500 DATA SEGMENT
034357          IF N5FUNCTION><N5REL THEN
034363             IF A=N5RES OR 5FUNCTION=N5RES OR A=MRESSPES OR A=SSTDOM THEN
034400                "DASEGSTART+DSEGSIZE-2"=:L; A:=0=:D
034404                X:="DASEGSTART"; DO AD=:X.DS0; WHILE X<<L; X+2; OD % CLEAR ND500 DATA SEGMENT
034412                X:="DFIX5DSPAGE"; AD:=IOFEXIT=:X.DS0               % SET UP IOF;EXIT
034415             FI
034415          FI
034415          IF 5FUNCTION=N5RES THEN 5P1=:SGBRKADR FI
034423   NOPA1: IF 5FUNCTION=WISON OR A=TSTFUNC THEN
034432             SBUFFR=:B; D:=0; X:="S500S"
034436             DO WHILE D<<=MX5PROCS
034441                X.RTRES=:S1; B+1; D+1; X+5PRDSIZE             % COPY RTRES OF ALL PROCS TO SBUFFR ON ND-500 DATA SEGM
034446             OD; A:=D=:SBUFFER.S0                             % NUMBER OF ND-500 PROCS IN THIS SYSTEM
034452             "N500DF"=:B
034454          FI
034454          N5FUNCTION SH 10\/5FUNCTION=:5FUNCTION
034460          T:="S500DF"; A:="N500DF+ZPREG"=:D; A:=5DFSIZE=:L
034465          *MOVAA                                              % SAVE MONCALL INFO ON ND-500 DATA SEGM.
034466   TOSYMON: *IOF
034467          X:=RTREF; CALL BRELEASE; *ION                       % RELEASE N500DF DATAFIELD
034472          IF "S500DF-ZPREG".5FUNCTION/\377=FORGET THEN        % STOP-ND-500?
034500             CALL CFORGET
034501          FI
034501          IF BACKGROUND><0 THEN CALL ESCON FI                 % SET ESCAPE ON.
034504          CALL FPT2ENTRY                                      % ENTER ND-500 SYSTEM MONITOR
034505          GO FAR 5PT2RET                                      % NORMAL RETURN (ERROR AND OK)
034506          GO FAR SYMNLOAD                                     % ERROR, SYSTEM MONITOR NOT LOADED
034507   *)FILL
034534
034534   %==========================================================================
034534   %      ( 5 )    F U N C S
034534   %
034534   % TABLE OF ENTRY POINTS TO GO TO AFTER RETURNING FROM THE SYSTEM MONITOR
034534   % THE "MON 60" FUNCTION CODE IS INDEX IN THIS TABLE.
034534   @ICR
034534   INTEGER ARRAY FUNCS:=(
034534          FRREG,RET5,FPMREAD,FDMREAD,RET5,RET5,RET5,RET5,     % 000 - 007
034544          FRRGS,RET5,FPRSTART,FCONNFI,RET5,FRES5,FREL5,RET5,  % 010 - 017
034554          RET5,RET5,RET5,FRCNT,RET5,RET5,FDMEXA,RET5,         % 020 - 027
034564          FPMEXA,RET5,FDAMR,RET5,RET5,RET5,RET5,RET5,         % 030 - 037
034574          RET5,FRSTATU,FABRELS,RET5,RET5,RET5,RET5,RET5,      % 040 - 047
034604          RET5,FRIREG,RET5,RET5,RET5,RET5,RET5,FRMICV,        % 050 - 057
034614          FLIMEM,RET5,RET5,RET5,RET5,RET5,RET5,FSPRTE,        % 060 - 067
034624          FGPRTE,FSSGTE,FGSGTE,FRPHSG,RET5,RET5,RET5,RET5,    % 070 - 077
034634          RET5,RET5,FFORGET,RET5,RET5,FSPRIOR,RET5,0,         % 100 - 107
034644          RET5,RET5,RET5,RET5,RET5,RET5,FXN5REL,RET5,         % 110 - 117
034654          RET5,FRSWPDATA,RET5,RET5,RET5,RET5,RET5,RET5,       % 120 - 127
034664          FSFSYDOM,RET5,RET5,RET5,RET5,FABLOG,FPRACTIVE,0,    % 130 - 137
034674          RET5,RET5,RET5,RET5,RET5,FSSTDOM,RET5,FREL3,        % 140 - 147
034704          RET5,RET5,RET5,RET5,RET5,RET5,FNRMICV,FRCNT,        % 150 - 157
034714          RET5,RET5,RET5,RET5,RET5,RET5,FRDTRACE,RET5,        % 160 - 167
034724          FGCPUTYPE,RET5,FRSCRREG,RET5,RET5,RET5,RET5,RET5);  % 170 - 177
034734   @CR;
034734
034734   %-------------------------------------------------------------------------------
034734   % RETURN FROM REENTRAN SYSTEM MONITOR HERE.
034734   SYMNLOAD: ENOCPU=:"S500DF-ZPREG".ZAREG                     % ERROR, SYSTEM MONITOR NOT INITIALIZED
034737   5PT2RET: "N500DF"=:B; *IOF
034742   RESAGAIN: X:=RTREF; CALL BRESERVE                          % TRY TO RESERVE N500DF
034744          IF A<0 THEN                                         % ALREADY RESERVED?
034745             CALL FREXQU; CALL TOWQU; CALL ANTIJAMMER         % YES, WAIT FOR IT
034750             "STUPR"; *IRW MLEVB DP
034752             MLEV; *MST PID; ION
034755             GO 5PT2RET                                       % TRY TO RESERVE IT AGAIN (SHOULD HAVE IT NOW!)
034756          FI; *ION
034757          T:="ZPREG"+B; A:=5DFSIZE=:L; "S500DF"=:D
034765          *MOVAA                                              % COPY SAVED MONCALL INFO TO N500DF
034766          CALL SUPDWINDOW                                     % SET WINDOW ADDR TO MON60 BUFFER
034767          X:=5PRDESCR.MESSBUFF
034771          CALL GCPUDF; CALL ERRFATAL; A=:CCPUDF               % ACTUAL ND-500 CPU DF.
034774          5RSEGM=:RTREF.RSEGM; T:=5OLDSEG; CALL M1MEXY        % GET USER'S ORIGINAL SEGMENTS
035001          IF 5FUNCTION=SSTDOM THEN                            % START STANDARD-DOMAIN FROM S3 OPCOM
035005             IF ZAREG=ESCSTOP GO FAR FROMESC                  % ESCAPE WHILE USING SWAPPER
035011             CALL ESCOFF; CALL ALLRELEASE                     % SET ESCOFF AND RELEASE ALL ND-500 RESOURCES
035013             A:=5PRDESCR; CALL X5DCN                          % DISCONNECT XMSG
035015             X:=5TTIFIELD; *IOF
035017             CALL CHDFPAGE; X.FLAGB BZERO 5ESC2SET=:X.FLAGB   % CLEAR DELAYED ESCAPE INFO
035023             5BCOM=:X.BSTATE                                  % SET S3 OPCOM MODE
035025             CALL CLINONMSG                               % CLEAR IN5MSG/ON5MSG
035026             X:=RTREF; 5PRDESCR=:B; CALL BRELEASE             % RELEASE ND-500 PROC.
035032             "N500DF"=:B; GO MXRET                            % RETURN TO S3 OP.COM AFTER MON60
035035          FI
035035          IF ZAREG><0 THEN                                    % ERROR RETURN?
035037             IF A=ESCSTOP GO FAR FROMESC                      % YES, IS IT ESCAPE WHILE USING SWAPPER?
035042             IF 5FUNCTION><N5REL AND A><N5RES AND A><XN5REL AND A><ABREL GO FAR RET5
035057          FI; X:=5FUNCTION; *1BANK
035061          FUNCS(X); *2BANK
035063          A=:P                                                % GO TO ROUTINE ACCORDING TO
035064                                                              % "MON 60" FUNCTION CODE
035064   *)FILL
035117
035117
035117
035117   %-------------------------------------------------------------------------------
035117   FRREG:       % FUNCTION=000: READ A REGISTER
035117          X:=5P2; AD:=5DD2; CALL STDS0                    % COPY REGISTER VALUE TO USER
035122          GO FAR RET5
035123
035123   %-------------------------------------------------------------------------------
035123   FRSWPDATA:   % FUNCTION=121: READ FROM SWAPPERS DATA MEMORY (LOGICAL ADDRS)
035123   FPMREAD:     % FUNCTION=002: LOGICAL PROGRAM MEMORY READ
035123   FDMREAD:     % FUNCTION=003: LOGICAL DATA MEMORY READ
035123   FDAMR:       % FUNCTION=032: PHYSICAL DATA MEMORY READ
035123          X:=5P4; AD:=5DD4; CALL STDS0                    % COPY NO. OF BYTES READ TO USER
035126          T:=5P3; A:=5D42; CALL TOUSMOVE                  % COPY THE READ DATA TO USER
035131          GO FAR RET5
035132
035132   %-------------------------------------------------------------------------------
035132   FRRGS:       % FUNCTION=010: READ ALL REGISTERS
035132          A:=NREGS SHZ 2; T:=5P1; CALL TOUSMOVE           % COPY REGISTER CONTETNTS TO USER
035136          GO FAR RET5
035137
035137   %-------------------------------------------------------------------------------
035137   FPRSTART:    % FUNCTION=012: START ND-500 PROGRAM
035137   IFPRSTART:
035137          X:=5P1; AD:=5DD1; CALL STDS0                    % COPY STOP-REASON TO USER
035142          T:=5P2; A:=200; CALL TOUSMOVE                   % COPY "STOP (TRAP) INFO" TO USER
035145          GO FAR RET5
035146
035146   %-------------------------------------------------------------------------------
035146   FCONNFI:     % FUNCTION=013: CONNECT FILE
035146          X:=5P5; AD:=5DD5; CALL STDS0                    % COPY OPEN-FILE NUMBER TO USER
035151          GO FAR RET5
035152
035152
035152   %-------------------------------------------------------------------------------
035152   FRES5:       % FUNCTION=015: RESERVE ND-500 PROCESS
035152          T:=5P2; A:=11; CALL TOUSMOVE
035155          GO FAR RET5
035156
035156   %-------------------------------------------------------------------------------
035156   FXN5REL:     % FUNCTION=116: LOG OFF OWN PROCESS
035156   FREL5:       % FUNCTION=016: RELEASE ND-500 PROCESS
035156          A:=5PRDESCR; CALL X5DCN                             % CLOSE XMSG PORTS
035160          T:=5MBBANK; X:=5PRDESCR.MESSBUFF; *AAX 5MSFL; LDATX
035165          IF A BIT 5IBRK AND 5PRDESCR.PSTAT/\5RUNSTATUS><5USRBRK THEN
035175             % --- IN "BREAK" STATUS AND NOT RETURN TO S3 OP.COM
035175             IF A=5IDLE GO FAR RET5                           % YES, IS PROCESS IDLE?
035200             *IOF                                             % NO
035201             X.PSTAT/\5CLRUSTATUS+5IDLE=:X.PSTAT              % SET PROC. IDLE
035205             *ION
035206             GO FAR IFPRSTART                                 % COPY STOP-REASON AND STOP-REASON INFO TO USER
035207          FI
035207          GO IFABREL; *)FILL
035221
035221   INTEGER CTTIFIELD
035222
035222   %-------------------------------------------------------------------------------
035222   FRCNTS:      % FUNCTION=023: READ CONTROL STORE (equal for func=157)
035222          T:=5P3; A:=5D22 SH 1; CALL TOUSMOVE                % COPY CONTROL STORE DATA TO USER
035226          GO FAR RET5
035227
035227   %-------------------------------------------------------------------------------
035227   FSFSYDOM:    % FUNCTION=130: START STANDARD DOMAIN
035227   FSPRTE:      % FUNCTION=067: READ PROCESS ENTRY FROM SYS.MON
035227   FDMEXA:      % FUNCTION=026: DATA MEMORY EXAMINE (4 BYTES)
035227   FPMEXA:      % FUNCTION=030: PROGRAM MEMORY EXAMINE (4BYTES)
035227          X:=5P2; AD:=5DD2; CALL STDS0
035232          GO FAR RET5
035233
035233   %-------------------------------------------------------------------------------
035233   FRSTATU:     % FUNCTION=041: READ ND-500 INTERFACE STATUS
035233          X:=5P1; AD:=5DD1; CALL STDS0
035236          X:=5P2; AD:=5DD2; CALL STDS0
035241          X:=5P3; AD:=5DD3; CALL STDS0
035244          GO FAR RET5
035245
035245   %-------------------------------------------------------------------------------
035245   FABREL:      % FUNCTION=042: LOG OFF PROC. AND TERMINATE ND-100 PROGR.
035245   IFABREL:
035245          IF BACKGROUND><0 THEN CALL ESCOFF FI                % DISABLE ESCAPE
035250          CALL ALLRELEASE                                     % RELEASE ALL ND-500 LOCS.
035251          CALL RELREFS                                        % RELEASE ALL LOGGING FACILITIES.
035252          IF 5FUNCTION><FORGET THEN
035256             *IOF
035257             X:=5PRDESCR.MESSBUFF; CALL FRQUES                % REMOVE MESS FROM ANY QUEUE
035262             "N500DF"=:B
035264             *ION
035265          FI
035265          IF BACKGROUND><0 THEN
035267             X:=5TTIFIELD; *IOF
035271             CALL CHDFPAGE                                    % MAKE SURE THAT TERM.WINDOW PAGE IS IN PIT
035272             X.FLAGB BZERO 5ESC2SET BONE 5ESCON=:X.FLAGB      % CLEAR DELAYED ESCAPE AND SET ESCAPE ON
035276             X=:CTTIFIELD
035277          FI; X:=RTREF; 5PRDESCR=:B
035302          MLEV; *MCL PIE; ION
035305          CALL BRELEASE                                       % RELEASE PROCESS DESCR.
035306          "N500DF"=:B
035310          IF 5FUNCTION><FORGET THEN
035314             IF A><XN5REL AND A><ABREL THEN
035322                T:=5MBBANK; X:=5PRDESCR.MESSBUFF; *AAX 5MSFL; LDATX
035327                IF A NBIT 5IBRK GO FAR RET5
035331             FI
035331             T:=5MBBANK; X:=5PRDESCR.MESSBUFF
035334             *AAX 5ERRF; LDATX                                % ABORT-BATCH ABORT-JOB?
035336             IF A=1 THEN                                      % YES
035341                X:=RTREF; CALL BRELEASE                       % RELEASE N500DF
035343                T:=5MBBANK; X:=5PRDESCR.MESSBUFF
035346                *AAX 5BRG; LDATX; COPY SA DB                  % SET B-REG
035351                *AAX LRET-5BRG; LDXTX                         % X=ROUTINE ADDR TO ENTER
035353                "M5BABORT"; *IRW MLEVB DP
035355                MLEV; *MST PID; MST PIE
035360             FI
035360          FI
035360          IF 5FUNCTION=FORGET OR A=XN5REL
035365          OR 5PRDESCR.PSTAT BIT 5SYSABORT OR A BIT SOFFLOGG THEN
035375             T:=5BUSER
035376          ELSE
035377             IF 5FUNCTION=N5S3REL THEN T:=5ND5ESC ELSE T:="5BUSER+1" FI
035406          FI
035406          IF BACKGROUND><0 THEN
035410             *IOF
035411             T=:CTTIFIELD.BSTATE                              % SET BSTATE IN DATAFIEL
035413             "YBRTWT"; GO FAR SMLVROUTINE                     % START ROUTINE ON MONITOR LEVEL
035415          FI; MLEV; *MST PIE
035417          GO FAR RET5
035420   *)FILL
035441
035441   %-------------------------------------------------------------------------------
035441   FRIREG:      % FUNCTION=051: READ INTERFACE (COMMUNICATION), IODATUT REGISTER
035441   FRMICV:      % FUNCTION=057: READ MICRO PROGRAM VERSION
035441          X:=5P1; AD:=5DD1; CALL STDS0
035444          GO FAR RET5
035445
035445   %-------------------------------------------------------------------------------
035445   FLIMEM:      % FUNCTION=060: LIST MEMORY CONFIGURATION
035445          T:=5P1; A:=74; CALL TOUSMOVE                    % COPY MEMORY CONF. TABLE TO USER
035450          GO FAR RET5
035451   %-------------------------------------------------------------------------------
035451   FGPRTE:      % FUNCTION=070: READ A PROCESS TABLE ENTRY FROM THE SYS.MON
035451          A:=250; T:=5P2; CALL TOUSMOVE; GO FAR RET5
035455
035455
035455   %-------------------------------------------------------------------------------
035455   FSSGTE:      % FUNCTION=071: SEARCH FOR AND READ A PHYS.SEGMENT TABLE ENTRY FROM SYS.MON.
035455          A:=154; T:=5P2; CALL TOUSMOVE; GO FAR RET5
035461
035461   %-------------------------------------------------------------------------------
035461   FGSGTE:      % FUNCTION=072: READ A PHYS.SEGMENT TABLE ENTRY FROM SYS.MON
035461          A:=154; T:=5P2; CALL TOUSMOVE; GO FAR RET5
035465
035465   %-------------------------------------------------------------------------------
035465   FRPHSG:      % FUNCTION=073: READ FROM A PHYSICAL SEGMENT
035465          X:=5P5; AD:=5DD5; CALL STDS0
035470          A:=5D32; T:=5P4; CALL TOUSMOVE; GO FAR RET5
035474
035474   %-------------------------------------------------------------------------------
035474   FFORGET:     % FUNCTION=102: STOP-ND-50
035474          5MSINIT BZERO 5INBUF=:5MSINIT
035477   %      CALL RELMBPAGES; 0/\0
035477   %      XN5REL=:5FUNCTION
035477          GO FAR IFABREL
035500
035500   %-------------------------------------------------------------------------------
035500   FSPRIOR:     % FUNCTION=105: SET PRIORITY
035500          *IOF
035501          5DD2
035502          IF A=-1 AND D=T THEN
035507             X:=5PRDESCR
035510          ELSE
035511             A:=D-5SWPROC*5PRDSIZE+"S500S"=:X
035516          FI
035516          T:=5MBBANK; X:=X.MESSBUFF; *AAX 5MSFL; LDATX; AAX -5MSFL
035523          IF A BIT 5IEXQUEUE THEN
035525             CALL SLOCK; GO FSPERR
035527             CALL GCPUDF; CALL ERRFATAL; A=:B
035532   *NNT41=*
035532             CALL TER500; GO FSPERR
035534             CALL IFM500XQ; CALL ITO500XQ
035536             CALL SUNLOCK
035537             CALL ACTRDY
035540             CALL LOWACT500
035541             "N500DF"=:B
035543          FI
035543          *ION
035544   FSPOUT:GO RET5
035545   FSPERR:CALL RSTARTALL; GO FSPOUT; *)FILL
035574
035574
035574   %-------------------------------------------------------------------------------
035574   %%FMRELSPES:   % FUNCTION=123: RELEASE ND-500 AND MEMORY USED BY THE ND-500 TEST SYSTEM (HAREM)
035574   %%      XN5REL=:5FUNCTION; GO FAR IFABREL
035574
035574   %-------------------------------------------------------------------------------
035574   FABLOG:      % FUNCTION=135: LOG OFF PROCESS AND ABORT ND-100 PROGRAM
035574          CALL RELREFS                                    % RELEASE LOGGING FACILITIES
035575          A:=5PRDESCR; CALL X5DCN                             % CLOSE XMSG PORTS
035577          T:=5MBBANK; X:=5PRDESCR.MESSBUFF; *AAX PDCFL; LDATX
035604          IF A><0 THEN                                        % SINTRAN "SYSTEM DETECTED ERROR"?
035605             *IOF
035606             X:=RTREF; 5PRDESCR=:B; CALL BRELEASE             % YES, RELEASE PROC.DESCR
035612             "N500DF"=:B
035614             T:=5MBBANK; X:=5PRDESCR.MESSBUFF; *AAX PDCFL; LDATX % A=MONITOR LEVEL ROUTINE TO START
035621             GO FAR SMLVROUTINE                               % START MLEV ROUTINE
035622          FI; X:=RTREF; CALL FRWQU; CALL FTIMQU               % ABORT ND-100 PROGRAM (SIMULATE ABORT)
035625          GO ABRETXIT
035626   *)FILL
035640
035640   %-------------------------------------------------------------------------------
035640   FSSTDOM:     % FUNCTION=145: START STANDARD DOMAIN FROM S3 OP.COM.
035640          A:=5PRDESCR; CALL X5DCN         % DISCONNECT FROM XMSG
035642          GO FAR RET5
035643   *)FILL
035645
035645   %-------------------------------------------------------------------------------
035645   FREL3:       % FUNCTION=147: ESCAPE TYPED WHILE RUNNING STANDARD DOMAIN STARTED FROM S3 OP.COM.
035645          A:=5PRDESCR; CALL X5DCN; GO FAR IFABREL
035650
035650   %-------------------------------------------------------------------------------
035650   FDBUGSW:     % FUNCTION=154: DEBUG SWAPPER <ON/OFF>
035650          IF 5D12=1 THEN X:=CCPUDF; CALL RELCPU FI
035656          GO FAR RET5
035657
035657   %-------------------------------------------------------------------------------
035657   FNRMICV:     % FUNCTION=156: READ SYSTEM INFO
035657          T:=5P1; A:=134; CALL TOUSMOVE
035662          GO FAR RET5
035663
035663   %-------------------------------------------------------------------------------
035663   FRDTRACE:    % FUNCTION=166: DUMP-TRACE-MEMORY
035663          T:=5P2; A:=5D12 * 24; CALL FAR TOUSMOVE; % 160D bits per trace-word
035667          GO FAR RET5
035670
035670   %-------------------------------------------------------------------------------
035670   FGCPUTYPE:   % FUNCTION=170: READ ND-500 CPU-TYPE AND MIC.VERSION
035670          X:=5P1; AD:=5DD1; CALL STDS0
035673          X:=5P2; AD:=5DD2; CALL STDS0
035676          GO FAR RET5
035677
035677   %-------------------------------------------------------------------------------
035677   FRSCRREG:    % FUNCTION=172: READ HW SCRATCH REGISTER FILE
035677          T:=5P3; A:=5D22*4; CALL FAR TOUSMOVE; % 4 Byte per register
035703          GO FAR RET5
035704
035704   %-------------------------------------------------------------------------------
035704   % COMMON POINT FOR RETURNING TO USER
035704   RET5:  *IOF
035705          IF BACKGROUND><0 THEN
035707             *ION
035710             X:=5TTIFIELD; *IOF
035712             CALL CHDFPAGE
035713             IF 5FUNCTION=SSTDOM THEN 5BCOM ELSE 5BUSER FI
035722             A=:X.BSTATE                                      % SET USER MODE
035723             X.FLAGB BZERO 5ESC2SET=:X.FLAGB                  % CLEAR ESCAPE TYPED WHILE IN ESCOFF
035726          FI
035726          IF 5FUNCTION><FORGET THEN
035732             T:=5MBBANK; X:=5PRDESCR.MESSBUFF
035735             *AAX 5MSFL; LDATX
035737             A BZERO 5IBRK; *STATX                            % CLEAR PSTAT BITS
035741             X:=5PRDESCR; CALL CLINONMSG                      % CLEAR IN5MSG/ON5MSG
035743          FI
035743          GO MXRET
035744
035744   %-------------------------------------------------------------------------------
035744   % OK-RETUR FOR FUNCTIONS NOT USING SYS.MON
035744   5OKRET: MLEV; *MST PIE
035746          0=:"N500DF".ZAREG; B:=X; MIN ZPREG; 0/\0; GO RET5
035754   RBUS
035777
035777
035777   %===================================================
035777   %       ( 5 )    5 A T R A N S
035777   %
035777   % SUBROUTINE TO SET WINDOW TO PHYS.ADDR IN AD-REGS.
035777   % THE DATA-BUFFER WINDOW IS USED.
035777   %
035777   % ENTRY: AD=PHYS.ADDR TO SET WINDOW TO
035777   %
035777   % EXIT:  A= LOGICAL ADDRESS OF BUFFER
035777   %
035777   SUBR 5ATRANS
035777   INTEGER SVT,SVA,SVD,SVX; TRIPLE SVTAD=SVT
036003   5ATRANS: *IOF
036004          TAD=:SVTAD; X=:SVX; AD SHZ 6                        % A=PHYS PAGE
036007          A=:RTREF.BUFWINDOW                                  % SETUP IN RT-DESCR
036011          X:="WNDBF+WNDBF+174000"; T:=0; A=:D:=142000; *STDTX % SET PIT ENTRY
036016          SVD/\1777+"WNDBF*2000"=:SVA                         % A=LOGICAL ADDR.
036022          TAD:=SVTAD; X:=SVX
036024          *ION
036025          EXIT
036026   RBUS
036033
036033
036033   %=====================================================================
036033   %       ( 5 )    5 S G A L T O N
036033   %
036033   % SET THE PAGE TABLE OF SEGMENT IN B-REG AS CURRENT ALTERNATIVE PAGE TABLE
036033   %
036033   SUBR 5SGALTON
036033   INTEGER SVT,SVA,SVD,SVX; TRIPLE SVTAD=SVT
036037   5SGALTON: *IOF
036040          TAD=:SVTAD; X=:SVX
036042          A:=B*5SEGSIZE+SEGSTART=:X; T:=SEGTBANK; *LOGAD@3 LDATX
036050          A SH 1/\3600+"N5PIT+ALEVB+ERNG2"=:RTREF.ACTPRI; *TRR PCR
036056          TAD:=SVTAD; X:=SVX
036060          *ION
036061          EXIT
036062   RBUS
036070
036070
036070   %==========================================================================
036070   %       ( 5 )    S E G M O N C   -   R S G M O N C
036070   %
036070   % EXECUTES MONITOR CALLS NEEDING A SEGMENT TO TRANSFER DATA TO/FROM
036070   %
036070   % SEGMONC:   THE MONITOR CALL REQUIRE STANDARD PARAMETER LISTE
036070   % RSGMONC:   THE MONITOR CALL REQUIRE PARAMETERS IN THE REGISTERS
036070   %
036070   % ENTRY:     X=LOGICAL ADDR OF ND-500 MESSAGE
036070   %            T=SEGMENT NUMBER TO TRANSFER DATA TO/FROM
036070   %            THE MONITOR CALL NUMBER IS FOUND IN L-REG+1 (CALL SEGMONC(XX) FOR EXECUTING MON XX)
036070   %            THE PARAMETERS ARE FOUND IN THE ND-500 MESSAGE (5TADRG+5XRG)
036070   %
036070   SUBR SEGMONC,RSGMONC,5EXMRET
036070   INTEGER CMONINSTR(0); *MON
036071
036071   SEGMONC: X=:B:=L=:5LREG
036074          *1BANK
036075          X.S0; *2BANK
036077          A+CMONINSTR; CALL M1MEXY; T=:SEG5; A=:L             % CHANGE SEGMENT; L=MON.INSTRUCTION TO EXECUTE
036103          A:=B+"5PLIST"                                       % A=PARAMTER LISTE
036105          GO FELLS
036106
036106   RSGMONC: X=:B:=L=:5LREG
036111          *1BANK
036112          X.S0; *2BANK
036114          A+CMONINSTR; CALL M1MEXY; T=:SEG5; A=:L             % CHANGE SEGMENT; L=MON.INSTRUCTION TO EXECUTE
036120          TAD:=5TADRG; X:=5XRG                                % T,A,D,X REGISTERS ARE MONITOR CALL PARAMETERS
036122   FELLS: GO 5EXMONC                                          % IN MICRCO COMMON
036123   5EXMRET:                                                   % RETURN FROM MICRO COMMON HERE
036123          TAD=:5TADRG; X=:5XRG                                % SAVE RETURN VALUES FROM MON.CALL
036125          T:=SEG5; CALL M1MEXY                                % GET IN ND-500 DATA SEGMENT AGAIN
036127          5LREG=:L; IF K NBIT THEN L+1 FI                     % SKIP-RETURN
036134          TAD:=5TADRG
036135          A=:B:=DBASE:=:B                                     % RESET B, AND RETURN WITH REG.VALUES AS FROM FILSYS.
036140          EXITA
036141   RBUS
036146
036146
036146   %=============================================================================
036146   %       ( 5 )    5 P U T L
036146   %
036146   % INSERT VALUE IN ONE LOCATION OF A SEGMENT
036146   %
036146   % ENTRY:     A=VALUE
036146   %            X=ADDRESS IN SEGMENT
036146   %            T=SEGMENT NUMBER
036146   %
036146   % EXIT:      ERROR
036146   %
036146   % EXIT+1:    OK
036146   %
036146   SUBR 5PUTL,1PUGT
036146   5PUTL: X:=:L; X=:5LREG; A=:B
036151          CURPROG.ACT2SEG=:D; 0=:X.ACT2SEG; A:=B; T=:B
036157          X:=L; CALL M1MEXY; CALL 5SGALTON      % CANGE SEGMENT AND SET NEW SEGM'S PIT AS APT
036162          A=:B=:X.S0
036164   1PUGT: CALL ALTOFF; X=:L; A:=D=:CURPROG.ACT2SEG; X:=L
036172          CALL M1MEXY; 5LREG=:L; DBASE; A:=:B; EXITA
036200   RBUS
036206
036206
036206   %============================================================================
036206   %       ( 5 )    5 G E T L
036206   %
036206   % GET ONE LOCATIOIN FROM A SEGMENT
036206   %
036206   % ENTRY:     X=ADDRESS
036206   %            T=SEGMENT
036206   %
036206   % EXIT:      ERROR
036206   %
036206   % EXIT+1:    A=VALUE OF LOCATION
036206   %
036206   SUBR 5GETL
036206   5GETL: A:=L=:5LREG
036210          X=:L; CURPROG.ACT2SEG=:D; 0=:X.ACT2SEG; X:=L
036216          T=:B; CALL M1MEXY; CALL 5SGALTON      % CHANGE SEGM, AND SET NEW SEGM'S PT AS APT
036221          X.S0=:B; GO 1PUGT                         % GET IN ND-500 DATA SEGM AGAIN
036224   RBUS
036231
036231
036231   %====================================================================
036231   %       ( 5 )    G E T C A P A B I L I T Y
036231   %
036231   % ROUTINE TO READ A CAPABILITY TABLE FROM A ND-500 NDATA SEGMENT
036231   %
036231   % ENTRY:     D=SOURCE ADDRESS
036231   %            T=ND-500 SEGMENT NUMBER TO COPY FROM
036231   %            X=LOGICAL ADDRESS OF ND-500 MESSAGE
036231   %            A=NUMBER OF WORDS TO READ
036231   %            THE MON60 BUFFER SHOULD BE IN THE WINDOW WNDBF
036231   %
036231   % EXIT:      T,A,D REGISTERS ARE DESTROYED
036231   %            THE CAPABILITY TABLE ARE IN THE MON60 BUFFER
036231   %
036231   SUBR GETCAPABILITY
036231   GETCAPABILITY:
036231          A=:B:=L=:X."LRET"
036234          CALL M1MEXY; T=:X.SEG5; X=:L              % SWITCH SEGMENT; SAVE OLD SEG
036237          A:=RTREF.ACT1SEG*5SEGSIZE+SEGSTART=:X
036244          T:=SEGTBANK; *LOGAD@3 LDATX
036246          A SH 1; T:="WNDBF*2000"; X:=B; L=:B       % A=NEW.SEG'S PIT IN ALT.PIT POSITION
036252          K:="0"; CALL MOVUS
036254          T:=SEG5; CALL M1MEXY                      % GET ORIGINAL ND-500 DATA SEGM.
036256          "LRET"=:L; X:=B; DBASE=:B
036263          EXIT
036264   RBUS
036274
036274
036274   %========================================================================
036274   %       ( 5 )    5 G N S E G   -   5 G S Y D S G
036274   %
036274   % REMOVE THE DATA SEGMENT, GET THE NAME SEGMENT/STANDARD DOMAIN SEGMENT
036274   % SETS PT0 AS ALTERNATIVE PAGE TABLE
036274   %
036274   % ENTRY:     X=ADDRESS OF PROCESS DESCRIPTION
036274   %
036274   % EXIT:      APIT=0
036274   %            T,A,D,X REGISTERS ARE DESTROYED
036274   %
036274   SUBR 5GNSEG,5GSYDSG
036274   TRIPLE 5GSTADRG; INTEGER 5GSXRG
036300   INTEGER CSS5PIT(0); *S5DPT@7+174000+176          % LAST PIT-ENTRY ON S5DPT PIT
036301   5GSYDSG: T:=55SDS
036302          L=:D; CALL FIX5DSPAGE                     % FIX LAST PAGE IN ND-500 DATASEG
036304          TAD=:5GSTADRG; X=:5GSXRG                  % FIX5DSPAGE RETURNS IN IOF
036306          X:=175776; T:=0; *LDDTX; STZTX            % GET PIT ENTRY FOR LAST PAGE IN DATASEG, AND CLEAR PIT ENTRY
036312          X:=CSS5PIT; *STDTX                        % COPY PIT.ENTRY TO STANDARD DOMAIN SEG PIT.TAB ENTRY
036314          D SH 2=:X; T:=CORMBANK
036317          A BONE 5FIX; *PROTE@3 STATX               % FIX PAGE
036321          TAD:=5GSTADRG
036322          CALL M1MEXY                               % SWITCH SEGMENT
036323          "N5PIT+AS5DP+ALEVB+ERNG2"=:RTREF.ACTPRI; *TRR PCR  % SET ALT.PIT=14
036327          X:=5GSXRG
036330          D=:P                                      % EXIT
036331
036331   INTEGER CNS5PIT(0); *X5DPT@7+174000+176          % LAST PIT-ENTRY ON X5DPT PIT
036332   5GNSEG: T:=55NSG
036333          L=:D; CALL FIX5DSPAGE                     % FIX LAST PAGE IN ND-500 DATASEG
036335          TAD=:5GSTADRG; X=:5GSXRG                  % FIX5DSPAGE RETURNS IN IOF
036337          X:=175776; T:=0; *LDDTX; STZTX            % GET PIT ENTRY FOR LAST PAGE IN DATASEG, AND CLEAR PIT ENTRY
036343          X:=CNS5PIT; *STDTX                        % COPY PIT.ENTRY TO NAME.SEG
036345          D SH 2=:X; T:=CORMBANK
036350          A BONE 5FIX; *PROTE@3 STATX               % FIX PAGE
036352          TAD:=5GSTADRG
036353          CALL M1MEXY                               % SWITCH SEGMENT
036354          "N5PIT+AX5DP+ALEVB+ERNG2"=:RTREF.ACTPRI; *TRR PCR  % SET ALT.PIT=13
036360          X:=5GSXRG
036361          D=:P                                      % EXIT
036362   RBUS
036371
036371
036371   %===========================================================================
036371   %       ( 5 )    5 G D S E G
036371   %
036371   % REMOVE THE NAME SEGMENT/STANDARD DOMAIN SEGMENT, GET THE DATA SEGMENT
036371   % RESET THE ALTERNATIVE PAGE TABLE TO DPIT
036371   %
036371   % ENTRY:     X=D= ADDRESS OF PROCESS DESCRIPTION
036371   %
036371   % EXIT:      T,A,D,X REGISTERS ARE DESTROYED
036371   %
036371   SUBR 5GDSEG
036371   INTEGER 5GDXRG
036372   5GDSEG: "N5PIT+ADPIT+ALEVB+ERNG2"; *IOF; TRR PCR  % RESET ALT.PIT TO DPIT
036375          X=:5GDXRG; A=:RTREF.ACTPRI
036400          IF X.ACT1SEG=55NSG THEN "X5DPT*200+176" ELSE "S5DPT*200+176" FI
036407          X:=174000+A; T:=0; *LDDTX; STZTX          % A=PIT ENTRY; D=PHYS PAGE
036414          A BZERO 5FIX                              % RESET FIX BIT (PAGE NO LONGER FIXED)
036415          D SH 2=:X; T:=CORMBANK; *PROTE@3 STATX
036421          A:=5GDXRG-"S500S"=:D:=0; T:=5PRDSIZE; *RDIV ST
036427          A-1*2+"F5DSG"=:T; GO M1MEXY               % SWITCH SEGMENT AND RETURN AFTER CALL TO 5GDSEG!
036434   RBUS
036446
036446
036446   %============================================================================
036446   %       ( 5 )    R E S N A M S E G
036446   %
036446   % SUBROUTINE TO RESERVE A ND-500 LOCK.
036446   %
036446   % ENTRY:     T=0 : NAME SEGMENT
036446   %            T=1 : LOAD CONTROL STORE FUNCTION
036446   %            T=2 : PLACE SWAPPER FUNCTION
036446   %            T=3 : WAIT FOR FIX-PAGES
036446   %            T=4 : RESERVE CONTROL STORE
036446   %                  RETURN:     CONTROL STORE ALREADY RESERVED
036446   %                  SKIPRETURN: CONTROL STORE IS RESERVED BY CALLER
036446   %            T=5 : RESERVE "PLACE-SWAPPER"
036446   %                  RETURN:     "PLACE-SWAPPER" ALREADY RESERVED
036446   %                  SKIPRETURN: "PLACE-SWAPPER" IS RESERVED BY CALLER
036446   %            T=6 : RESERVE THE SYSTEM-DOMAIN SEGMENT
036446   %            T=7 : RESERVE RTPWORKA FOR RT-PROGRAMS
036446   %            T=10: WRITE ERROR MESSAGE TO SINTRAN ERROR DEVICE
036446   %            T=11: General free lock
036446   %                  RETURN:     LOCK ALREADY RESERVED
036446   %                  SKIPRETURN: LOCK IS RESERVED BY CALLER
036446   %            T=12-21: RESERVE ADDITIONAL CPU DF (WHEN USING ACCP BUFFERS)
036446   %
036446   %
036446   SUBR RESNAMSEG
036446   INTEGER RXRG,RRTRG
036450   RESNAMSEG: A:=L=:5LREG
036452   RESN1: MLEV; *MCL PIE
036454          X=:RXRG; T=:RRTRG
036456          XSEMS(T)=:B                                    % B=ADDRESS OF LOCK DF.
036461          X:=RTREF; CALL BRESERVE
036463          IF A<0 THEN                                    % LOCK OCCUPIED?
036464             IF 4=RRTRG OR 5=T OR 11=T THEN              % YES, SHOULD NOT CALLER WAIT FOR IT?
036476                X:=RXRG; MLEV; *MST PIE                  % LOCK OCCUPIED, BUT CALLER SHOULD NOT WAIT FOR IT
036501                5LREG=:L; GO RESRET                      % NO SKIP RETURN
036504             ELSE
036505                CALL FREXQU; CALL TOWQU; CALL ANTIJAMMER % YES, SET IN WAITING-QUEUE FOR THE LOCK
036510                "STUPR"; *IRW MLEVB DP
036512                X:=RXRG; T:=RRTRG; MLEV; *MST PID; MST PIE
036517                GO RESN1
036520             FI
036520          FI; T:=RRTRG; X:=RXRG                          % LOCK IS RESERVED BY CALLER
036522          MLEV; *MST PIE
036524          5LREG+1=:L                                     % SKIP RETURN
036527   RESRET: DBASE=:B; EXIT
036532   RBUS
036543
036543
036543   %========================================================================
036543   %       ( 5 )    R E L N A M S E G
036543   %
036543   % RELEASE A ND-500 LOCK.
036543   %
036543   % ENTRY:     SAME AS FOR RESNAMSEG
036543   %
036543   SUBR RELNAMSEG
036543   INTEGER RXRG,RRTRG
036545   RELNAMSEG: A:=L=:5LREG
036547          MLEV; *MCL PIE
036551          X=:RXRG
036552          XSEMS(T)=:B                               % B=LOCK DATAFIELD ADDR
036555          IF X:=RTRES=RTREF THEN CALL BRELEASE FI   % RELEASE LOCK IF RESERVED BY THE CALLER
036562          X:=RXRG
036563          MLEV; *MST PIE
036565          5LREG=:L; DBASE=:B; EXITA                  % SKIP RETURN
036572   RBUS
036577
036577
036577
036577   %=========================================================================
036577   %       ( 5 )    A C T D R I V E R
036577   %
036577   % ACTIVATE THE N500 HANDLER  N 5 0 0 M R  ON MONITOR LEVEL
036577   %
036577   % ENTRY:     A=0 MEANS STOP PROCESS; X=PROCESS DESCRIPTION ADDRESS
036577   %            A=ADDRESS OF PROCESS DESCRIPTION
036577   %
036577   SUBR ACTDRIVER
036577   INTEGER CXX, CLL
036601   ACTDRIVER: *IOF
036602          IF A=0 THEN
036603             X=:CXX; A:=L=:CLL                      % STOP-PROCESS (I.E. REMOVE MESS FROM ANY QUEUE)
036606             X:=X.MESSBUFF; CALL FRQUES             % REMOVE MESSAGE FROM ANY QUEUE
036610             X:=CXX; CLL=:L; DBASE=:B
036615             *ION
036616             EXIT
036617          FI; *IRW MLEVB DX                         % SET X-REG ON MLEV=PROCESS DESCR
036620          "N500C"; *IRW MLEVB DP                    % N500C IS ENTRY ON MONITOR LEVE
036622          MLEV; *MST PIE; MST PID; ION
036626          EXIT
036627   RBUS
036632
036632
036632   *"8N500 8F5UD
"036632   %============================================================================
036632   %       ( 5 )    5 D T U P
036632   %
036632   % ROUTINE CALLED FROM THE SYSTEM MONITOR TO UPDATE A COPY
036632   % OF THE DATA-CAPABILITY TABLE AND THE FIXED-INFO TABLE
036632   % THESE TABLES ARE USED IN THE FAST MON UDMA INTERFACE
036632   %
036632   % ENTRY:     X=ND-500 PROCESS NUMBER
036632   %            A=ADDRESS OF FIX-INFO TABLE OR DATA-CAPABILITY TABLE ON THE
036632   %              SYSTEM MONITOR'S DATA SEGMENT
036632   %            T=FLAG; =1: COPY FIX-INFO TABLE
036632   %                    =2: COPY DATA CAPABILITY TABLE
036632   %
036632   % EXIT:      ONLY B-REGISTER IS SAVED
036632   %
036632   SUBR 5DTUP
036632   INTEGER 5DMPY
036633   *"8N500
"036633   5DTUP:
036633   *"8N500 8F5UD
"036633          A=:D
036634   *"8N500 8F5UD -8MTRA
"036634          A:=D
036635          *IOF
036636          IF T-1=0 THEN T:="MFSIZE*MFANT"=:5DMPY:=5FXTBL; GO LABLZ FI  % FIX-INFO TABLE
036645          IF T-1=0 THEN T:=MFNCP=:5DMPY:=5DSPS; GO LABLZ FI            % DATA CAPABILITY
036654          *ION
036655          GO 5DTU9                                  % ERROR
036656   LABLZ: A=:D:=X-1*5DMPY; T+A                      % T=DESTINATION ADDR
036663          X:=5FXBNK; A:=5DMPY:=:L; *ION; MOVAP      % COPY TABLE
036670          A=:L
036671   *"8N500
"036671   5DTU9: EXIT
036672   RBUS
036676
036676
036676   %==============================================================================
036676   %       ( 5 )    E R A B O R T
036676   %
036676   % LOGGOUT PROCESS AFTER ERROR DETECTED BY SINTRAN III MONITOR
036676   % (ERROR IN MONITOR CALL ETC...)
036676   %
036676   % LEVEL 1
036676   %
036676   % ENTRY: B=SINTRAN III MONITOR ROUTINE TO RETURN TO AFTER
036676   %          TERMINATED THE ND-500 PROCESS
036676   %
036676   SUBR ERABORT
036676   ERABORT: *IOF; 2BANK
036700            RTREF=:D; CALL FSEMA; CALL ERRFATAL     % FIND PROCESS DESCRIPTION
036704            CALL SLOCK; 0/\0
036706            T:=5MBBANK; X:=X.MESSBUFF; *AAX 5MSFL; LDATX
036712            A BONE 5IBRK BONE 52ESCSET; *STATX      % MARK THAT PROC IS IN "CLEANING UP" STATUS
036715            A:=B; *AAX PDCFL-5MSFL; STATX           % SAVE MON.LEVEL ADDRESS TO ENTER AFTER CLEANING UP
036720            A:=ABLOG; *AAX SV5FU-PDCFL; STATX       % USE FUNC=ABLOG TO TERMINATE PROCESS
036723            CALL SUNLOCK
036724            *ION
036725            IF BACKGROUND><0 THEN
036727               UEFLG BZERO 5UECM=:UEFLG             % CLEAR UEFLAG BIT 5UECOM
036732            FI; GO FROMESC; *)FILL
036744   RBUS
036744
036744
036744   %=============================================================================
036744   %       ( 5 )    F R O M E S C
036744   %
036744   % ENTRY POINT IN PROGRAMS AFTER ESCAPE OR CLEANING-UP SEQUENCES
036744   %
036744   SUBR FROMESC,FESC1
036744   INTEGER POINTER P3STPNT:=STPNT
036745   INTEGER CCDFSA,CCODE,CCTYPR
036750   FROMESC: *2BANK
036751          *IOF
036752          RTREF=:D; CALL FSEMA; CALL ERRFATAL
036756          X:=X.MESSBUFF; CALL FRQUES
036760          IF BACKGROUND><0 THEN
036762             *IOF
036763             RTREF.DSEGM=:X.DACTSEG                           % CONTINUE WITH ORIGINAL SEGMENTS
036766             "STUPR"; *IRW MLEVB DP
036770             MLEV; *MST PIE; MST PID; ION
036774             X:=5TTIFIELD; *IOF
036776             CALL CHDFPAGE
036777             X.FLAGB BZERO 5ESC2SET=:X.FLAGB; *ION            % CLEAR DELAYED ESCAPE
037003             "STBEG"=:P3STPNT                                 % RESET STPNT IN BFIELD ON SYSTEM SEGMENT
037005             IF 5BCHFLAG><1 THEN                              % IF NOT BATCH
037011                *IOF
037012                CALL CHDFPAGE
037013                IF X.TYPRING NBIT 5BAD AND A BIT M144B THEN T:=-2 ELSE T:=-1 FI
037023                IF A BIT 5SPLITDF THEN
037025                   X=:CCDFSA; T=:CCODE; 0=:X.IN5MSG; X.KSETDV; X:=X.TDRADDR
037032                   T:=CCODE; CALL 5CSETDV                  % CALL SETDV FROM COMMON (RPIT)
037034                   X:=CCDFSA.DFOPP; 0=:X.ON5MSG; X.KSETDV; X:=X.TDRADDR
037041                   T:=CCODE; CALL 5CSETDV                  % CALL SETDV
037043                   GO FESCX; *)FILL
037057                FI; *JMP *
037060   FESCX:       *ION
037061             FI
037061          FI; *IOF
037062          CALL CLINONMSG                   % CLEAR ON5MSG AND IN5MSG
037063          *ION; IOF
037065          RTREF=:D; CALL FSEMA; CALL ERRFATAL
037071          X=:D                                       % X&D=PROCESS DESCRIPTION
037072          *ION
037073          IF BACKGROUND><0 THEN
037075             IF 5TTIFIELD.FLAGB BIT 5LOGOUT THEN     % LOGOUT FROM SINTRAN?
037101                *IOF
037102                D.PSTAT BONE SOFFLOG=:X.PSTAT        % YES, MARK THAT ND-500 PROC SHOULD BE LOGGED OFF
037106                *ION
037107             FI
037107             T:=5MBBANK; X:=D:=X.MESSBUFF
037112             *AAX PDCFL; LDATX
037114             X:=D                                    % X=PROCESS DESCRIPTION
037115             IF A=0 AND UEFLG BIT 5UECM THEN         % ESCAPE WHEN IN UECOM?
037121                 A BZERO 5UECM =: UEFLG
037123                 5SPASSTYPE=:5PASSTYPE
037125                 "STACK"=:5CSTCK
037127                 CALL ALLRELEASE
037130                 *IOF
037131                 CALL SLOCK; 0/\0
037133                 T:=5MBBANK; X:=X.MESSBUFF; *AAX 5MSFL; LDATX
037137                 A BZERO 5IBRK BZERO 52ESCSET; *STATX
037142                 *AAX XADPR-5MSFL; LDXTX
037144                 CALL SUNLOCK
037145                 GO 5RETUECOM
037146             FI
037146          FI
037146          A:=0; GO FESC1; *)FILL                    % NOT ABORT-BATCH/ABORT-JOB
037171
037171   INTEGER CCSEMAPHORE
037172   FESC1:                                           % A=ABORT FLAG (1 IF ABORT-JOB/ABORT-BATCH)
037172          T:=5MBBANK; X:=X.MESSBUFF; *AAX 5ERRF; STATX; AAX -5ERRF
037177          *IOF
037200          CALL FRQUES; CALL ALLRELEASE
037202          *ION; IOF
037204          CALL SLOCK; 0/\0
037206          T:=5MBBANK; *AAX 5MSFL; LDATX             % X=PROCESS DESCR
037211          A BZERO 52ESCSET BONE 5IBRK; *STATX
037214          *AAX XADPR-5MSFL; LDXTX
037216          X=:CCSEMAPHORE
037217          CALL SUNLOCK
037220          MLEV; *MCL PIE; ION
037223          "N500DF"=:B; X:=RTREF; CALL BRESERVE                % RESERVE N500DF
037227          IF A<0 THEN                                         % OCCUPIED?
037230             CALL FREXQU; CALL TOWQU; CALL ANTIJAMMER         % YES, WAIT FOR IT
037233             "STUPR"; *IOF; IRW MLEVB DP
037236             MLEV; *MST PIE; MST PID
037241             X:=CCSEMAPHORE
037242             *ION
037243             GO FESC2; *)FILL
037260          ELSE
037261             X:=CCSEMAPHORE
037262   FESC2: FI; X=:5PRDESCR                                     % SAVE ADDR OF PROC. IN N500DF
037263          MLEV; *MST PIE
037265          A:=5PRDESCR-"S500S"=:D:=0; T:=5PRDSIZE; *RDIV ST
037273          A-1*2+"F5DSG"                                       % A=ND500 DATA SEGM FOR PROCESS
037276          IF A><RTREF.ACT1SEG THEN                            % IS THIS SEGM. THE CURRENT ONE?
037302             0=:X.RSEGM; T:=A; CALL M1MEXY                    % NO, GET IN AS ONE OF THE ACTIVE SEGMS
037305          FI; T:=5MBBANK; X:=5PRDESCR.MESSBUFF
037310          *AAX PDCFL; LDATX
037312          IF A><0 THEN
037313             *AAX SV5FU-PDCFL; LDATX
037315          ELSE
037316             T:="S500DF-ZPREG".5FUNCTION
037320             IF SSTDOM=T THEN N5S3REL ELSE N5REL FI
037326          FI; A=:"S500DF-ZPREG".5FUNCTION=:5FUNCTION          % SET NEW FUNCTION CODE
037331          CALL SUPDWIDOW                                      % SET WINDOW TO MON60 BUFFER
037332          GO FAR TOSYMON
037333   RBUS
037344
037344
037344   %==============================================================================
037344   %       ( 5 )    S 5 T S L T Y P E
037344   %
037344   % SET ND-500 TIMESLICE TYPE FOR ND-500 PROCESS
037344   %
037344   % ENTRY:     A=TIMESLICE TYPE
037344   %            X=PROCESS NUMBER
037344   %
037344   % EXIT:      ALL REGISTERS EXCEPT B ARE USED
037344   %
037344   SUBR S5TSLTYPE
037344   S5TSLTYPE:
037344          *IOF
037345          A/\17 SH 7CUTY=:D
037350          A:=X*5PRDSIZE+"S500S"=:X
037354          IF X.PSTAT BIT SLICE THEN
037357             A BONE 55BRKPRIOR=:X.PSTAT
037361             T:=5MBBANK; X:=X.MESSBUFF; *AAX 5TSLS; LDATX    % A=5TSLSTATUS
037365             A/\170377\/D; *STATX                             % SET NEW TIMESLICE TYPE
037370             *AAX 5PRIO-5TSLS; STZTX                          % SET PRIORITY=0 SO TIMELICER WILL CHANGE PRIORITY
037372          FI; *ION
037373          EXIT
037374   RBUS
037401
037401
037401   %=============================================================================
037401   %       ( 5 )    5 S 1 T S L T Y P E   -   5 R 1 T S L T Y P E
037401   %
037401   % SUBROUTIEN TO SET AND RESET TIMESLICE TYPE
037401   %
037401   % ENTRY:     X=RT-DESCRIPTION ADDRESS
037401   %            A=NEW SLICE TYPE (0-17)
037401   %
037401   % EXIT:      A=0: NO CHANGE DONE
037401   %            A><0 CHANGE IS DONE (NEW OR SAVED CLASS IS SET)
037401   % REGISTER USED:             T,D,L
037401   %
037401   SUBR 5S1TSLTYPE,5R1TSLTYPE
037401
037401   INTEGER POINTER L2RG
037402   INTEGER CTYP,PROG,3SLICE
037405
037405   5S1TSLTYPE: K:="0"; GO FELLX
037407   5R1TSLTYPE: K:=1
037410   FELLX: *IOF
037411          0=:3SLICE
037412          IF T:=X.STATUS BIT 5TSLICED THEN                    % PROGRAM TIMESLICED?
037415             A/\17 SH 7CUTY=:CTYP:=L=:"L2RG"; X=:PROG         % YES, CTYP=NEW TIMESLICE CLASS
037423             CALL GTSLPINDEX; GO UT                           % GET TIMESLICE-PROGRAM INDEX
037425             1=:3SLICE
037427             A:=X*TSLSIZE+GTSLTAB=:X; T:=GLTMBANK; *TSLST@3 LDATX
037435             IF K THEN
037437                A=:T SHZ -4/\7400:=:T/\170377\/T              % SET CURRENT CLASS=SAVED CLASS
037445             ELSE
037446                A SH 4/\170000\/CTYP                          % SET NEW ND-100 TIMESLICE CLASS
037451             FI; T:=GLTMBANK; *TSLST@3 STATX
037453             PROG.STATUS BONE 5ESCF=:X.STATUS
037457   UT:       X:="L2RG"=:L:=PROG
037462          FI; A:=3SLICE
037463          *ION
037464          EXIT
037465   RBUS
037475
037475
037475   *)KILL E5PIT; E5PIT=*
037475
037475   *5SPAS=SPAST
037475   *5CSTC=CSTCK
037475   *5BCHF=BCHFL
037475   *5PASS=PASST
037475   *5TTIF=TTIFI
037475   *"
"037475
037475   @DEV 1
