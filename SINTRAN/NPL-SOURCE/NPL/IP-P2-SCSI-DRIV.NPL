066562   @DEV (S-S-L)IP-P2-SCSI-DRIV:NPL
066562   SUBR SCLLD, SCINT, SCTIO
066562   %
066562   % CALL SEQUENCE
066562   %
066562   %       JPL I (SCLLD
066562   %       JMP BUSY
066562   %       JMP ERROR
066562   %       JMP FIN
066562   %
066562   % ENTRY:
066562   %          A = FUNTION TYPE
066562   %          X = UNIT DATAFIELD
066562   %
066562
066562   % ERROR CONDITIONS
066562   SYMBOL 7SIDE=5               % INITIATOR DETECTED ERROR
066562   SYMBOL 7SMRJ=7               % MESSAGE REJECT
066562   SYMBOL 7SMPE=11              % MESSAGE PARITY ERROR
066562
066562   %..............................
066562   % DEFINITIONS FOR SCSI DRIVER .
066562   %..............................
066562
066562   SYMBOL RLMAR=00              % READ MEMORY ADDRESS REGISTER BITS 14-0
066562   SYMBOL WLMAR=01              % WRITE MEMORY ADDRESS REGISTER BITS 14-0
066562   SYMBOL REDAT=02              % READ DATA
066562   SYMBOL WRDAT=03              % WRITE DATA
066562   SYMBOL RSTAU=04              % READ STATUS
066562   SYMBOL CTBUS=02              % CONTROLLER BUSY
066562   SYMBOL NCRIT=11              % INTERRUPT FROM NCR
066562   SYMBOL SCREQ=15              % REQ FROM SCSI BUS
066562   SYMBOL WCONT=05              % WRITE CONTROL
066562   SYMBOL ENDMA=05              % ENABLE DMA
066562   SYMBOL RHMAR=06              % READ MEMORY ADDRESS REGISTER BITS 23-15
066562   SYMBOL WHMAR=07              % WRITE MEMORY ADDRESS REGISTER BITS 23-15
066562   SYMBOL RNDAT=40              % READ NCR DATA REGISTER
066562   SYMBOL WNDAT=41              % WRITE NCR DATA REGIISTER
066562   SYMBOL RNCOM=42              % READ NCR COMMAND REGISTER
066562   SYMBOL WNCOM=43              % WRITE NCR COMMAND REGISTER
066562   SYMBOL RNCNT=44              % READ NCR CONTROL REGISTER
066562   SYMBOL WNCNT=45              % WRITE NCR CONTROL REGISTER
066562   SYMBOL RDESI=46              % READ DESTINATION ID REGISTER
066562   SYMBOL WDESI=47              % WRITE DESTINATION ID REGISTER
066562   SYMBOL RAUXS=50              % READ AUXILIARY STATUS
066562   SYMBOL DARGF=07              % DATA REGISTER FULL
066562   SYMBOL WAUXS=51              % WRITE AUXILIARY STATUS
066562   SYMBOL ROIDN=52              % READ OWN ID NUMBER
066562   SYMBOL RITRG=54              % READ INTERRUPT REGISTER
066562
066562   SYMBOL ILCOM=16              % ILLEGAL COMMAND
066562   SYMBOL RECON=14              % RECONNECT
066562   SYMBOL CONEC=13              % CONNECT
066562   SYMBOL DISCO=12              % DISCONNECT
066562   SYMBOL BUSSI=11              % BUS SERVICE INTERRUPT
066562   SYMBOL FUCOM=10              % FUNCTION COMPLETE
066562   SYMBOL PARIT=06              % PARITY ERROR
066562   SYMBOL PAUSE=02              % PAUSED
066562   SYMBOL TCZRO=01              % TRANSFER COUNT ZERO
066562
066562   SYMBOL RSOUI=56              % READ SOURCE ID
066562   SYMBOL RDIST=62              % READ DIAGNOSTIC STATUS
066562   SYMBOL SLFCO=07              % SELFTEST COMPLEAT NCR 5386
066562   SYMBOL RTCM=70               % READ TRANSFER COUNTER MOST
066562   SYMBOL WTCM=71               % WRITE TRANSFER COUNTER MOST
066562   SYMBOL RTC2=72               % READ TRANSFER COUNTER 2ND.
066562   SYMBOL WTC2=73               % WRITE TRANSFER COUNTER 2ND.
066562   SYMBOL RTCL=74               % READ TRANSFER COUNTER LEAST
066562   SYMBOL WTCL=75               % WRITE TRANSFER COUNTER LEAST
066562
066562   SYMBOL 6SFUN=17              % FUNCTION STARTED
066562   SYMBOL 6SIDE=16              % SEND IDENTIFY
066562   SYMBOL 6SRST=15              % SCSI BUS RESET
066562   SYMBOL 6SMSO=12              % MESSAGE OUT PENDING
066562   SYMBOL 6SRFD=11              % RETURN ON COMMAND ACCEPTED (I.E. FIRST DISCONNECT)
066562   SYMBOL 6SDIS=10              % DISCONNECT MESSAGE RECEIVED
066562   SYMBOL 6SCCO=7               % COMMAND COMPLETE MESSAGE RECEIVED
066562   SYMBOL 6SMSI=6               % MESSAGE RECEIVED
066562   SYMBOL 6SARB=5               % ARBITRATION PHASE
066562   SYMBOL 6SSBT=4               % SINGLE BYTE TRANSFER
066562   SYMBOL 6SCRP=3               % CONNECT/RECONNECT PHASE
066562
066562   SYMBOL CNCLR=177000          % MASK FOR FLAGS CLEARED ON CONNECT PATH
066562   SYMBOL DCCLR=136400          % MASK FOR FLAGS CLEARED ON DISCONNECT PATH
066562   SYMBOL NPCLR=137000          % MASK FOR FLAGS CLEARED ON NEW PHASE
066562
066562   % SAVED STATUS ON ERROR RESET
066562
066562   DISP -32
066562      INTEGER SEST1, SEST2, SEST3, SEST4, SEST5
066562   PSID
066562
066562   SCLLD: IF D:=A SHZ -10<3 THEN
066567             A=:X.SUCON:=L=:X.SULRG; 0=:X.SUTRG     % CONTROLL AND RETURN ADDRESS
066573             IF NCROK<0 THEN
066575                T:=NCRST; GO FAR EXDRI              % ERROR IN INTERFACE
066577             FI
066577             CALL INITO                             % INITIALIZE OPERATION
066600             IF BUSFL=0 THEN CALL SELEC FI          % START ARBITRATION
066603          ELSE
066604          IF D=3 THEN
066607             A=:X.SUCON:=L=:X.SULRG; 0=:X.SUTRG     % CONTROLL AND RETURN ADDRESS
066613             "0" BONE 6SFUN=:X.SUTHS                % INITIAL STATUS
066616             CALL ENTIM                             % ENABLE TIMER
066617          ELSE
066620          IF D=4 THEN
066623             IF T:=NCROK NBIT 8SRST THEN
066626                X=:SCRXR:=L=:SCRLR             % SAVE REGISTERS
066631                A=:SCRCO; GO FAR SCRST         % PERFORM RESET
066633             ELSE
066634                T:=0; L+1; EXITA               % ALREADY IN PROGRESS
066637             FI
066637          ELSE
066640             T:=ILDCO                          % ILLEGAL FUNCTION
066641          FI FI FI
066641          GO SCWTI                             % BUSY RETURN
066642   *)FILL
066647
066647   % INTERRUPT HANDLER
066647
066647   DOUBLE RBSIR(0); * 1@BUSSI; NEWPH
066651
066651   SCINT: T:=HDEV+RSTAU; *IOXT                 % READ DEVICE STATUS
066654          IF X:=64/\A><0 THEN
066657             IF A BIT 2 GO SCWTI               % CONTROLLER BUSY
066661             IF A=:SCSSR BIT 5 THEN
066664                T:=SBRST; GO FAR SCDIS         % SCSI BUS RESET RECEIVED
066666             FI
066666             IF A BIT 4 THEN
066670                CALL SCIDE; A:=SCSSR           % "INITIATOR DETECTED ERROR"
066672             FI
066672          FI
066672          IF A=:SCSSR BIT 11 THEN              % INTERRUPT FROM NCR
066675             "0"; T:=HDEV+WCONT; *IOXT         % CLEAR TO MEMORY
066701             T+"RAUXS-WCONT"; *IOXT            % READ AUXILIARY STATUS
066703             AD SHZ -10
066704             T+"RITRG-RAUXS"; *IOXT            % READ INTERRUPT REGISTER
066706             AD SHZ -10
066707             A:=D=:SCNIS; 0=:SCCCW             % NEW STATUS
066712
066712             IF A/\177500=SCEIM THEN           % EXPECTED INTERRUPT
066716                CALL SCISR                     % INTERRUPT SERVICE ROUTINE
066717             ELSE
066720                A SHZ -10; X:=BUSFL
066722                IF A=4 AND X><0 THEN
066726                   IF X:=SCCSU><0 THEN
066730                      CALL DCTHR               % DISCONNECT LOGICAL THREAD
066731                   ELSE
066732                   IF BUSFL BIT 6SARB THEN     % ABITRATION TIMEOUT
066735                      MIN SCWAQ.SUTMR; X:=0    % COUNT RETRIES
066740                      IF X><0 THEN
066741                         0=:SCTST; CALL RSTMR  % RESET TIMER
066743                         CALL RFWAQ            % NOT ABLE TO SELECT DEVICE
066744                         T:=NESER; CALL TEROP  % TERMINATE OPERATION
066746                      FI
066746                   FI FI
066746                   GO FAR BUSFP
066747   *)FILL
066757                ELSE
066760                IF A=1 AND X BIT 6SARB THEN
066765                   CALL RFWAQ                  % ARBITRATION WON
066766                   CNCLR/\X.SUTHS BONE 6SCRP   % FETCH FLAGS
066771                   A=:BUSFL; CALL CNTHR        % CONNECT PHYSICAL PATH
066773                   0=:SCTST; CALL ENTIM        % START TIMER
066775                   RBSIR=:SCNIH                % ALLOW BUS SERVICE INTERRUPTS
066777                ELSE
067000                IF A=20 AND X BZERO 6SARB BZERO 6SRST=0 THEN
067007                   X BONE 6SCRP=:BUSFL         % INDICATE RECONNECT
067011                   RBSIR=:SCNIH                % ALLOW BUS SERVICE INTERRUPTS
067013                ELSE
067014                IF A=1 AND T:=NCROK BIT 8SDIA THEN
067022                   GO FAR STFIN                % SELFTEST FINSHED
067023                ELSE
067024                IF A=SCEIM SHZ -10 AND D BIT PARIT AND 11/\BUSFL=1 THEN
067037                   MIN SCNPE; 0/\0             % COUNT PARITY ERRORS
067041                   IF 17/\BUSFL=7 THEN
067046                      CALL SCMPE               % "MESSAGE PARITY ERROR"
067047                   ELSE
067050                      CALL SCIDE               % "INITATOR DETECTED ERROR"
067051                   FI
067051                   CALL SCISR
067052                ELSE
067053                   T:=NCRER; GO FAR SCDIS      % ILLEGAL INTERRUPT
067055                FI FI FI FI FI
067055             FI
067055          FI
067055          5\/SCCCW; T:=HDEV+WCONT; *IOXT       % ACTIVATE+ENABLE INTERRUPT
067062          GO SCWTI
067063   *)FILL
067073
067073   % TIMEOUT ROUTINE
067073
067073   INTEGER ABFLG=?
067073
067073   SCTIO: IF NCROK BIT 8SCLR THEN
067076             SCRSQ=:SCRSL; A:=B=:SCRSQ         % ENABLE DIRECT TIMEOUT
067102             IF X:=SCCSU><0 THEN
067104                T:=X.SUTRG; 0=:X.SUTRG; CALL TEROP  % DISCONNECT CURRENT
067107                0=:SCCSU; "0" BONE 6SRST=:BUSFL     % ALLOW RECONNECT
067113                GO FAR EXDRI                        % RETURN TO USER
067114             FI
067114             X:="SCTQP-SULINK"+B
067116             DO
067116               WHILE X:=X.SULINK><0
067120               IF X.SUTHS BIT 6SRST THEN
067123                  CALL TEROP; GO FAR EXDRI     % TERMINATE OPERATION
067125               FI
067125             OD
067126             NCROK BZERO 8SCLR=:NCROK          % RESET CLEANUP FINISHED
067131          FI
067131          IF NCROK><0 AND A BZERO 8SRST=0 THEN
067135             0=:NCROK=:SCTST; CALL RSTMR       % RESTART TIMER
067140             IF BUSFL BZERO 6SRST=0 THEN
067143                CALL SELEC                     % RESTART INTERFACE
067144             FI
067144          FI
067144          IF SCRCO><0 AND NCROK NBIT 8SDIA AND NBIT 8SRIN THEN
067153             0=:SCRCO; X:=SCRXR                % RETURN FROM BUS RESET
067155             IF NCROK BIT 8SERR THEN
067160                T:=NCRST; SCRLR=:P             % ERROR RETURN
067163             ELSE
067164                T:=0; SCRLR+2=:P               % OK RETURN
067170   *)FILL
067176             FI
067176          FI
067176          IF TMR=0 THEN
067200             IF NCROK><0 THEN
067202                IF A BIT 8SRIN GO FAR SCRST   % TIMEOUT ON RESET RECEIVE
067204                IF A BIT 8SERR THEN
067206                   IF X:=SCTQP=0 THEN
067211                      IF X:=SCWAQ><0 THEN CALL RFWAQ FI
067214                   FI
067214                   IF X><0 THEN
067215                      SCRSQ=:SCRSL; A:=B=:SCRSQ     % DIRECT TIMEOUT
067221                      T:=NCRST; CALL TEROP          % TERMINATE OPERATION
067223                      GO FAR EXDRI                  % RETURN TO USER
067224                   FI
067224                ELSE
067225                   GO FAR STFIN              % DIAGNOSTICS TIMEOUT
067226                FI
067226             ELSE
067227             IF SCTST=1 THEN
067233                IF BUSFL BIT 6SARB THEN
067236                   MIN SCWAQ.SUTMR; X:=0     % ABITRATION TIMEOUT
067241                   IF X><0 THEN
067242                      CALL RFWAQ; X=:SCCSU   % TERMINATE
067244                      IF X.SUTRG=CMTMO THEN
067250                         T:=CMTMA            % TIMEOUT, NOT ABORT
067251                      ELSE
067252                         T:=ASOTM            % SELECT TIMEOUT
067253                      FI
067253                   ELSE
067254                      T:=ASOTM               % SELECT TIMEOUT
067255                   FI
067255                   T:=ASOTM; GO FAR SCDIS    % RESET INTERFACE
067257                FI
067257             ELSE
067260             IF X:=SCTQP><0 THEN
067262                IF X.SUTMR=0 THEN
067264                   IF SCCSU=X THEN
067267                      T:=CNTMO; GO FAR SCDIS   % CONNECTION TIMEOUT
067271                   FI
067271                   CMTMO=:X.SUTRG              % OPERATION TIMEOUT
067273                   CALL DITIM; ABFLG=:X.SUTHS  % ABORT OPERATION
067276                   177760/\X.SUCON\/2=:X.SUCON % NEW TIMEOUT
067302                   SCWAQ=:X.SULINK; X=:SCWAQ   % INSERT IN ARBITRATION QUEUE
067305                   -1=:X.SUTMR; CALL SELEC     % SEND ABORT
067310                FI
067310                CALL RSTMR                     % RESET TIMER
067311             FI FI FI
067311          FI
067311          GO SCWTI
067312   *)FILL
067325
067325   INTEGER ABFLG:=6; * *-1/1@6SMSO+^; *-1/1@6SIDE+^
067326
067326   % FATAL ERROR: RESET SCSI BUS
067326   SCDIS: IF X:=SCCSU><0 THEN
067330             IF X.SUTRG=0 THEN T=:X.SUTRG FI
067333          FI
067333          BUSFL=:SEST1                         % SAVE STATUS
067335          SCSSR=:SEST2
067337          SCNIS=:SEST3
067341          CMSGI=:SEST4
067343          CMSGO=:SEST5
067345          T=:SCLRE; GO FAR SCRST               % RESET SCSI BUS
067347
067347   % BUS FREE PHASE
067347   BUSFP: CALL SELEC                           % CHECK ARBITRATION QUEUE
067350
067350   % EXIT DRIVER: X = LUN DATAFIELD OR 0. T = STATUS
067350   EXDRI: IF X=0 GO SCWTI; X.SULRG             % NO RETURN
067353          IF T=0 THEN A+2=:P FI                % NORMAL RETURN
067357          IF T<0 THEN
067361             A+1                               % INTERMIDIATE RETURN
067362          ELSE
067363             MIN ERCNT; 0/\0                   % ERROR RETURN
067365          FI
067365          A=:P
067366   *)FILL
067370
067370   % MSGIN: MESSAGE IN HANDLING
067370
067370   INTEGER POINTER HOME2
067371   DOUBLE BSISR(0); * 1@BUSSI; NEWPH
067373
067373   MSGIN: A:=L=:"HOME2"
067375          IF BUSFL BIT 6SMSI THEN CALL MSGI FI % MESSAGE IN HANDLING
067401          4; T:=HDEV+WNCOM; *IOXT              % "Message Accepted" TO NCR
067405          BSISR=:SCNIH; GO HOME2               % ALLOW BUS SERVICE INTERRUPTS
067410
067410
067410   MSGI:  IF X:=SCCSU=0 THEN
067413             IF CMSGI BIT 7 THEN
067416                AD SHZ 15 SHZ -35              % LUN
067420                T:=HDEV+RSOUI; *IOXT           % READ BUS DEVICE NUMBER
067423                X:=7/\A:=SCDVD(X)
067426                IF X><0 AND D<X.SDNLU THEN
067432                   SULEN; *RMPY SD DA
067434                   X+D+"SDLEN"; T:=X.SUTHS
067437                   CNCLR/\T+7=:BUSFL           % RESTORE FLAGS
067443                   IF T BIT 6SDIS GO FAR CNTHR % CONNECT THREAD
067445                   GO FAR SCABO                % CONNECT THREAD
067446                FI
067446             FI
067446             GO FAR SCDIS                      % IMPOSSIBLE
067447             *)FILL
067454          FI
067454
067454          IF CMSGI<=7 THEN
067460
067460             GOSW MSG0,MSG1,MSG2,MSG3,MSG4,MSGR,MSGR,MSG7
067471
067471   MSG0:     BUSFL BONE 6SCCO=:BUSFL           % "COMMAND COMPLETE"
067474             EXIT
067475
067475   MSG2:     SCCDP=:X.SUSDP; SCCBC=:X.SUSBC    % "SAVE DATA POINTER"
067501             EXIT
067502
067502   MSG1:     IF SCEM1=2400 THEN                % MODIFY DATA POINTER
067506
067506             ELSE
067507             IF A=2261 THEN                    % SYNCRONOUS TRANSFER REQUEST
067512
067512             ELSE
067513             IF A=1002 THEN                    % EXTENDED IDENTIFY
067516
067516             FI FI FI
067516             GO FAR MSGR
067517
067517
067517   MSG3:     X.SUSDP=:SCCDP; X.SUSBC=:SCCBC    % "RESTORE POINTERS"
067523             EXIT
067524
067524   MSG4:     BUSFL BONE 6SDIS=:BUSFL           % "DISCONNECT"
067527             EXIT
067530
067530   MSG7:     IF LMSGO/\377=5 OR =11 THEN       % "MESSAGE REJECT"
067540                T:=TRANE; GO FAR SCABO         % SEND "ABORT"
067542             ELSE
067543             IF A=14 THEN
067546                T:=FNIPL; GO FAR SCABO         % NOT IMPLEMENTED
067550             ELSE
067551                T:=SPCER; GO FAR SCABO         % ILLEGAL
067553             FI FI
067553          FI
067553
067553   MSGR:  GO FAR SCMRJ                         % RETURN "MESSAGE REJECT"
067554   *)FILL
067563
067563   % SELEC: ACTIVATE CONTROLLER
067563
067563   TRIPLE SVTAD
067566   INTEGER SAVXR
067567
067567   SELEC: TAD=:SVTAD; X=:SAVXR
067571          -1=:SCEIM                            % DISABLE INTERRUPT
067573          IF X:=SCWAQ><0 THEN
067575             "0"; T:=HDEV+WCONT; *IOXT         % CLEAR TO MEMORY
067601             A BONE 6SARB=:BUSFL               % INDICATE ARBITRATION
067603             X.SUDLU SHZ -14                   % SCSI IDENT
067605             T+"WDESI-WCONT"; *IOXT
067607             WATFS; T+"WTCM-WDESI"; *IOXT      % SET WAITING TIME INTO TRANSFER COUNTER
067612             AD SHZ 10; T+"WTC2-WTCM"; *IOXT
067615             AD SHZ 10; T+"WTCL-WTC2"; *IOXT
067620             10; IF X:=X.SUCON BIT 4SINA THEN 11 FI
067625             T+"WNCOM-WTCL"; *IOXT             % SELECT COMMAND TO NCR
067627             IF X:=SCTST>>2 THEN
067633                TMR=:X.SUTMR                   % SAVE CURRENT TIMER
067635             FI
067635             1=:SCTST; -5=:TMR                 % ENABLE SELECT TIMEOUT
067641          ELSE
067642             0=:BUSFL                          % BUS FREE
067643          FI
067643          5; T:=HDEV+WCONT; *IOXT              % ENABLE INTERRUPT
067647          X:=SAVXR; SVTAD
067651          EXIT
067652
067652   % RFWAQ: REMOVE FROM WAITING FOR ARBITRATION QUEUE.
067652
067652   RFWAQ: IF X:=SCWAQ><0 THEN
067654             X.SULINK=:SCWAQ; 0=:X.SULINK
067657          FI
067657          EXIT
067660   *)FILL
067660
067660
067660   % INITO: INITIALIZE OPERATION
067660
067660   INTEGER INOPR:=0; * *-1/1@6SFUN+^; *-1/1@6SIDE+^
067661   INTEGER INABO:=6; * *-1/1@6SMSO+^
067662   INTEGER INBDR:=14; * *-1/1@6SMSO+^
067663
067663   INITO: IF X.SUCON=:D SHZ -10=0 THEN
067667             INOPR                             % NORMAL OPERATION
067670             IF D BIT 4SRCA THEN
067672                A BONE 6SRFD                   % RETURN ON COMMAND ACCEPTED
067673             FI
067673          ELSE
067674          IF A-1=0 THEN
067676             INABO                             % ABORT
067677          ELSE
067700             INBDR                             % BUS DEVICE RESET
067701          FI FI
067701          A=:X.SUTHS; -1=:X.SUSTA              % INITIAL STATUS
067704          X.SUIDP=:X.SUSDP; X.SUIBC=:X.SUSBC   % INITIAL POINTERS
067710          -2=:X.SUTMR; "SCWAQ-SULINK"+B; T:=X  % INSERT IN ARBITRATION WAITING QUEUE
067715          DO A=:X; WHILE X.SULINK><0 OD
067721          T=:X.SULINK
067722          EXIT
067723
067723   % CNTHR: CONNECT PHYSICAL PATH
067723
067723   CNTHR: X=:SCCSU                             % INDICATE PATH CONNECTED
067724          IF X.SUTHS BIT 6SMSO THEN
067727             A/\377=:CMSGO                     % RESTORE MESSAGE
067731             3; T:=HDEV+WNCOM; *IOXT           % "Set ATN" TO NRC
067735          FI
067735          X.SUSDP=:SCCDP; X.SUSBC=:SCCBC       % RESTORE POINTERS
067741          EXIT
067742
067742   % DCTHR: DISCONNECT PHYSICAL PATH
067742
067742   INTEGER POINTER HOME4
067743
067743   DCTHR: A:=L=:"HOME4"                        % RETURN ADDRESS
067745          IF T:=X.SUTRG=0 AND BUSFL BIT 6SDIS THEN
067753             IF A BIT 6SRFD THEN
067755                A/\DCCLR\/CMSGO=:X.SUTHS       % SAVE THREAD STATUS
067760                T:=-1                          % INTERMIDIATE RETURN
067761             ELSE
067762                A/\DCCLR\/CMSGO=:X.SUTHS       % SAVE THREAD STATUS
067765                X:=0                           % NO RETURN
067766             FI
067766          ELSE
067767             CALL TEROP                        % TERMINATE (T=STATUS)
067770          FI
067770          0=:SCCSU; GO HOME4                   % RETURN
067772   *)FILL
067776
067776   % RESTART LOGICAL UNIT TIMER
067776
067776   INTEGER SAVRX
067777
067777   RSTMR: IF SCTST=0 THEN
070001             IF X=:SAVRX:=SCTQP><0 THEN
070004                IF X.SUTMR=:TMR><0 THEN        % NEW TIME VALUE
070007                   0=:X.SUTMR; X=:SCTST        % INDICATE TIMER RUNNING
070011                ELSE
070012                   SCRSQ=:SCRSL; A:=B=:SCRSQ   % DIRECT TIMEOUT
070016                FI
070016             ELSE
070017                0=:TMR                         % DISABLE TIMER
070020             FI
070020             X:=SAVRX
070021          FI
070021          EXIT
070022
070022   INTEGER SHTIN(0); *SHT ZIN                  % SHIFT INSTRUCTION
070023   INTEGER POINTER HOME5=?
070023   TRIPLE TITAD=?
070023   INTEGER SAVX=?
070023
070023   % ENABLE LOGICAL UNIT TIMER
070023
070023   ENTIM: TAD=:TITAD; X:=:L=:"HOME5"
070026          IF X:=SCTST>>2 THEN
070032             TMR=:X.SUTMR; 0=:SCTST            % SAVE CURRENT TIMER
070035          FI
070035          17/\L.SUCON\/SHTIN; T:=-1; *EXR SA   % CALCULATE MAX TIME
070043          D:=B; X:="SCTQP-SULINK"+B
070046          DO
070046             WHILE X=:B:=X.SULINK><0 AND X.SUTMR>=T
070054             T-A
070055          OD
070056          IF X><0 THEN A-T=:X.SUTMR FI         % UPDATE NEXT ELEMENT
070061          SULINK=:L.SULINK; X=:SULINK; B:=D    % LINK INTO TIMER QUEUE
070066          T=:X.SUTMR; CALL RSTMR               % RESET TIMER
070070          TITAD; GO HOME5
070072   *)FILL
070075
070075   % TEROP: TERMINATE OPERATION, T = STATUS
070075
070075   INTEGER POINTER HOME5
070076   TRIPLE TITAD
070101   INTEGER SAVX
070102
070102   TEROP: IF X.SUTRG=0 THEN
070104             IF SCCSU=X THEN
070107                SCCDP=:X.SUSDP; SCCBC=:X.SUSBC % SAVE DATA POINTERS
070113                IF BUSFL NBIT 6SMSO THEN
070116                   IF A BIT 6SCCO THEN
070120                      IF X.SUSTA<0 THEN
070122                         T:=NOSST              % NO STATUS RECEIVED
070123                      ELSE
070124                         T:=0                  % COMMAND COMPLETE
070125                      FI
070125                   ELSE
070126                   IF X.SUCON SHZ -10=0 AND T=0 THEN
070133                      T:=UNDIS                 % UNEXPECTED DISCONNECT
070134                   FI FI
070134                ELSE
070135                IF T=0 THEN
070137                   IF CMSGO=5 OR =11 THEN
070146                      T:=TRANE                 % PARITY ERROR
070147                   ELSE
070150                      T:=MNIBT                 % MESSAGE NOT IMPLEMENTED
070151                   FI
070151                FI FI
070151             ELSE
070152             IF X.SUCON SHZ -10-3=0 AND CMTMO=T THEN
070161                T:=0                           % TIMER FINISHED
070162             FI FI
070162          ELSE
070163             A=:T                              % ERROR IN OPERATION
070164          FI
070164          0=:X.SUTHS                           % INDICATE FREE
070165
070165   % DITIM: DISABLE LOCICAL UNIT TIMER
070165
070165   DITIM: TAD=:TITAD; X=:SAVX:=:L=:"HOME5"
070171          IF X:=SCTQP><0 THEN
070173             IF X:=SCTST>>2 THEN
070177                TMR=:X.SUTMR; 0=:SCTST         % SAVE TIMER
070202             FI
070202             X:="SCTQP-SULINK"+B
070204             DO
070204                X=:D
070205                WHILE X:=X.SULINK><L AND X><0
070211             OD
070212             IF X=L THEN
070214                IF T:=X.SULINK=:D.SULINK><0 THEN
070221                   0=:L.SULINK
070223                   X.SUTMR+T.SUTMR=:X.SUTMR
070227                FI
070227             FI
070227             CALL RSTMR
070230          FI
070230          X:=SAVX; TITAD; GO HOME5
070233   *)FILL
070235
070235   % START RESET SEQUENCE ON SCSI BUS
070235
070235   INTEGER RWCNT
070236   INTEGER CLMSK:=0; * *-1/1@8SRIN+^; *-1/1@8SDIA+^; *-1/1@8STMR+^
070237                     * *-1/1@8SERR+^; *-1/177777-^
070237
070237   SCRST: IF NCROK=0 THEN
070241             IF X:=SCTST>>2 THEN TMR=:X.SUTMR FI    % SAVE CURRENT TIMER
070247             IF SCSSR BIT 5 THEN
070252                NCROK BONE 8SRIN=:NCROK; T:=SBRST   % RECEIVING RESET
070256             ELSE
070257                T:=LIRST
070260             FI
070260             IF X:=SCCSU><0 THEN
070262                IF X.SUTRG=0 THEN T=:X.SUTRG FI     % SET STATUS
070265                BUSFL BONE 6SRST=:BUSFL             % INDICATE RESET STARTED
070270                NCROK BONE 8SCLR=:NCROK             % RECOVERY NECESSARY
070273             ELSE
070274                "0" BONE 6SRST=:BUSFL               % INDICATE RESET STARTED
070277             FI
070277             X:="SCTQP-SULINK"+B
070301             DO
070301                WHILE X:=X.SULINK><0                % FOR ALL ACTIVE OPERATIONS
070303                IF X.SUCON NBIT 4SRST AND X.SUTHS BIT 6SDIS THEN
070311                   A BONE 6SRST=:X.SUTHS            % INDICATE RESET
070313                   IF X.SUTRG=0 THEN T=:X.SUTRG FI  % SET STATUS
070316                   NCROK BONE 8SCLR=:NCROK          % RECOVERY NECESSARY
070321                FI
070321             OD
070322          ELSE
070323             NCROK/\CLMSK=:NCROK
070326          FI
070326          NCROK BONE 8SRST=:NCROK              % INDICATE RESET STARTED
070331          -1=:SCEIM; 2=:SCTST                  % LOCK INTERFACE
070335          "0"; T:=HDEV+WNCOM; *IOXT            % DISCONNECT TO NCR
070341          20; T+"WCONT-WNCOM"; *IOXT           % CLEAR CONTROLLER
070344          T+"RSTAU-WCONT"; *IOXT               % READ STATUS
070346          IF A BIT 5 THEN
070350             NCROK BONE 8SRIN=:NCROK; -1=:TMR  % RECEIVING RST
070355          ELSE
070356             2000; T+"WCONT-RSTAU"; *IOXT      % SET RESET ON SCSI BUS
070361             -10000=:TMR; FOR TMR DO OD        % LEAVE "RST" ON FOR AT LEAST 250 us
070365             "0"; *IOXT                        % CLEAR RESET
070367             T+"RDIST-WCONT"                   % READ DIAGNOSTIC STATUS
070370             FOR X:=-1000 DO
070371                *IOXT
070372                WHILE A NBIT 7
070374             OD
070375             IF A=200 THEN                     % SELFCHECK OK
070400                NCROK BONE 8SDIA=:NCROK
070403                13; T:=HDEV+WNCOM; *IOXT       % START DIAGNOSTIC
070407                376; T+"WNDAT-WNCOM"; *IOXT    % WRITE DATAREGISTER
070412                5; T+"WCONT-WNDAT"; *IOXT      % ENABLE INTERRUPT
070415                -2=:TMR                        % DIAGNOSTIC TIMER
070417             ELSE
070420                100000=:NCROK                  % ERROR IN INTERFACE
070422             FI
070422          FI
070422          GO SCTIO
070423   *)FILL
070433
070433   % TERMINATE RESET ON SCSI BUS
070433
070433   STFIN: IF NCROK BIT 8SDIA THEN
070436             A BZERO 8SDIA=:NCROK              % DIAGNOSTICS FINSHED
070440             IF TMR><0 AND D BIT DARGF THEN
070444                T:=HDEV+RNDAT; *IOXT           % READ DATAREGISTER
070447                16; T+"WNCNT-RNDAT"; *IOXT     % SET NCR CONTROLL
070452                IF A NBIT 1 THEN
070454                   5; T+"WCONT-WNCNT"; *IOXT   % ENABLE INTERRUPT
070457                FI
070457                0=:TMR
070460                IF T:=NCROK BIT 8SPWF THEN
070463                   T BZERO 8SPWF; X:=SLTMR     % LONG TIMER (POWER LOST)
070465                ELSE
070466                   X:=SRTMR                    % RESET TIMER
070467                FI
070467                IF SCRCO NBIT 0 THEN
070472                   X=:TMR; T BONE 8STMR        % ALLOW DEVICE SETTLE TIME
070474                FI
070474                T=:NCROK
070475             ELSE
070476                0=:TMR; 100000=:NCROK          % ERROR IN INTERFACE
070501             FI
070501          ELSE
070502          IF A BIT 8STMR THEN
070504             A BZERO 8STMR=:NCROK; 0=:TMR      % RESET FINSHED
070507          ELSE
070510             0=:TMR; 100000=:NCROK             % ERROR IN INTERFACE
070513          FI FI
070513          GO FAR SCTIO
070514   *)FILL
070520
070520   % NEW PHASE
070520
070520   TRIPLE SAVRG
070523
070523   NEWPH: T:=BUSFL                             % OLD STATUS
070524          IF 36/\T=0 THEN                      % OLD PHASE WAS DATA
070527             TAD=:SAVRG; K:=1
070531             IF D NBIT TCZRO THEN
070533                *BLDA 00 DT                    % PREFECTH FLAG
070534                T:=HDEV+RTCL; *IOXT            % READ LSB OF BYTECOUNT
070537                AD SHZ -10
070540                T+"RTC2-RTCL"; *IOXT           % READ BYTE 2 OF BYTECOUNT
070542                AD SHZ -10
070543                T+"RTCM-RTC2"; *IOXT           % READ MSB OF BYTECOUNT
070545             ELSE
070546                A:=0; D:=0                     % ZERO BYTECOUNT
070550             FI
070550             T:=SCCB1; X:=SCCB2; AD=:SCCBC     % SAVE OLD AND STORE NEW BYTECOUNT
070553             *RADD SD CM2 DX; RADD SA CM1 ADC DT    % GET BYTES TRANSFERED
070555             SCCDP; *RADD SX DD; RADD ST ADC DA     % AND NEW MEMORY ADDRESS
070560             AD=:SCCDP SHZ -1; X:=A            % EXPECTED MAR
070563             IF M THEN                         % STOPPED ON ODD BYTE
070565                IF BUSFL BIT 0 THEN            % ODD BYTE TO ND-100 MEMORY
070570                   150; T:=HDEV+WCONT; *IOXT   % SET TEST MODE
070574                   T+"RLMAR-WCONT"; *IOXT      % FORCE LAST BYTE TO MEMORY
070576                   "0"; T+"WCONT-RLMAR"; *IOXT % CLEAR TEST MODE
070601                FI
070601                D+1; X:=X+C                    % ADJUST EXPECTED MAR
070603             FI
070603             IF K THEN
070605                T:=HDEV+RLMAR; *IOXT           % READ LEAST SIGNIFICANT MAR
070610                IF A><D GO MARER
070612                T+"RHMAR-RLMAR"; *IOXT         % READ MOST SIGNIFICANT MAR
070614                IF A><X GO MARER
070616             FI
070616             SAVRG
070617          FI
070617          AD SHZ 32 SHZ -15                    % GET NEW PHASE
070621          X:=NPCLR/\T\/A=:BUSFL                % SET NEW STATUS
070625
070625          GOSW DAOPH,DAIPH,COMPH,STAPH,FAR ILOPH,FAR ILIPH,FAR MSOPH,FAR MSIPH
070636
070636   MARER: T:=ILMAR; GO FAR SCDIS               % MAR NOT AS EXPECTED
070640
070640   *)FILL
070646
070646   INTEGER WP1, WP2
070650   DOUBLE DWP=WP1
070650
070650   % #0: DATA OUT PHASE
070650
070650   DAOPH: 40=:SCCCW; GO DATPH
070653
070653   % #1 DATA IN PHASE
070653
070653   DAIPH: 140=:SCCCW
070655   DATPH: IF X>=0 GO FAR ILLPH                 % PHASE ILLEGAL
070657          T:=HDEV+WCONT; *IOXT                 % SET TRANSFER DIRECTION
070662          SCCDP SHZ -1                         % MEMORY ADDRESS
070664          IF M THEN                            % MEMORY ADDRESS ON ODD BYTE !
070666             X BONE 6SSBT=:BUSFL; AD=:DWP; SCCBC
070672             *RADD CM1 DD; RADD CM1 ADC DA     % DECREMENT BYTECOUNT
070674             IF A BIT 17 GO BCZRO; AD=:SCCBC   % NEW BYTECOUNT NEGATIVE !
070677             "0"=:SCCCW; *IOXT                 % CLEAR TO MEMORY
070702             124; T+"WNCOM-WCONT"; *IOXT       % "Transfer info, single byte" TO NCR
070705             IF X BIT 0 THEN
070707                L=:D; CALL RINFO; D=:L:=A      % READ BYTE
070713                T:=WP1; X:=WP2; *LDATX         % GET MSB
070716                A/\177400\/D; *STATX           % STORE NEW WORD
070721             ELSE
070722                T:=WP1; X:=WP2; *LDATX         % GET BYTE
070725                T:=HDEV+WNDAT; *IOXT           % SEND
070730             FI
070730             DWP; D+1; A:=A+C; AD SHZ 1=:SCCDP % NEW MEMORY ADDRESS
070735          ELSE
070736             T+"WHMAR-WCONT"; *IOXT            % WRITE MSW OF MAR
070740             A:=D; T+"WLMAR-WHMAR"; *IOXT      % WRITE LSW OF MAR
070743             SCCBC; IF A=D AND D=0 GO BCZRO    % CHECK BYTECOUNT
070750             T+"WTCM-WLMAR"; *IOXT             % MSB OF TRANSFERCOUNTER
070752             AD SHZ 10; T+"WTC2-WTCM"; *IOXT   % 2B OF TRANSFER COUNTER
070755             AD SHZ 10; T+"WTCL-WTC2"; *IOXT   % LSB OF TRANSFER COUNTER
070760             224; T+"WNCOM-WTCL"; *IOXT        % DMA MODE + TRANSFER INFO
070763          FI
070763          EXIT
070764
070764   BCZRO: T:=BCERR; GO FAR SCDIS
070766   *)FILL
070773
070773   % #2 COMMAND PHASE
070773
070773   COMPH: IF X>=0 GO FAR ILLPH                 % PHASE NOT VALID
070775          40=:SCCCW; T:=HDEV+WCONT; *IOXT      % SET TRANSFER DIRECTION
071002          SCCSU.SUCM1; T+"WHMAR-WCONT"; *IOXT  % WRITE MSW OF MAR
071006          X.SUCM2; T+"WLMAR-WHMAR"; *IOXT      % WRITE LSW OF MAR
071011          "0"; T+"WTCM-WLMAR"; *IOXT           % MSB OF TRANSFERCOUNTER
071014          T+"WTC2-WTCM"; *IOXT                 % 2B OFTRANSFER COUNTER
071016          14; T+"WTCL-WTC2"; *IOXT             % LSB OF TRANSFER COUNTER
071021          224; T+"WNCOM-WTCL"; *IOXT           % DMA MODE + TRANSFER INFO
071024          EXIT
071025   *)FILL
071027
071027   % #3 STATUS PHASE
071027
071027   STAPH: IF X>=0 GO FAR ILLPH                 % PHASE NOT VALID
071031          124; T:=HDEV+WNCOM; *IOXT            % "Transfer info, single byte" TO NCR
071035          L=:D; CALL RINFO                     % READ STATUS BYTE
071037          A=:SCCSU.SUSTA; D=:P
071042   *)FILL
071044
071044   % #4 UNSPECIFIED OUT PHASE
071044
071044   ILOPH: T:=ILEPH; GO FAR SCDIS               % "ILLEGAL PHASE"
071046   *)FILL
071047
071047   % #5 UNSPECIFIED IN PHASE
071047
071047   ILIPH: T:=ILEPH; GO FAR SCDIS               % "ILLEGAL PHASE"
071051   *)FILL
071052
071052   % #6 MESSAGE OUT PHASE
071052
071052   MSOPH: IF T BIT 6SIDE THEN
071054             IF SCCSU.SUCON BIT 4SRNA THEN
071060                7/\X.SUDLU\/200                % "IDENTIFY"
071063             ELSE
071064                7/\X.SUDLU\/300                % "IDENTIFY" + "DISCONNECT ALLOWED"
071067             FI
071067             IF T BIT 6SMSO THEN
071071                X:=BUSFL BZERO 6SMSO=:BUSFL    % MARK MESSAGE TRANSFERED
071074                A SHZ 10\/CMSGO; 0=:CMSGO
071077             FI
071077             X:=A
071100          ELSE
071101          IF 17/\T-6=0 THEN
071105             X:=LMSGO                          % RESEND LAST MESSAGE
071106          ELSE
071107          IF T BIT 6SMSO THEN
071111             BUSFL BZERO 6SMSO=:BUSFL          % MARK MESSAGE TRANSFERED
071114             X:=CMSGO; 0=:CMSGO
071116          ELSE
071117             X:=10                             % "NO OPERATION"
071120          FI FI FI
071120          IF X=:LMSGO<0 THEN
071123             40=:SCCCW; T:=HDEV+WCONT; *IOXT   % SET TRANSFER DIRECTION
071130             T:=X; SCPMB; *DEPO                % STORE MESSAGE
071133             T:=HDEV+WHMAR; *IOXT              % WRITE MSW OF MAR
071136             A:=D; T+"WLMAR-WHMAR"; *IOXT      % WRITE LSW OF MAR
071141             "0"; T+"WTCM-WLMAR"; *IOXT        % MSB OF TRANSFERCOUNTER
071144             T+"WTC2-WTCM"; *IOXT              % 2B OFTRANSFER COUNTER
071146             2; T+"WTCL-WTC2"; *IOXT           % LSB OF TRANSFER COUNTER
071151             224; T+"WNCOM-WTCL"; *IOXT        % DMA MODE + TRANSFER INFO
071154          ELSE
071155             124; T:=HDEV+WNCOM; *IOXT         % "Transfer info, single byte" TO NCR
071161             A:=X; T+"WNDAT-WNCOM"; *IOXT      % SEND MESSAGE
071164          FI
071164          EXIT
071165   *)FILL
071170
071170   % #7 MESSAGE IN PHASE
071170
071170   DOUBLE EMEI1(0); *1@FUCOM; EMFC1            % EXTENDED MESSAGE HANDLER
071172   DOUBLE MSFCS(0); *1@FUCOM; MSGIN            % MESSAGE IN HANDLER
071174   INTEGER POINTER MIRET
071175
071175   MSIPH: A:=L=:"MIRET"
071177          124; T:=HDEV+WNCOM; *IOXT            % "Transfer Info, single byte" TO NCR
071203          CALL RINFO; A=:CMSGI                 % READ MESSAGE
071205          IF A-1=0 THEN
071207             EMEI1=:SCNIH                      % EXTENDED MESSAGE
071211          ELSE
071212             MSFCS=:SCNIH                      % FUNCTION COMPLETE NEXT
071214          FI
071214          BUSFL BONE 6SMSI=:BUSFL; GO MIRET    % RETURN
071220
071220   % FUNCTION COMPLETE FOR EXTENDED MESSAGE BYTE
071220
071220   DOUBLE EMEI2(0); *1@BUSSI; EMRML
071222
071222   EMFC1: 4; T:=HDEV+WNCOM; *IOXT              % "Message accepted"
071226          EMEI2=:SCNIH; EXIT                   % EXPECTED INTERRUPT
071231
071231   % READ LENGTH OF MESSAGE
071231
071231   DOUBLE EMEI3(0); *1@FUCOM; EMFC2
071233
071233   EMRML: 124; T:=HDEV+WNCOM; *IOXT            % "Transfer info, single byte"
071237          D:=L; CALL RINFO; L:=D               % GET MESSAGE LENGTH
071242          IF A=:SCEML>SCMBL GO FAR ILLPH       % MESSAGE TO LARGE FOR BUFFER
071246          EMEI3=:SCNIH; EXIT                   % EXPECTED INTERRUPT
071251
071251   % FUNCTION COMPLETE FOR MESSAGE LENGTH BYTE
071251
071251   DOUBLE EMEI4(0); *1@BUSSI; EMRMB
071253
071253   EMFC2: 4; T:=HDEV+WNCOM; *IOXT              % "Message accepted"
071257          EMEI4=:SCNIH; EXIT                   % EXPECTED INTERRUPT
071262
071262   % READ MESSAGE BYTES
071262
071262   DOUBLE EMEI5(0); *1@FUCOM; MSGIN
071264
071264   EMRMB: A:=L=:"MIRET"
071266          "0"; T:=HDEV+WTCM; *IOXT             % MSB OF TRANSFERCOUNTER
071272          T+"WTC2-WTCM"; *IOXT                 % 2B OF TRANSFER COUNTER
071274          SCEML; T+"WTCL-WTC2"; *IOXT          % LSB OF TRANSFER COUNTER
071277          X:=0; 24; T+"WNCOM-WTCL"; *IOXT      % TRANSFER INFO
071303          DO
071303             CALL RINFO; T:="SCEMB"+B; *SBYT   % READ AND STORE BYTE
071307             WHILE X+1<SCEML
071313          OD
071314          EMEI5=:SCNIH; GO MIRET               % FUNCTION COMPLETE NEXT
071317   *)FILL
071322
071322   % IMPOSSIBLE BUS PHASE
071322
071322   ILLPH: T:=SPCER; GO FAR SCDIS
071324   *)FILL
071325
071325   % READ A INFORMATION BYTE FROM SCSI BUS
071325
071325   INTEGER RDCNT
071326
071326   RINFO: T:=HDEV+RAUXS; -2000=:RDCNT
071332          FOR RDCNT DO
071332             *IOXT
071333             IF A BIT DARGF THEN
071335                T+"RNDAT-RAUXS"; *IOXT
071337                EXIT
071340             FI
071340          OD
071342          GO FAR SCINT                         % CHECK FOR INTERRUPT
071343   *)FILL
071345
071345
071345   % SCMRJ: SET "MESSAGE REJECT" MESSAGE TO TARGET
071345
071345   SCMRJ: 7SMRJ; T:=SPCER; GO EMSGO            % SEND MESSAGE
071350
071350   % SCMPE: SET "MESSAGE PARITY ERROR" MESSAGE TO TARGET
071350
071350   SCMPE: BUSFL BZERO 6SMSI=:BUSFL             % THROW AWAY MESSAGE
071353          7SMPE; T:=TRANE; GO EMSGO
071356
071356   % SCIDE: SET "INITIATOR DETECTED ERROR" MESSAGE TO TARGET
071356
071356   SCIDE: IF BUSFL BIT 6SMSO AND CMSGO=7SIDE THEN EXIT FI
071366          7SIDE; T:=TRANE; GO EMSGO
071371
071371   EMSGO: IF X:=BUSFL BIT 6SMSO THEN
071374
071374   % SCABO: ABORT OPERATION
071374
071374   SCABO:    IF X:=SCCSU><0 THEN
071376                IF X.SUTRG=0 THEN T=:X.SUTRG FI     % SET ERROR MESSAGE
071401             FI
071401             "6"                                    % ABORT MESSAGE
071402          FI
071402
071402   % INDICATE MESSAGE READY
071402
071402         A=:CMSGO; T:=BUSFL BONE 6SMSO=:BUSFL  % SEND MESSAGE
071406         3; T:=HDEV+WNCOM; *IOXT               % "Set ATN" TO NCR
071412         EXIT
071413   *)FILL
071413   RBUS
071413   @DEV 1
