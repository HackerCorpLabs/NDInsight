122677   @DEV (S-S-L)RP-P2-ACCRT:NPL
122677   %

122677   *"8ACC
"122677   %            *********************
122677   %            **                 **
122677   %            **    A C C R T    **
122677   %            **                 **
122677   %            *********************
122677   %
122677   %            ACCRT IS AN RT-PROGRAM WHICH DUMPS THE ACCUMULATED CPU TIME USED
122677   %            BY USER RT-PROGRAMS AND THE NUMBER OF 1K PAGE DISK ACCESSES
122677   %            MADE THROUGH THE FILE SYSTEM, FROM TABLES CONTAINED IN THE POF
122677   %            AREA OF MEMORY TO THE FILE (SYSTEM)ACCOUNTS:DATA.
122677   %
122677   %      VERSION F : 11 / JULY / 1985.
122677   %
122677   %      FOR USE WITH VSX K VERSION AND LATER.
122677   %==============================================================================
122677   %       UTILITIES FOR ACCRT.
122677   SUBR AR2READ,AR2WRITE,AR3READ,AR3WRITE
122677   INTEGER SAVTT
122700   AR2READ:    T=:SAVTT:=GLTMBANK; *LDDTX; LDT SAVTT; EXIT
122705   AR2WRITE:   T=:SAVTT:=GLTMBANK; *STDTX; LDT SAVTT; EXIT
122712   AR3READ:    T:=GLTMBANK; *LDDTX
122714               A=:SAVTT;    *LDATX 20
122716               A:=:D; T:=SAVTT; EXIT
122721   AR3WRITE:   T=:SAVTT:=GLTMBANK; *STDTX 10
122724               A:=SAVTT; *STATX; EXIT
122727   RBUS
122730
122730   %-------------------------------------------------------------------------------
122730   %
122730   %      R T A C R T
122730
122730   SUBR RTACRT
122730
122730   %      LOCAL VARIABLES
122730
122730   @ICR
122730
122730   SYMBOL   5ACCO = 36,                   % ACCOUNT:DATA BLOCK SIZE
122730            5RTPR = 14;                   % RTPROJ:DATA BLOCK SIZE
122730
122730
122730   %      BASE VARIABLES
122730
122730   DISP -200;
122730   INTEGER ARRAY BACCO(5ACCO),  % BUFFER FOR ACCOUNTING RECORD. (I.E. BLOCKS FROM ACCOUNTS:DATA).
122730                 BRTPR(5RTPR);  % BUFFER TO READ RTPROJ RECORD
122730
122730   DOUBLE  ARRAY DBACC=BACCO;   % USED TO ACCESS BACCO AS A DOUBLE INTEGER ARRAY.
122730
122730   INTEGER FILN1, %:=0,            % FILE-NUMBER FOR RTPROJ:DATA
122730           BLOC1, %:=0,            % BLOCK-NUMBER FOR RTPROJ:DATA
122730           FILN2, %:=0,            % FILE-NUMBER FOR ACCOUNTS:DATA
122730           BLOC2, %:=0,            % BLOCK-NUMBER FOR ACCOUNTS:DATA
122730
122730           PARA1(5), %:=(FILN1,"0",BRTPR,BLOC1,"5RTPR"),           % R/W 1 RTPROJ BLOCK
122730           PARA2(5), %:=(FILN2,"0",BACCO,BLOC2,"5ACCO"),           % R/W 1 ACCOUNT BLOCK
122730
122730           ACPOI, % :=0,           % CURRENT ENTRY IN ACTAB
122730           ENDAC, % :=0,           % ADDRESS OF LAST RECORD IN ACTAB.
122730           IOPOI, % :=0,           % CURRENT ENTRY IN IOACT
122730           SAERR, % :=0,           % SAVED ERROR-CODE
122730           NMBR , % :=0,           % NUMBER OF ACCOUNTING RECORDS IN FILE
122730           DMAX , % :=0,           % MAX NUMBER OF ACCOUNTS BEFORE A WARNING IS GIVEN
122730           RMAX , % :=0,           % MAX NUMBER OF ACCOUNTS PERMITTED IN THE FILE.
122730           ERFL,  % :=0,           % ERROR-FLAG :THE ACCOUNTING FILE IS FULL IF = 244
122730           BUNI , % :=0,
122730           SEC  , % :=0,
122730           MINUT, % :=0,
122730           HOUR , % :=0,
122730           DAY  , % :=0,
122730           MNTH , % :=0,
122730           YEAR , % :=0,
122730
122730           TPARA; % :=(BUNI);
122730
122730   PSID;
122730   @CR;
122730   *)FILL
122730
122730   %-------------------------------------------------------------------------------
122730   %            INITIALISATION.
122730
122730   RTACRT: *2BANK                         % SET 2 BANK MODE
122731          "ACBASE"=:B                     % INITIALISE B-REGISTER.
122733          IF ACTAB=-1 THEN
122737             *MON 2RTEX                   % NO RT-ACCOUNTING IN THIS SYSTEM.
122740          FI
122740          0=:SAERR=:ERFL                  % ZERO SAVED ERROR CODE AND ERROR FLAG.
122742   %
122742   %      RESERVE ACCOUNTING SEMAPHORE, OPEN FILES AND SET THE BLOCK SIZE.
122742   %
122742          "ACCSEMRE"; *MON 2RESR             % RESERVE ACCOUNTING SEMAPHORE
122744
122744          X:="XRTPROJ"; T:=3; A:="TYPD"
122747          *MON 2NOPE; JMP I (SEMER                  % OPEN (SYSTEM)RTPROJ:DATA
122751          A=:FILN1=:T:=5RTPR; *MON 2SBLZ            % SET BLOCK SIZE ( 5RTPR )
122755          GO FAR CL1FI
122756          X:="AFILNAM"; T:=2; A:="TYPD"             % OPEN ACCOUNTS:DATA
122761          *MON 2NOPE; JMP I (CL1FI                  % OPEN (SYSTEM)ACCOUNTS:DATA
122763          A=:FILN2=:T:=5ACCO; *MON 2SBLZ            % SET BLOCK SIZE ( 5ACCO )
122767          GO FAR CL2FI
122770   %
122770   %      READ FIRST BLOCK OF ACCOUNTS:DATA TO DETERMINE THE NEXT BLOCK TO WRITE.
122770   %
122770          0=:BLOC2; "PARA2+ACBASE"; *MON 2RFIL
122773          IF A><0 THEN
122774             GO FAR CL2FI
122775          FI
122775          BACCO(0)=:NMBR=:BLOC2           % CURRENT NUMBER OF BLOCKS IN FILE
123001          BACCO(1)=:DMAX                  % NUMBER OF BLOCKS BEFORE A WARNING MESSAGE IS GIVEN.
123004          BACCO(2)=:RMAX                  % MAXIMUM NUMBER OF BLOCKS ALLOWED IN THE FILE.
123007          IF NMBR > RMAX THEN             % ACCOUNTING FILE FULL
123013             244=:ERFL
123015             GO FAR FICL
123016          FI
123016   %
123016   %      EXAMINE THE RT-ACCOUNTING TABLE IN POF AND DETERMINE WHICH RT-PROGRAMS ARE BEING ACCOUNTED.
123016   %
123016          NBSRT SHZ 1 + IOACTAB=:IOPOI    % COMPUTE START OF I/O ACCOUNTING TABLE FOR USER RT-PROGRAMS;
123022          ACTAB=:ACPOI                    % (FOLLOWS TABLE FOR SYSTEM RT-PROGRAMS.)
123024          NBRTP-1*3+ACPOI=:ENDAC          % LAST RECORD (3 WORDS IN ACTAB).
123031   %
123031   %      GIVE VALUES TO THOSE PARTS OF ACCOUNTS RECORD WHICH ARE CONSTANT.
123031   %
123031          1=:BACCO(10)                    % ACCOUNT TYPE = RT ENTRY
123034          0=:BACCO(13)=:BACCO(14)         % TERMINAL TIME USED.
123040          0=:BACCO(15)                    % TERMINAL NUMBER.
123042          GO FILSK
123043   *)FILL
123063   %-------------------------------------------------------------------------------
123063   %            READ ALL RECORDS IN ACTAB AND DETERMINE WHICH RT-PROGRAMS SHOULD BE ACCOUNTED
123063
123063   FILSK: A:=ACPOI                        % INITIALISE LOOP VARIABLE.
123064   MLOOP:
123064   %      FOR ACPOI STEP 3 TO ENDAC DO
123064             IF A >> ENDAC GO FAR SUIT6
123067             A=:X
123070             CALL AR3READ                 % READS TAD FROM TABLE-AREA
123071   %
123071   %         DETERMINE IF THIS PROGRAM SHOULD BE LOGGED. IF T-REG >< 0 IT SHOULD.
123071   %         A RECORD NEED ONLY BE WRITTEN IF AD-REG >< 0 OR ENTRY FROM
123071   %         IOACT >< 0.
123071   %
123071             IF T=0 GO FAR NEXT           % PROGRAM MUST NOT BE ACCOUNTED
123073             T=:BLOC1                     % BLOCK NUMBER IN RTPROJ:DATA FILE
123074             IF A><0 OR D><0 THEN
123077                T:=1
123100             ELSE
123101                T:=0
123102             FI
123102             AD=:DBACC(16)                % CPU TIME USED.
123104             IF IOACTAB><X:=-1 THEN
123110                X:=IOPOI
123111                CALL AR2READ
123112             ELSE
123113                0=:A=:D
123115             FI
123115             IF T=0 AND D=0 AND A=0 GO FAR NEXT
123123             AD=:DBACC(30)                          % NUMBER OF BLOCK I/O TRANSFERS.
123125   %
123125   %         CHECK THAT THERE IS ROOM IN THE ACCOUNTS:DATA FILE BEFORE TRYING TO WRITE THIS RECORD.
123125   %
123125             IF NMBR > RMAX THEN                    % ACCOUNTS:DATA FILE FULL.
123131                244=:ERFL; GO FAR SUIT6
123134   *)FILL
123142             ELSE
123143                IF A > DMAX THEN
123146                   243=:ERFL
123150                FI
123150             FI
123150             "PARA1+ACBASE"; *MON 2RFIL; JAF LERR6    % READ PROJECT NAME.
123153   %
123153   %         BUILD RECORD FOR ACCOUNTS:DATA.
123153   %
123153             FOR X:=0 TO 3 DO; BRTPR(X)=:BACCO(X); OD         % COPY RT-PROGRAM NAME TO ACCOUNTS:DATA RECORD.
123163             A:=#  ;
123164             FOR X:=4 TO 7 DO; A=:BACCO(X); OD                % PAD WITH SPACES
123173
123173   %         PACK AND INSERT TIME OF DUMPING THIS RECORD.
123173
123173             "TPARA+ACBASE"; *MON 2CLOC                       % GET TIME OF DUMP
123175             D:=0; A:=YEAR-3554/\77; AD SHR 4; A+MNTH
123203             AD SHR 5; A+DAY; AD SHR 5
123206             A+HOUR; AD SHR 6; A+MINUT; AD SHR 6; A+SEC:=:D
123214             A=:BACCO(11); A:=D=:BACCO(12)                    % STORE TIME IN ACCOUNTING RECORD.
123221             X:=ACPOI+1
123223             0=:A=:D
123225             CALL AR2WRITE                                    % RESET ENTRY
123226             IF IOACTAB >< -1 THEN
123232                X:=IOPOI
123233                0=:A=:D
123235                CALL AR2WRITE                                 % RESET ENTRY
123236             FI
123236             FOR T:=4 TO A:=13 DO
123242                BRTPR(T); X:=X+14; A=:BACCO(X)                % COPY PROJECT NAME TO ACCOUNTING RECORD.
123246             OD
123250             MIN BLOC2
123251             "PARA2+ACBASE"; *MON 2WFIL; JAF LERR6             % WRITE BLOCK TO ACCOUNTS:DATA
123254             MIN NMBR
123255   NEXT:
123255             IOPOI+2=:IOPOI
123260             ACPOI+3=:ACPOI
123263             0=:BACCO(32)=:BACCO(33)=:BACCO(34)=:BACCO(35)
123273             GO FAR MLOOP
123274   %      OD
123274
123274   *)FILL
123306   %-------------------------------------------------------------------------------
123306   %      WHOLE TABLE EXAMINED AND RECORDS WRITTEN. UPDATE FIRST BLOCK OF ACCOUNTS:DATA
123306   %      AND WRITE IT BACK.
123306   %
123306   LERR6: A=:SAERR                                            % FILESYSTEM ERROR IN LOOP.
123307   SUIT6: NMBR=:BACCO(0); DMAX=:BACCO(1); RMAX=:BACCO(2)
123320          0=:BLOC2; "PARA2+ACBASE"; *MON 2WFIL; JAF CL2FI     % ALL RECORDS JUST WRITTEN LOST IF THIS ERROR OCCURS.
123324          IF A:=SAERR >< 0 GO CL2FI
123326
123326   %      CLOSE FILES AND RELEASE ACCOUNTING SEMAPHORE.
123326
123326   FICL:  T:=FILN2; *MON 2CLOS; JAF CL2FI              % CLOSE FILES
123331          T:=FILN1; *MON 2CLOS; JAF CL1FI
123334          "ACCSEMRE";  *MON 2RELE                      % RELEASE ACCOUNTING SEMAPHORE
123336
123336          IF ERFL >< 0 THEN
123340             *MON 2ERMS                   % ACCOUNTS:DATA FULL OR NEARLY FULL.
123341          FI
123341
123341          *MON 2RTEX                      % EXIT
123342
123342   %-------------------------------------------------------------------------------
123342   %            ERROR EXITS.
123342
123342   INTEGER XERSA:=0
123343   CL2FI: A=:SAERR                        % ERROR WITH 2 FILES OPEN AND SEMAPHORE RESERVED.
123344          T:=FILN2; *MON 2CLOS; JMP *+1   % CLOSE ACCOUNTS:DATA
123347          GO CLOFI
123350   CL1FI: A=:SAERR                        % ERROR WITH 1 FILE OPEN AND SEMAPHORE RESERVED.
123351   CLOFI: T:=FILN1; *MON 2CLOS; JMP *+1   % CLOSE RTPROJ:DATA
123354          GO RESEM
123355   SEMER: A=:SAERR                        % ERROR WITH SEMAPHORE RESERVED.
123356   RESEM: "ACCSEMRE"; *MON 2RELE          % RELEASE SEMAPHORE.
123360          IF SAERR=147 AND XERSA><T THEN  % ONLY ONE ERROR MESSAGE
123367             SAERR=:XERSA;   *MON 2ERMS;  % IF DEVICE UNIT RESERVED FOR SPECIAL USE
123372          FI
123372          *MON 2RTEX
123373
123373   RBUS
123376   *"
"123376   @DEV 1
